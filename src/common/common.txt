{======================================================================
======================================================================}
:GetData // 0@: label, 1@: offset for address
0AC6: 10@ = label 0@ offset
005A: 10@ += 1@  // (int)
0A8D: 11@ = read_memory 10@ size 4 virtual_protect 0
0AB2: ret 1 11@

:StoreData // 0@: label, 1@: value, 2@: offset for address
0AC6: 10@ = label 0@ offset
005A: 10@ += 2@  // (int)
0A8C: write_memory 10@ size 4 value 1@ virtual_protect 0
0AB2: ret 0

{======================================================================
======================================================================}
:GetInt // 0@: label, 1@: offset for address
0AC6: 10@ = label 0@ offset
005A: 10@ += 1@  // (int)
0A8D: 11@ = read_memory 10@ size 1 virtual_protect 0
0AB2: ret 1 11@

:StoreInt // 0@: label, 1@: value, 2@: offset for address
0AC6: 10@ = label 0@ offset
005A: 10@ += 2@  // (int)
0A8C: write_memory 10@ size 1 value 1@ virtual_protect 0
0AB2: ret 0

{======================================================================
======================================================================}
:TeleportTo // pos 0@ 1@ 2@ angle 3@ interior 4@
2@ -= 1.0
04BB: select_interior 4@
0860: link_actor $PLAYER_ACTOR to_interior 4@
04E4: unknown_refresh_game_renderer_at 0@ 1@
03CB: set_rendering_origin_at 0@ 1@ 2@
//04F9: set_interior 1 color_to 0
00A1: put_actor $PLAYER_ACTOR at 0@ 1@ 2@
0173: set_actor $PLAYER_ACTOR Z_angle_to 3@
0373: set_camera_directly_behind_player
//02EB: restore_camera_with_jumpcut
0AB2: ret 0

{======================================================================
======================================================================}
:CreateObject // label 0@ model 1@ position 2@ 3@ 4@ angle 5@ offset 6@
0107: 10@ = create_object 1@ at 2@ 3@ 4@
0453: set_object 10@ XY_rotation 0.0 0.0 angle 5@
0AB1: @StoreData 3 label 0@ value 10@ offset 6@
0AB2: ret 1 object 10@

{======================================================================
======================================================================}
:CreatePickup // label 0@ model 1@ position 2@ 3@ 4@ type 5@ offset 6@
0213: 10@ = create_pickup 1@ type 5@ at 2@ 3@ 4@
0AB1: @StoreData 3 label 0@ value 10@ offset 6@
0AB2: ret 1 pickup 10@

{======================================================================
======================================================================}
:CreateWeaponPickup // label 0@ model 1@ position 2@ 3@ 4@ group 5@ ammo 6@ offset 7@
032B: 10@ = create_weapon_pickup 1@ group 5@ ammo 6@ at 2@ 3@ 4@
0AB1: @StoreData 3 label 0@ value 10@ offset 7@
0AB2: ret 1 pickup 10@

{======================================================================
======================================================================}
:RemoveObjects // label 0@ min_index 1@ max_index 2@
for 10@ = 1@ to 2@
    0085: 11@ = 10@ // (int)
    11@ *= 4
    0AB1: @GetData 2 label 0@ offset 11@ ret_object 12@
    01C4: remove_references_to_object 12@
end
0AB2: ret 0

{======================================================================
======================================================================}
:DestroyPickups // label 0@ min_index 1@ max_index 2@
for 10@ = 1@ to 2@
    0085: 11@ = 10@ // (int)
    11@ *= 4
    0AB1: @GetData 2 label 0@ offset 11@ ret_pickup 12@
    0215: destroy_pickup 12@
end
0AB2: ret 0

{======================================================================
======================================================================}
:CreateVehicle // label 0@ model 1@ position 2@ 3@ 4@ angle 5@ primary_color 6@ secondary_color 7@ offset 8@
10@ = Car.Create(1@, 2@, 3@, 4@)
Car.Angle(10@) = 5@
0229: set_car 10@ primary_color_to 6@ secondary_color_to 7@
0AB1: @StoreData 3 label 0@ value 10@ offset 8@
0AB2: ret 1 vehicle 10@

{======================================================================
======================================================================}
:DestroyVehicle // label 0@ offset 1@
0AB1: @GetData 2 label 0@ offset 1@ ret_vehicle 10@
00A6: destroy_car 10@
0AB1: @StoreData 3 label 0@ value 10@ offset 8@
0AB2: ret 0

{======================================================================
======================================================================}
:CreateActor // label 0@ model 1@ position 2@ 3@ 4@ angle 5@ pedtype 6@ offset 7@ weapon 8@ ammo 9@
10@ = Actor.Create(6@, 1@, 2@, 3@, 4@)
if 8@ > 0
then
    01B2: give_actor 10@ weapon 8@ ammo 9@
    01B9: set_actor 10@ armed_weapon_to 8@
end
Actor.Angle(10@) = 5@
0AB1: @StoreData 3 label 0@ value 10@ offset 7@
0AB2: ret 1 actor 10@

{======================================================================
======================================================================}
:GetDifficultyParameters // difficulty_id 0@
if 0@ == 1  // Agent
then
    3@ = 35
    2@ = 45
    1@ = 45
end
if 0@ == 2  // Special Agent
then
    3@ = 25
    2@ = 60
    1@ = 70
end
if 0@ == 3  // Perfect Agent
then
    3@ = 18
    2@ = 75
    1@ = 100
end
0AB2: ret 3 health 1@ accuracy 2@ sub_machine_ammo 3@

{======================================================================
======================================================================}
:SetActorAsEnemy // label 0@ offset 1@ weapon 2@ difficulty_id 3@
0AB1: @GetData 2 label 0@ offset 1@ ret_actor 10@
0AB1: @GetDifficultyParameters 1 difficulty_id 3@ ret_health 4@ accuracy 5@ sub_machine_ammo 6@
02E2: set_actor 10@ weapon_accuracy_to 5@
0223: set_actor 10@ health_to 4@
if 2@ > 0
then
    01B2: give_actor 10@ weapon 2@ ammo 6@
    01B9: set_actor 10@ armed_weapon_to 2@
end
077A: set_char_relationship 10@ relationship 4 to_ped_type 0
0AB1: @StoreData 3 label 0@ value 10@ offset 1@
0AB2: ret 1 actor 10@

{======================================================================
======================================================================}
:RemoveActors // label 0@ min_index 1@ max_index 2@
for 10@ = 1@ to 2@
    0085: 11@ = 10@ // (int)
    11@ *= 4
    0AB1: @GetData 2 label 0@ offset 11@ ret_actor 12@
    01C2: remove_references_to_actor 12@
    0AB1: @StoreData 3 label 0@ value 12@ offset 11@
end
0AB2: ret 0

{======================================================================
======================================================================}
// Type of Markers
const
    ENTRANCE_MARKER_TYPE     = 0
    MARKER_ABOVE_CAR_TYPE    = 1
    MARKER_ABOVE_ACTOR_TYPE  = 2
    MARKER_ABOVE_OBJECT_TYPE = 3
end

{======================================================================
======================================================================}
:CreateMarker // label 0@ offset 1@ type 2@ target 3@ position 4@ 5@ 6@ color 7@
if 2@ == ENTRANCE_MARKER_TYPE
then 0A40: 10@ = create_entrance_marker_at 4@ 5@ 6@ color 7@
end
if 2@ == MARKER_ABOVE_CAR_TYPE
then 0186: 10@ = create_marker_above_car 3@     // 10@ = Marker.CreateAboveCar(3@)
end
if 2@ == MARKER_ABOVE_ACTOR_TYPE
then 0187: 10@ = create_marker_above_actor 3@   // 10@ = Marker.CreateAboveActor(3@)
end
if 2@ == MARKER_ABOVE_OBJECT_TYPE
then 0188: 10@ = create_marker_above_object 3@  // 10@ = Marker.CreateAboveObject(3@)
end
0AB1: @StoreData 3 label 0@ value 10@ offset 1@
0AB2: ret 1 marker 10@

{======================================================================
======================================================================}
:DestroyMarkers // label 0@ min_index 1@ max_index 2@
for 10@ = 1@ to 2@
    0085: 11@ = 10@ // (int)
    11@ *= 4
    0AB1: @GetData 2 label 0@ offset 11@ ret_marker 20@
    0164: disable_marker 20@
    0A41: destroy_entrance_marker 20@
    0AB1: @StoreData 3 label 0@ value 20@ offset 11@
end
0AB2: ret 0

{======================================================================
======================================================================}
// Controls
const
    // On foot controls
    PAD_ANSWER_PHONE      = 4
    PAD_PREV_WEP_ZOOM_IN  = 5
    PAD_NEXT_WEP_ZOOM_OUT = 7
    PAD_CONVERSATION_YES  = 11
    PAD_ENTER_VEHICLE     = 15
    PAD_SPRINT            = 16
    
    // Vehicle controls
    PAD_RADIO_STATION_UP   = 8
    PAD_RADIO_STATION_DOWN = 9
    
    // Controls for Pause Menu
    KEYCONTROL_PAUSE_MENU = PAD_ANSWER_PHONE
    KEYCONTROL_ACCEPT     = PAD_SPRINT
    KEYCONTROL_CANCEL     = PAD_ENTER_VEHICLE
end

{======================================================================
======================================================================}
:PauseMenu // mission_name_gxt 0@ 1@ objective1_gxt 2@ 3@ objective2_gxt 4@ 5@ objective3_gxt 6@ 7@ objective4_gxt 8@ 9@ objective5_gxt 10@ 11@ objective_flags 12@ 13@ 14@ 15@ 16@
if 00E1:   player 0 pressed_key KEYCONTROL_PAUSE_MENU
then
    015D: set_gamespeed  0.0001 // 0.3 o 1.0 slow motion
    01B4: set_player $PLAYER_CHAR can_move 0 
    03F0: enable_text_draw 1

    repeat
    wait 0
        if 30@ == 0 // Show Mission Status Menu
        then
            038E: draw_box_position 400.0 270.0 size 400.0 220.0 RGBA 123 34 111 75
            038E: draw_box_position 400.0 185.0 size 340.0 1.2 RGBA 255 255 255 60
            038E: draw_box_position 400.0 337.0 size 340.0 1.2 RGBA 255 255 255 60
        
            0AB1: @SetDrawText 8 font 1 linewidth 600.0 position 225.0 170.0 size 0.5 1.2 gxt 0@ 1@ // Mission Name
        
            05AA: 22@s = 'OBTB01' // Abort Mission
            0AB1: @SetDrawText 13 font 1 linewidth 600.0 position 540.0 340.0 size 0.4 1.0 gxt 22@ 23@ use_number 0 number 0 center 0 center_linewidth 0.0 align_right 1
        
            0007: 18@ = 200.0
            0087: 19@ = 18@ // (float)
            000B: 19@ += 10.0
            
            for 20@ = 0 to 4
                0085: 21@ = 20@ // (int)
                000A: 21@ += 1
                
                if 85AD:   not 2@(20@,5s) == 'DUMMY'
                then
                    05AA: 22@s = 'OBJ_X' // Objective ~1~
                    0AB1: @SetDrawText 10 font 1 linewidth 600.0 position 225.0 18@ size 0.4 1.0 gxt 22@ 23@ use_number 1 number 21@
                    
                    05AA: 22@s = 2@(20@,5s) // (Objective X Entry)
                    0AB1: @SetDrawText 8 font 1 linewidth 600.0 position 315.0 18@ size 0.4 1.0 gxt 22@ 23@
                    
                    // Objective ~1~ Status (Complete / Incomplete)
                    if 0038: 12@(20@,5i) == 0 // Is objective incomplete ?
                    then
                    05AA: 22@s = 'OBJINC' // @s = 'short'
                    0AB1: @SetDrawText 24 font 1 linewidth 600.0 position 540.0 19@ size 0.4 1.0 gxt 22@ 23@ use_number 0 number 0 center 0 center_linewidth 0.0 align_right 1 enable_outline 0 outline 0 outline_rgba 0 0 0 0 customize_rgba 1 rgba 250 164 96 255
                    else
                    05AA: 22@s = 'OBJCMP' // @s = 'short'
                    0AB1: @SetDrawText 24 font 1 linewidth 600.0 position 540.0 19@ size 0.4 1.0 gxt 22@ 23@ use_number 0 number 0 center 0 center_linewidth 0.0 align_right 1 enable_outline 0 outline 0 outline_rgba 0 0 0 0 customize_rgba 1 rgba 135 206 235 255
                    end
                end
                
                18@ += 25.0 
                19@ += 25.0 
            end
            
            if 00E1:   player 0 pressed_key KEYCONTROL_ACCEPT
            then 30@ = 1
            end
        end
        
        if 30@ == 1
        then
            if 80E1:   not player 0 pressed_key KEYCONTROL_ACCEPT
            then 30@ = 2
            end
        end
        
        if 30@ == 2 // Abort Mission Confirm Menu
        then
            038E: draw_box_position 315.0 209.0 size 304.0 89.0 RGBA 123 34 111 75
            038E: draw_box_position 315.0 181.0 size 298.0 2.0 RGBA 255 255 255 60
            038E: draw_box_position 315.0 233.0 size 298.0 2.0 RGBA 255 255 255 60
            
            05AA: 22@s = 'OBTB02' // Abort Mission
            0AB1: @SetDrawText 8 font 1 linewidth 600.0 position 174.0 166.0 size 0.4 1.2 gxt 22@ 23@
            
            05AA: 22@s = 'OBTB03' // Are you sure you want to abort the mission?
            0AB1: @SetDrawText 8 font 1 linewidth 390.0 position 173.0 193.0 size 0.3 1.1 gxt 22@ 23@
            
            05AA: 22@s = 'OBTB04' // SPACE: accept / F: cancel
            0AB1: @SetDrawText 8 font 2 linewidth 600.0 position 334.0 235.0 size 0.2 1.0 gxt 22@ 23@
            
            if 00E1:   player 0 pressed_key KEYCONTROL_ACCEPT
            then
                015D: set_gamespeed  1.0 // 0.3 o 1.0 slow motion
                01B4: set_player $PLAYER_CHAR can_move 1
                03F0: enable_text_draw 0
                0AB2: ret 1 abort_flag 1
            end
            
            if 00E1:   player 0 pressed_key KEYCONTROL_CANCEL
            then 30@ = 0
            end
        end
            
        if 80E1:   not player 0 pressed_key KEYCONTROL_PAUSE_MENU
        then
            015D: set_gamespeed  1.0 // 0.3 o 1.0 slow motion
            01B4: set_player $PLAYER_CHAR can_move 1
            03F0: enable_text_draw 0
            break
        end
    until false
end
0AB2: ret 1 abort_flag 0

{======================================================================
======================================================================}
:SetDrawText // font 0@ linewidth 1@ position 2@ 3@ size 4@ 5@ gxt 6@ 7@ use_number 8@ number 9@ center 10@ center_linewidth 11@ align_right 12@ enable_outline 13@ outline 14@ outline_rgba 15@ 16@ 17@ 18@ customize_rgba 19@ rgba 20@ 21@ 22@ 23@ customize_shadow 24@ shadow_size 25@ shadow_rgba 26@ 27@ 28@ 29@ background 30@
0345: enable_text_draw_background 30@
if 19@ == 1
then 0340: set_text_draw_RGBA 20@ 21@ 22@ 23@
else 0340: set_text_draw_RGBA 255 255 255 255
end
0349: set_text_draw_font 0@
if 10@ == 1
then
    0342: set_text_draw_centered 1
    0344: set_text_draw_linewidth 11@ for_centered_text
else 0343: set_text_draw_linewidth 1@
end
033F: set_text_draw_letter_size 4@ 5@
03E4: set_text_draw_align_right 12@
if 13@ == 1
then 081C: draw_text_outline 14@ RGBA 15@ 16@ 17@ 18@
else 081C: draw_text_outline 0 RGBA 0 0 0 255
end
if 24@ == 1
then 060D: draw_text_shadow 25@ rgba 26@ 27@ 28@ 29@
else 060D: draw_text_shadow 2 rgba 0 0 0 255
end
if 8@ == 1
then 045A: draw_text_1number 2@ 3@ GXT 6@s number 9@
else 033E: set_draw_text_position 2@ 3@ GXT 6@s
end
0AB2: ret 0

{======================================================================
======================================================================}
:ForceRadioOff // entries: none
if 00DF:   actor $PLAYER_ACTOR driving
then
    051E: 12@ = get_current_radio_station
    000E: 12@ -= 1
    
    if not 12@ == 11 // Radio off
    then 041E: set_radio_station 11 // force radio off
    end
    
    if 00E1:   player 0 pressed_key PAD_RADIO_STATION_UP
    then
        041E: set_radio_station 11 // force radio off
        repeat
        wait 0
        until 80E1:   not player 0 pressed_key PAD_RADIO_STATION_UP
        041E: set_radio_station 11 // force radio off
    end
    
    if 00E1:   player 0 pressed_key PAD_RADIO_STATION_DOWN
    then
        041E: set_radio_station 11 // force radio off
        repeat
        wait 0
        until 80E1:   not player 0 pressed_key PAD_RADIO_STATION_DOWN
        041E: set_radio_station 11 // force radio off
    end
end
0AB2: ret 0

{======================================================================
======================================================================}
:GetObjectivesStatus // label 0@
0AB1: @GetInt 2 label 0@ offset 0 ret 11@
0AB1: @GetInt 2 label 0@ offset 1 ret 12@
0AB1: @GetInt 2 label 0@ offset 2 ret 13@
0AB1: @GetInt 2 label 0@ offset 3 ret 14@
0AB1: @GetInt 2 label 0@ offset 4 ret 15@
0AB2: ret 5 obj1_status 11@ obj2_status 12@ obj3_status 13@ obj4_status 14@ obj5_status 15@

{======================================================================
======================================================================}
:JoannaResponse
0209: 10@ = random_int_in_ranges 0 3
if 10@ == 0
then 00BC: show_text_highpriority GXT 'MJO_1' time 4000 flag 1  // (Joanna) Received.
end
if 10@ == 1
then 00BC: show_text_highpriority GXT 'MJO_2' time 4000 flag 1  // (Joanna) Understood.
end
if 10@ == 2
then 00BC: show_text_highpriority GXT 'MJO_3' time 4000 flag 1  // (Joanna) Confirmed.
end
if 10@ == 3
then 00BC: show_text_highpriority GXT 'MJO_4' time 4000 flag 1  // (Joanna) Affirmative.
end
0AB2: ret 0
