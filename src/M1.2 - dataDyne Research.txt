DEFINE MISSIONS 2
DEFINE MISSION 0 AT @INITIAL
DEFINE MISSION 1 AT @RESEARCH


DEFINE EXTERNAL_SCRIPTS -1 // Use -1 in order not to compile AAA script
//DEFINE SCRIPT {NAME} AT {LABEL} @

DEFINE UNKNOWN_EMPTY_SEGMENT 0

DEFINE UNKNOWN_THREADS_MEMORY 0

{$VERSION 3.0.0000}
var
$PLAYER_CHAR: Player
end // var
03A4: name_thread 'MAIN'
01F0: set_max_wanted_level_to 6
0111: toggle_wasted_busted_check 0
00C0: set_current_time_hours_to 8 minutes_to 0
04E4: unknown_refresh_game_renderer_at 1049.9067 1031.3267
03CB: set_rendering_origin_at 1049.9067 1031.3267 10.685
0053: $PLAYER_CHAR = create_player #NULL at 1049.9067 1031.3267 10.685 //1453.0439 -1841.0349 13.5469
01F5: $PLAYER_ACTOR = create_player_actor $PLAYER_CHAR
07AF: $PLAYER_GROUP = player $PLAYER_CHAR group
0373: set_camera_directly_behind_player
01B6: set_weather 0
0001: wait 0 ms
087B: set_player $PLAYER_CHAR clothes_texture "PLAYER_FACE" model "HEAD" body_part 1
087B: set_player $PLAYER_CHAR clothes_texture "JEANSDENIM" model "JEANS" body_part 2
087B: set_player $PLAYER_CHAR clothes_texture "SNEAKERBINCBLK" model "SNEAKER" body_part 3
087B: set_player $PLAYER_CHAR clothes_texture "VEST" model "VEST" body_part 0
070D: rebuild_player $PLAYER_CHAR
01B4: toggle_player $PLAYER_CHAR can_move 0
016A: fade 0 time 0
04BB: select_interior 0
0629: change_integer_stat 181 to 4
016C: restart_if_wasted_at 2027.77 -1420.52 15.99 angle 137.0 town_number 0
016D: restart_if_busted_at 1550.68 -1675.49 14.51 angle 90.0 town_number 0
0180: set_on_mission_flag_to $ONMISSION // Note: your missions have to use the variable defined here
0004: $DEFAULT_WAIT_TIME = 250
03E6: remove_text_box

023C: load_special_actor 'reimu' as 1
038B: load_requested_models
repeat
wait 0 ms
until 023D:   special_actor 1 loaded
09C7: change_player $PLAYER_CHAR model_to #SPECIAL01
0296: unload_special_actor 1

wait 1000 ms
016A: fade 1 time 2000
01B4: toggle_player $PLAYER_CHAR can_move 1
0417: start_mission 0 // Initial
wait 0
// put your create_thread commands here
00D7: create_thread @CI_MISSIONS_SNIFFER

:MAIN_LOOP
0001: wait $DEFAULT_WAIT_TIME ms
0002: jump @MAIN_LOOP




{==============================================================
GLOBAL SUBROUTINES
===============================================================}
:SUB_FADE_500MS
0169: set_fade_color_RGB 0 0 0 
016A: fade 0 time 500 

:LITCAS_282
00D6: if 
016B:   fading 
004D: jump_if_false @LITCAS_306 
0001: wait 0 ms 
0002: jump @LITCAS_282 

:LITCAS_306
0051: return 
//------------------------
:JOANNA_RESPONSE
0209: $RAND = random_int_in_ranges 0 3
00D6: if
0039:   $RAND == 0
004D: jump_if_false @J01 
00BC: show_text_highpriority GXT 'MJO_1' time 4000 flag 1  // (Joanna) Received.
:J01
00D6: if
0039:   $RAND == 1
004D: jump_if_false @J02 
00BC: show_text_highpriority GXT 'MJO_2' time 4000 flag 1  // (Joanna) Understood.
:J02
00D6: if
0039:   $RAND == 2
004D: jump_if_false @J03 
00BC: show_text_highpriority GXT 'MJO_3' time 4000 flag 1  // (Joanna) Confirmed.
:J03
00D6: if
0039:   $RAND == 3
004D: jump_if_false @J04 
00BC: show_text_highpriority GXT 'MJO_4' time 4000 flag 1  // (Joanna) Affirmative.
:J04
0051: return  
//------------------------
:SELECT_DIFFICULTY
01B4: set_player $PLAYER_CHAR can_move 0
0512:  show_permanent_text_box 'MHLP01'
08D4: $SEL_DIF_MENU = create_panel_with_title 'MTBL01' position 29.0 145.0 width {220.0} 300.0 columns 1 interactive 1 background 1 alignment 1
08DB:  set_panel $SEL_DIF_MENU column 0 header s$MISSION data 'MDIF1' 'MDIF2' 'MDIF3' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY'// 'M3_2' 'M4_1' 'M4_2' 'M4_3' 'M5_1' 'M5_2'
repeat
wait 0 ms
until 00E1: player 0 pressed_key 16 
08D7: $choice = panel $SEL_DIF_MENU active_row
08DA: remove_panel $SEL_DIF_MENU
03E6: remove text box
if $choice == 0
then
$MISSION_DIFFICULTY = 1          // Agent
end
if $choice == 1
then
$MISSION_DIFFICULTY = 2          // Special Agent
end
if $choice == 2
then
$MISSION_DIFFICULTY = 3          // Perfect Agent
end
0051: return  
//------------------------
:MISSION_BAR
if 00E1:   player 0 pressed_key 11    //~k~~CONVERSATION_YES~
then
    015D: set_gamespeed  0.001 // 0.3 o 1.0 slow motion
    01B4: set_player $PLAYER_CHAR can_move 0 

    038E: draw_box_position 400.0 270.0 size 400.0 220.0 RGBA 123 34 111 75
    038E: draw_box_position 400.0 185.0 size 340.0 1.2 RGBA 255 255 255 60

    033F: set_text_draw_letter_size 0.5 1.2
    0343: set_text_draw_linewidth 600.0 
    033E: set_draw_text_position 225.0 170.0 GXT s$MISSION    // Mission Name

    if 85AD:   not s$OBJ1 == 'DUMMY'
    then
        // Objective 1
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        033E: set_draw_text_position 225.0 200.0 GXT 'OBJ1'
        
        // Objective 1 Entry
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        033E: set_draw_text_position 315.0 200.0 GXT s$OBJ1
        
        // Objective 1 Status (Complete / Incomplete)
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        03E4: enable_text_draw_align_right 1
        if 0038: $MOBJ1 == 0
        then
        0340: set_text_draw_RGBA 250 164 96 255 
        033E: set_draw_text_position 540.0 210.0 GXT 'OBJINC'
        else
        0340: set_text_draw_RGBA 135 206 235 255 
        033E: set_draw_text_position 540.0 210.0 GXT 'OBJCMP' 
        end
    end 

    if 85AD:   not s$OBJ2 == 'DUMMY'
    then
        // Objective 2
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        033E: set_draw_text_position 225.0 225.0 GXT 'OBJ2'
        
        // Objective 2 Entry
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        033E: set_draw_text_position 315.0 225.0 GXT s$OBJ2
        
        // Objective 2 Status (Complete / Incomplete)
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        03E4: enable_text_draw_align_right 1
        if 0038: $MOBJ2 == 0
        then
        0340: set_text_draw_RGBA 250 164 96 255 
        033E: set_draw_text_position 540.0 235.0 GXT 'OBJINC'
        else
        0340: set_text_draw_RGBA 135 206 235 255 
        033E: set_draw_text_position 540.0 235.0 GXT 'OBJCMP' 
        end
    end 

    if 85AD:   not s$OBJ3 == 'DUMMY'
    then
        // Objective 3
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        033E: set_draw_text_position 225.0 250.0 GXT 'OBJ3'
        
        // Objective 3 Entry
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        033E: set_draw_text_position 315.0 250.0 GXT s$OBJ3
        
        // Objective 3 Status (Complete / Incomplete)
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        03E4: enable_text_draw_align_right 1
        if 0038: $MOBJ3 == 0
        then
        0340: set_text_draw_RGBA 250 164 96 255 
        033E: set_draw_text_position 540.0 260.0 GXT 'OBJINC'
        else
        0340: set_text_draw_RGBA 135 206 235 255 
        033E: set_draw_text_position 540.0 260.0 GXT 'OBJCMP' 
        end
    end 

    if 85AD:   not s$OBJ4 == 'DUMMY'
    then
        // Objective 4
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        033E: set_draw_text_position 225.0 275.0 GXT 'OBJ4'
        
        // Objective 4 Entry
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        033E: set_draw_text_position 315.0 275.0 GXT s$OBJ4
        
        // Objective 4 Status (Complete / Incomplete)
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        03E4: enable_text_draw_align_right 1
        if 0038: $MOBJ4 == 0
        then
        0340: set_text_draw_RGBA 250 164 96 255 
        033E: set_draw_text_position 540.0 285.0 GXT 'OBJINC'
        else
        0340: set_text_draw_RGBA 135 206 235 255 
        033E: set_draw_text_position 540.0 285.0 GXT 'OBJCMP' 
        end
    end 

    if 85AD:   not s$OBJ5 == 'DUMMY'
    then
        // Objective 5
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        033E: set_draw_text_position 225.0 300.0 GXT 'OBJ5'
        
        // Objective 5 Entry
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        033E: set_draw_text_position 315.0 300.0 GXT s$OBJ5
        
        // Objective 5 Status (Complete / Incomplete)
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        03E4: enable_text_draw_align_right 1
        if 0038: $MOBJ5 == 0
        then
        0340: set_text_draw_RGBA 250 164 96 255 
        033E: set_draw_text_position 540.0 310.0 GXT 'OBJINC'
        else
        0340: set_text_draw_RGBA 135 206 235 255 
        033E: set_draw_text_position 540.0 310.0 GXT 'OBJCMP' 
        end
    end 

    038E: draw_box_position 400.0 337.0 size 340.0 1.2 RGBA 255 255 255 60

    033F: set_text_draw_letter_size 0.4 1.0
    0343: set_text_draw_linewidth 600.0 
    03E4: enable_text_draw_align_right 1
    033E: set_draw_text_position 540.0 340.0 GXT 'OBTB01'     // Abort Mission

    repeat
    wait 0 ms
    until 80E1: not player 0 pressed_key 11
        
    repeat
    wait 0 ms
    until 00E1:   player 0 pressed_key 11

    015D: set_gamespeed  1.0 // 0.3 o 1.0 slow motion
    01B4: set_player $PLAYER_CHAR can_move 1
    03F0: enable_text_draw 0 

    repeat
    wait 0 ms
    until 80E1: not player 0 pressed_key 11
end
0051: return  






// put your mods (threads) here
:CI_MISSIONS_SNIFFER
thread "CI_SNIFF"
$MARKER_CARRINGTON_INSTITUTE = Marker.CreateIconAndSphere(42, $X_CARRINGTON_INSTITUTE, $Y_CARRINGTON_INSTITUTE, $Z_CARRINGTON_INSTITUTE)
repeat
wait $DEFAULT_WAIT_TIME
if and
player.Defined($PLAYER_CHAR)
$ONMISSION == 0
then
    if and
        00FF: actor $PLAYER_ACTOR sphere 0 in_sphere $X_CARRINGTON_INSTITUTE $Y_CARRINGTON_INSTITUTE $Z_CARRINGTON_INSTITUTE radius 1.0 1.0 2.0 on_foot
        Player.Controllable($PLAYER_CHAR)
        $TUT_MISSIONS_PASSED == 0
    then
        054C: use_GXT_table 'PD'
        $ONMISSION = 1
        s$MISSION = 'M1_2' // dataDyne Research: Investigation
        0050: gosub @SELECT_DIFFICULTY
        00BA: show_text_styled GXT 'M1_2' time 1000 style 2 // dataDyne Research: Investigation
        01B4: toggle_player $PLAYER_CHAR can_move 0
        0050: gosub @SUB_FADE_500MS 
        start_mission 1
    end
end
until $TUT_MISSIONS_PASSED == 1
marker.Disable($MARKER_CARRINGTON_INSTITUTE)
end_thread

//––––-Mission 0–––––
:INITIAL
$TUT_MISSIONS_PASSED = 0
$ONMISSION = 0
     
$X_CARRINGTON_INSTITUTE = 1046.3639
$Y_CARRINGTON_INSTITUTE = 1015.7791
$Z_CARRINGTON_INSTITUTE = 11.0
//0629: change_integer_stat 225 to 999 // capacidad pulmonar al máximo jolines tío no la empalmeis hostia!

model.Load(#FCR900)
038B: load_requested_models

:INITIAL_LOAD
wait 0 ms
if
model.Available(#FCR900)
004D: jump_if_false @INITIAL_LOAD

0674: set_car_model #FCR900 numberplate "DARK_"
$darkbike = car.Create(#FCR900,1452.9561, -1837.4128, 13.5469)
end_thread






//––––-Mission 1–––––
{==============================================================
DATADYNE RESEARCH: INVESTIGATION
===============================================================}
// Mission wrapper
:RESEARCH
thread 'M1_2'
gosub @RESEARCH_MAIN
if wasted_or_busted
then
gosub @RESEARCH_FAIL
end
gosub @RESEARCH_CLEANUP
end_thread

//––––––––––––-
:RESEARCH_MAIN
const         
{==============================================================================
Nomenclatura propuesta:     0-99: actors,vehicles 
                            100-199: flags y variables numéricas de uso general
                            200-299: objects or pickups
                            300-399: markers/spheres/checkpoints
                            400-499: decision makers, rutas y comportamientos
===============================================================================     }
    // 100@: variable para verificar la cantidad de "ammo" de la cámara fotográfica
    // 101@: contador de isótopos fotografiados
    // 304@ - 305@: markers para los isotopos
    // ======= Obj2
    // 300@ - 301@: spheres para activar los ciclos de mantenimiento de los bots
    // 302@ - 303@: checkpoints para las spheres
    // ======= Obj3
    Scientist1 = 0@
    Scientist2 = 1@
    Scientist3 = 2@      
    // 110@: contador de experimentos apagados
    // 111@: estado para máquina de científico 1
    // 112@: estado para máquina de científico 2
    // 113@: estado para máquina de científico 3
    // 310@ - 311@ - 312@: markers para los científicos
    // 313@: sphere para apagar experimento
    // 314@: checkpoint para la sphere
    // ======= Obj4
    // 220@ - 221@ - 222@: night vision, shield, k7 avenger
    // 120@: contador de experimental technologies tomadas
    // 124@: estado de máquina
    // 320@ - 321@ - 322@ - 323@: Markers
    // 121@ - 122@ - 123@: posiciones x-y-z para actor dDTrooper
    dDScientist = 4@
    dDTrooper = 5@
    // ======= Obj5
    DrCaroll = 6@
    // 340@: Marker para Dr Caroll
    // 341@: Checkpoint para la salida
    // =======
    // 130@: ángulo del actor jugador
    // 330@: marker para la computadora que abre la puerta del último sector
    // 400@: comportamiento nulo para actores
    BuildingDoor = 302@
end                                                                                                                                                            // ======= Obj1

// Initialize Mission Time
00C0: set_current_time_hours_to 0 minutes_to 0

// Initialize global variables for Mission Status Bar
if 0038:   $MISSION_DIFFICULTY == 1  // Agent
then s$OBJ1='M12OB1'
s$OBJ2='M12OB5'
s$OBJ3='DUMMY'
s$OBJ4='DUMMY'
s$OBJ5='DUMMY'
end
if 0038:   $MISSION_DIFFICULTY == 2  // Special Agent
then s$OBJ1='M12OB1'
s$OBJ2='M12OB2'
s$OBJ3='M12OB3'
s$OBJ4='M12OB5'
s$OBJ5='DUMMY'
end
if 0038:   $MISSION_DIFFICULTY == 3  // Perfect Agent
then s$OBJ1='M12OB1'
s$OBJ2='M12OB2'
s$OBJ3='M12OB3'
s$OBJ4='M12OB4'
s$OBJ5='M12OB5'
end

// Spawn actor
04BB: select_interior 2 
0860: link_actor $PLAYER_ACTOR to_interior 2 
04E4: unknown_refresh_game_renderer_at 2579.7454 -1299.9825 
03CB: set_rendering_origin_at 2579.7454 -1299.9825 1060.9922 
Actor.PutAt($PLAYER_ACTOR, 2579.7454, -1299.9825, 1060.9922)
Actor.Angle($PLAYER_ACTOR) = 0.0 
Camera.SetBehindPlayer()  
//Camera.Restore_WithJumpCut() 

// Spawn Objects
029B: 200@ = init_object #lift_dr at 2577.62 -1299.82 1061.14 
029B: 201@ = init_object #lift_dr at 2577.62 -1301.02 1061.14 
0453: set_object 200@ XY_rotation 180.0 180.0 angle 0.0//46.0
0453: set_object 201@ XY_rotation 0.0 0.0 angle 316.0//406.0
0177: set_object 200@ Z_angle_to 226.0 
0177: set_object 201@ Z_angle_to 406.0 
0107: 202@ = create_object #green_gloop at 2561.0 -1304.59 1054.27
0453: set_object 202@ XY_rotation 0.0 0.0 angle 0.0
0107: 203@ = create_object #green_gloop at 2561.0 -1283.09 1054.27
0453: set_object 203@ XY_rotation 0.0 0.0 angle 0.0
0188: 304@ = create_marker_above_object 202@ 
0188: 305@ = create_marker_above_object 203@
0165: set_marker 304@ color_to 0 
0165: set_marker 305@ color_to 0 
029B: 204@ = init_object #OTB_machine at 2566.35 -1304.14 1031.35
0453: set_object 204@ XY_rotation 0.0 0.0 angle 180.0
029B: 205@ = create_object #a51_labdoor at 2565.68 -1301.68 1030.26//205@ = init_object #a51_labdoor at 2565.68 -1301.68 1030.26 
0453: set_object 205@ XY_rotation 0.0 0.0 angle 90.0
0392: object 205@ toggle_in_moving_list 1 // Make the object #a51_labdoor movable.
029B: 206@ = init_object #CJ_WIN_POP2 at 2561.81 -1279.64 1032.12
0453: set_object 206@ XY_rotation 0.0 0.0 angle 0.0
029B: 207@ = init_object #nightclub02_lvs at 2519.08 -1306.12 1032.11
0453: set_object 207@ XY_rotation 180.0 180.0 angle 0.0


//Load models
Model.Load(#colt45)
Model.Load(#camera)
Model.Load(#micro_uzi)
Model.Load(#lvemt1)
Model.Load(#heck1)
Model.Load(#maverick)
038B: load_requested_models                                             

:RESEARCH_LOAD
00D6: if or
8248:   not model #colt45 available 
8248:   not model #camera available 
8248:   not model #micro_uzi available 
8248:   not model #lvemt1 available 
8248:   not model #heck1 available 
8248:   not model #maverick available 
004D: jump_if_false @RESEARCH_LOADED 
0001: wait 0 ms 
0002: jump @RESEARCH_LOAD 

:RESEARCH_LOADED
01B2: give_actor $PLAYER_ACTOR weapon 22 ammo 35
01B2: give_actor $PLAYER_ACTOR weapon 43 ammo 10
01B9: set_actor $PLAYER_ACTOR armed_weapon_to 22
0001: wait 1000 ms 
016A: fade 1 time 500
Player.CanMove($PLAYER_CHAR) = true                                         //01B4: toggle_player $PLAYER_CHAR can_move 1
0249: release_model #colt45
0249: release_model #camera
0249: release_model #micro_uzi
0249: release_model #lvemt1
0249: release_model #heck1
0249: release_model #maverick
0950: set_trip_skip 2575.6697 -1289.6407 1044.125 angle 47.416 // versionA 

// Initializing
060A: create_decision_maker_type 0 store_to 400@    // clean behavior for special actors
$MOBJ1=0
$MOBJ2=0
$MOBJ3=0
$MOBJ4=0
$MOBJ5=0
$OBJ1_STATUS=0
$OBJ2_STATUS=0
$OBJ3_STATUS=0
$OBJ4_STATUS=0
$OBJ5_STATUS=0
$AREA_STATUS=0
$OTHER_TASKS_STATUS=0
$ALLOBJECTIVES=0
33@=15000
0A40: BuildingDoor = create_entrance_marker_at 2577.9775 -1300.4467 1062.0 color 14 

// Wanted level of the mission
010D: set_player $PLAYER_CHAR wanted_level_to 0 
01F0: set_max_wanted_level_to 0 

{==============================================================
EL LAZO WHILE PRINCIPAL
===============================================================}
while   0039: $ALLOBJECTIVES == 0
wait 0 ms
    {CREACION DE ENEMIGOS DEL NIVEL}
    {00D6: if or 
    8039: not Floor0EnemysCreated == 0
    80FE: not actor $PLAYER_ACTOR 0 near_point_3d 372.3281 153.5288 1010.1875 radius 2.0 2.0 2.0 
    0AA0: gosub_if_false @DEFECTION_FLOOR0_ENEMIES         }

    {00D6: if or 
    8039: not BasementEnemysCreated == 0
    80FE: not actor $PLAYER_ACTOR 0 near_point_3d 370.3462 181.3226 1008.3828 radius 2.0 2.0 2.0 
    0AA0: gosub_if_false @DEFECTION_BASEMENT_ENEMIES       }

    {AGENT}    
    if 0039:   $MOBJ1 == 0
    then 0050: gosub @M1_2_OBJECTIVE1  // Holograph radioactive isotope
    end

    {SPECIAL AGENT Y PERFECT AGENT}
    if
    8039: not $MISSION_DIFFICULTY == 1 // Agent    
    then        
        if 0039:   $MOBJ2 == 0 
        then 0050: gosub @M1_2_OBJECTIVE2  // Start security maintenance cycle
        end
        
        if 0039: $MOBJ3 == 0 
        then 0050: gosub @M1_2_OBJECTIVE3  // Shut down experiments           
        end
        
        if and
        0039: $MISSION_DIFFICULTY == 3 // Perfect Agent
        0039: $MOBJ4 == 0
        then 0050: gosub @M1_2_OBJECTIVE4  // Obtain experimental technologies
        end           
    end
    
    {CONDICIÓN DE VICTORIA}
    if
    00FE:   actor $PLAYER_ACTOR 0 near_point_3d 2528.1394 -1293.7936 1031.4219 radius 1.0 1.0 1.0
    then
        if and
        0039: $MISSION_DIFFICULTY == 1  // Agent
        0039: $MOBJ1 == 1   // Holograph radioactive isotope
        then
        0050: gosub @RESEARCH_OUTRO // cutscene
        $ALLOBJECTIVES = 1
        end
        if and
        0039: $MISSION_DIFFICULTY == 2  // Special Agent
        0039: $MOBJ1 == 1   // Holograph radioactive isotope
        0039: $MOBJ2 == 1   // Start security maintenance cycle
        0039: $MOBJ3 == 1   // Shut down experiments
        then
        0050: gosub @RESEARCH_OUTRO // cutscene
        $ALLOBJECTIVES = 1
        end
        if and
        0039: $MISSION_DIFFICULTY == 3  // Perfect Agent
        0039: $MOBJ1 == 1   // Holograph radioactive isotope
        0039: $MOBJ2 == 1   // Start security maintenance cycle
        0039: $MOBJ3 == 1   // Shut down experiments
        0039: $MOBJ4 == 1   // Obtain experimental technologies
        then
        $MOBJ5=1
        0050: gosub @RESEARCH_OUTRO // cutscene
        end
    end
    if and
    00FE:   actor $PLAYER_ACTOR 0 near_point_3d 2578.4309 -1300.4418 1060.9922 radius 1.0 1.0 1.0
    0039: $MISSION_DIFFICULTY == 3  // Perfect Agent
    0039: $MOBJ1 == 1   // Holograph radioactive isotope
    0039: $MOBJ2 == 1   // Start security maintenance cycle
    0039: $MOBJ3 == 1   // Shut down experiments
    0039: $MOBJ4 == 1   // Obtain experimental technologies
    then
        if
        0104:   actor $PLAYER_ACTOR near_actor DrCaroll radius 6.0 6.0 6.0 sphere 0
        then
        $ALLOBJECTIVES = 1
        else
        00BC: show_text_highpriority GXT 'M12_051' time 1000 flag 1  // You cannot leave without ~b~Dr. Caroll~w~.
        end
    end

    {ATENDER OTRAS TAREAS INDEPENDIENTES DE LOS OBJETIVOS}
    0050: gosub @M1_2_OTHER_TASKS

    {CONDICIÓN DE FRACASO}    
    00D6: if 
    8039: not $OTHER_TASKS_STATUS == 99
    004D: jump_if_false @RESEARCH_FAIL 

    {DESPLEGAR BARRA DE STATUS DE MISIÓN TRAS PULSACIÓN DE TECLA ESPECIAL -OBJETIVOS COMPLETADOS Y OBJETIVOS FALTANTES-}
    0050: gosub @MISSION_BAR
end
0002: jump @RESEARCH_PASSED 

{=================================================================
OTHER TASKS
==================================================================}
:M1_2_OTHER_TASKS

041A: 100@ = actor $PLAYER_ACTOR weapon 43 ammo
if and
0039: 100@ == 0
0039: $MOBJ1 == 0   // Holograph radioactive isotope
then
    00BC: show_text_highpriority GXT 'M12_046' time 4000 flag 1  // Radioactive isotope is not going to be photographed.
    $OTHER_TASKS_STATUS = 99         // jump to fail mission routine
end

if
0018:   $OBJ3_STATUS > 0 
then
    if or
    0118:   actor Scientist1 dead 
    0118:   actor Scientist2 dead 
    0118:   actor Scientist3 dead
    then
        00BC: show_text_highpriority GXT 'M11_050' time 4000 flag 1  // Critical mission personnel killed.
        $OTHER_TASKS_STATUS = 99         // jump to fail mission routine
    end
end

if or
0039: $OBJ4_STATUS == 1
0039: $OBJ4_STATUS == 2
then
    if
    0118:   actor dDScientist dead
    then
    00BC: show_text_highpriority GXT 'M11_050' time 4000 flag 1  // Critical mission personnel killed.
    $OTHER_TASKS_STATUS = 99         // jump to fail mission routine
    end     
end

if
0039: $OTHER_TASKS_STATUS == 3
then
    if
    0118:   actor DrCaroll dead
    then
    00BC: show_text_highpriority GXT 'M11_050' time 4000 flag 1  // Critical mission personnel killed.
    $OTHER_TASKS_STATUS = 99         // jump to fail mission routine
    end     
end

if
00FE: actor $PLAYER_ACTOR 0 near_point_3d 2578.4309 -1300.4418 1060.9922 radius 1.0 1.0 1.0
then
    if
    0039: $MOBJ1 == 0   // Holograph radioactive isotope
    then
    00BC: show_text_highpriority GXT 'M12_051' time 1000 flag 1  // You cannot leave without ~b~Dr. Caroll~w~.
    end
end

if and
0039: $OTHER_TASKS_STATUS == 0
00FE: actor $PLAYER_ACTOR 0 near_point_3d 2568.9468 -1295.9443 1031.4219 radius 3.0 3.0 3.0
then
    00BC: show_text_highpriority GXT 'M12_027' time 4000 flag 1  // (Carrington) That's the highest security sector - Dr Caroll has to be nearby.
    wait 4000 ms
    0188: 330@ = create_marker_above_object 204@ 
    0165: set_marker 330@ color_to 1
    $OTHER_TASKS_STATUS = 1
    0050: gosub @JOANNA_RESPONSE    // (Joanna) Received.
end

if and
0039: $OTHER_TASKS_STATUS == 1
00FE: actor $PLAYER_ACTOR 0 near_point_3d 2567.1479 -1304.1865 1031.4219 radius 1.0 1.0 1.0
then
    if
    00E1:   player 0 pressed_key 15   // ~k~~VEHICLE_ENTER_EXIT~
    then
        if or
        0039: $MOBJ2 == 1
        0039: $MISSION_DIFFICULTY == 1 // Agent
        then
            00BC: show_text_highpriority GXT 'M12_038' time 4000 flag 1  // Connection established.
            wait 4000 ms
            00BC: show_text_highpriority GXT 'M12_039' time 4000 flag 1  // Searching for password.
            wait 7000 ms
            0172: 130@ = actor $PLAYER_ACTOR Z_angle
            if or
            8031: not 130@ >= 75.9
            8033: not 105.0 >= 130@
            80FE: not actor $PLAYER_ACTOR 0 near_point_3d 2567.1479 -1304.1865 1031.4219 radius 1.0 1.0 1.0
            then
                00BC: show_text_highpriority GXT 'M12_040' time 4000 flag 1  // Contact broken - reestablish link.
                wait 4000 ms
                00BC: show_text_highpriority GXT 'M12_041' time 4000 flag 1  // ACCESS DENIED - security code required.
                wait 2000 ms
            else
                00BC: show_text_highpriority GXT 'M12_042' time 4000 flag 1  // Password located - bypassing lock.
                wait 4000 ms
                00BC: show_text_highpriority GXT 'M12_043' time 4000 flag 1  // Security doors unlocked.
                0164: disable_marker 330@
                repeat
                wait 0 ms
                034E: move_object 205@ to 2565.68 -1301.68 1032.86 speed 0.0 0.0 0.01 flag 0 // posicion original del obj 2565.68 -1301.68 1030.26
                until 04E6:   object 205@ near_point 2565.68 -1301.68 1032.86 radius 0.1 0.1 0.1 flag 0
                $OTHER_TASKS_STATUS = 2
            end
        else //$MOBJ2 == 0
        00BC: show_text_highpriority GXT 'M12_041' time 4000 flag 1  // ACCESS DENIED - security code required.
        end
    else
        if and
        0029:   33@ >= 10000
        88FE: not text_box_displayed
        then
        03E5: show_text_box 'M12_050'  // Press ~s~ENTER~w~ to crack Security Locks.
        33@=0
        end
    end
end

0051: return    

{=================================================================
OBJECTIVE 1 - Holograph radioactive isotope
==================================================================}
:M1_2_OBJECTIVE1
if
0039: $OBJ1_STATUS == 0
then
    if or
    00FE: actor $PLAYER_ACTOR 0 near_point_3d 2561.0522 -1300.936 1054.6406 radius 1.0 1.0 1.0
    00FE: actor $PLAYER_ACTOR 0 near_point_3d 2560.8755 -1287.9502 1054.6406 radius 1.0 1.0 1.0
    then
    00BC: show_text_highpriority GXT 'M12_045' time 4000 flag 1  // WARNING - radioactive matter detected.
    $OBJ1_STATUS = 1
    101@ = 0
    end
end

if
0833: object 202@ photographed
then
    00BC: show_text_highpriority GXT 'M12_044' time 4000 flag 1  // Radioactive isotope successfuly photographed.
    0164: disable_marker 304@ 
    101@ += 1
end
if
0833: object 203@ photographed
then
    00BC: show_text_highpriority GXT 'M12_044' time 4000 flag 1  // Radioactive isotope successfuly photographed.
    0164: disable_marker 305@
    101@ += 1
end
if
0039: 101@ == 2
then
    00BA: show_text_styled GXT 'MOBJ1' time 1000 style 2      //Objective 1: Completed
    $MOBJ1 = 1
end
0051: return

{=================================================================
OBJECTIVE 2 - Start security maintenance cycle
==================================================================}
:M1_2_OBJECTIVE2
if and
0039: $OBJ2_STATUS == 0
00FE: actor $PLAYER_ACTOR 0 near_point_3d 2526.3821 -1303.5354 1048.2891 radius 3.0 3.0 3.0
then
    03BC: 300@ = create_sphere_at 2527.9578 -1290.7281 1048.2891 radius 1.0
    03BC: 301@ = create_sphere_at 2527.7854 -1282.665 1048.2891 radius 1.0
    018A: 302@ = create_checkpoint_at 2527.9578 -1290.7281 1048.2891 
    018A: 303@ = create_checkpoint_at 2527.7854 -1282.665 1048.2891
    00BC: show_text_highpriority GXT 'M12_002' time 4000 flag 1  // (Grimshaw) Reprogram that cleaning bot - it'll give you a way in.
    wait 4000 ms
    0050: gosub @JOANNA_RESPONSE     // (Joanna) Understood.
    $OBJ2_STATUS = 1
end

if       
00FE: actor $PLAYER_ACTOR 0 near_point_3d 2527.9578 -1290.7281 1048.2891 radius 1.0 1.0 1.0
then
    if
    00E1:   player 0 pressed_key 15
    then

        03E6: remove_text_box
        0687: clear_actor $PLAYER_ACTOR task
        
        04ED: load_animation "bd_fire" 
        repeat
        wait 0 ms
        until 04EE: animation "bd_fire" loaded
        0605: actor $PLAYER_ACTOR perform_animation_sequence "wash_up" IFP_file "bd_fire" 10.0 loop 0 0 0 0 time 2000
        04EF: release_animation "bd_fire"
        0173: set_actor $PLAYER_ACTOR Z_angle_to 122.5379   

        if
        0039: $OBJ2_STATUS == 1
        then
        00BC: show_text_highpriority GXT 'M12_028' time 4000 flag 1  // Enemy detected - weapon cache locked.
        end

        if
        0039: $OBJ2_STATUS == 2
        then
        00BC: show_text_highpriority GXT 'M12_030' time 4000 flag 1  // Maintenance robots reprogrammed.
        $OBJ2_STATUS = 3
        else
            if
            0039: $OBJ2_STATUS == 3
            then
            00BC: show_text_highpriority GXT 'M12_048' time 4000 flag 1  // Maintenance robots already reprogrammed.
            end
        end
        
    wait 2000 ms
    
    else
        if and
        0029:   33@ >= 10000
        88FE: not text_box_displayed
        then
        03E5: show_text_box 'M12_047' // Press ~s~ENTER~w~ to access Security System.
        33@=0
        end
    end          
end
        
if       
00FE: actor $PLAYER_ACTOR 0 near_point_3d 2527.7854 -1282.665 1048.2891 radius 1.0 1.0 1.0
then
    if
    00E1:   player 0 pressed_key 15
    then

        03E6: remove_text_box
        0687: clear_actor $PLAYER_ACTOR task

        04ED: load_animation "bd_fire" 
        repeat
        wait 0 ms
        until 04EE: animation "bd_fire" loaded
        0605: actor $PLAYER_ACTOR perform_animation_sequence "wash_up" IFP_file "bd_fire" 10.0 loop 0 0 0 0 time 2000
        04EF: release_animation "bd_fire"  
        0173: set_actor $PLAYER_ACTOR Z_angle_to 122.5379
        
        if
        0039: $OBJ2_STATUS == 1
        then
        00BC: show_text_highpriority GXT 'M12_029' time 4000 flag 1  // Maintenance robots deactivated.
        $OBJ2_STATUS = 2
        else    
            if
            0039: $OBJ2_STATUS == 2
            then
            00BC: show_text_highpriority GXT 'M12_031' time 4000 flag 1  // Maintenance robots activated.
            $OBJ2_STATUS = 1
            else
                if
                0039: $OBJ2_STATUS == 3
                then
                00BC: show_text_highpriority GXT 'M12_031' time 4000 flag 1  // Maintenance robots activated.
                00BA: show_text_styled GXT 'MOBJ2' time 2000 style 2      //Objective 2: Completed
                $MOBJ2 = 1
                03BD: destroy_sphere 300@
                03BD: destroy_sphere 301@
                0164: disable_marker 302@ 
                0164: disable_marker 303@
                end
            end
        end
        
    wait 2000 ms
    
    else
        if and
        0029:   33@ >= 10000
        88FE: not text_box_displayed
        then
        03E5: show_text_box 'M12_047' // Press ~s~ENTER~w~ to access Security System.
        33@=0
        end
    end          
end

0051: return

{=================================================================
OBJECTIVE 3 - Shut down experiments
==================================================================}
:M1_2_OBJECTIVE3
if
0038:   $OBJ3_STATUS == 0    
then
    0247: load_model #wmosci
    038B: load_requested_models         
    :LOAD    
    00D6: if 
    8248:   not model #wmosci available 
    004D: jump_if_false @LOADED 
    wait 0 ms
    jump @LOAD        
    :LOADED
    009A: Scientist1 = create_actor_pedtype 4 model #wmosci at 2549.8967 -1304.4363 1044.125 
    009A: Scientist2 = create_actor_pedtype 4 model #wmosci at 2562.9375 -1281.3452 1044.125 
    009A: Scientist3 = create_actor_pedtype 4 model #wmosci at 2541.3306 -1283.3806 1044.125 
    0173: set_actor Scientist1 Z_angle_to 270.0
    060B: set_actor Scientist1 decision_maker_to 400@
    0173: set_actor Scientist2 Z_angle_to 90.0
    060B: set_actor Scientist2 decision_maker_to 400@
    0173: set_actor Scientist3 Z_angle_to 270.0
    060B: set_actor Scientist3 decision_maker_to 400@
    0249: release_model #wmosci
    $OBJ3_STATUS = 10
    110@=0  // contador de experimentos apagados
    111@=0  // maquina de estado de amenaza 1
    112@=0  // maquina de estado de amenaza 2
    113@=0  // maquina de estado de amenaza 3   
end

if and
0038:   $OBJ3_STATUS == 10
00FE: actor $PLAYER_ACTOR 0 near_point_3d 2533.4878 -1283.0066 1048.2891 radius 3.0 3.0 3.0    
then
00BC: show_text_highpriority GXT 'M12_003' time 4000 flag 1  // (Joanna) Something around here's using a lot of power.
$OBJ3_STATUS = 1
0187: 310@ = create_marker_above_actor Scientist1 
0165: set_marker 310@ color_to 1 
end

{AMENAZANDO AL CIENTÍFICO 1}    
if or
0038:   $OBJ3_STATUS == 1
8038:   not 111@ == 0
then
    if and
    0104:   actor $PLAYER_ACTOR near_actor Scientist1 radius 5.0 5.0 5.0 sphere 0
    0038:   111@ == 0
    then
        00BC: show_text_highpriority GXT 'M12_004' time 4000 flag 1  // (Lab Technician) What are you doing in my lab?
        074D: AS_actor Scientist1 turns_to_and_look_at_actor $PLAYER_ACTOR timelimit -1
        wait 1000 ms
        111@=2
        $OBJ3_STATUS = 2
        0187: 311@ = create_marker_above_actor Scientist2 
        0165: set_marker 311@ color_to 1 
    end
    if and
    0457:   player $PLAYER_CHAR aiming_at_actor Scientist1
    0038:   111@ == 2
    then
        00BC: show_text_highpriority GXT 'M12_005' time 4000 flag 1  // (Joanna) Shut down the experiment.
        05C4: AS_actor Scientist1 hands_up 8000 ms
        wait 4000 ms
        00BC: show_text_highpriority GXT 'M12_006' time 4000 flag 1  // (Lab Technician) I'll shut it down.
        05F5: AS_actor Scientist1 goto_point 2551.7781 -1292.0471 1044.125 mode 6 -1 ms  // caminata inteligente
        111@=3
    end
    if and
    00FE:   actor Scientist1 sphere 0 in_sphere 2551.7781 -1292.0471 1044.125 radius 1.0 1.0 1.0
    0038:   111@ == 3
    then
        0687: clear_actor Scientist1 task
        04ED: load_animation "bd_fire" 
        repeat
        wait 0 ms
        until 04EE: animation "bd_fire" loaded
        0605: actor Scientist1 perform_animation_sequence "wash_up" IFP_file "bd_fire" 10.0 loop 1 0 0 0 time -1
        04EF: release_animation "bd_fire"  
        03E5: show_text_box 'M12_033'//00BC: show_text_highpriority GXT 'M12_033' time 4000 flag 1  // Powering down active systems.
        111@=4
        32@=0
    end
    if and
    0029:   32@ >= 5000
    0038:   111@ == 4
    then
        03E5: show_text_box 'M12_034'//00BC: show_text_highpriority GXT 'M12_034' time 4000 flag 1  // Experiment has been shut down.
        0164: disable_marker 310@
        0687: clear_actor Scientist1 task
        05C4: AS_actor Scientist1 hands_up 8000 ms
        00BC: show_text_highpriority GXT 'M12_007' time 4000 flag 1  // (Lab Technician) My experiments!
        110@+=1
        111@=5
    end    
end

{AMENAZANDO AL CIENTÍFICO 2} 
if or
0038:   $OBJ3_STATUS == 2
8038:   not 112@ == 0
then
    if and
    0104:   actor $PLAYER_ACTOR near_actor Scientist2 radius 5.0 5.0 5.0 sphere 0
    0038:   112@ == 0
    then
        00BC: show_text_highpriority GXT 'M12_008' time 4000 flag 1  // (Lab Technician) I haven't seen you before…
        074D: AS_actor Scientist2 turns_to_and_look_at_actor $PLAYER_ACTOR timelimit -1
        wait 1000 ms
        112@=2
        $OBJ3_STATUS = 3
        0187: 312@ = create_marker_above_actor Scientist3
        0165: set_marker 312@ color_to 1 
    end
    if and
    0457:   player $PLAYER_CHAR aiming_at_actor Scientist2
    0038:   112@ == 2
    then
        00BC: show_text_highpriority GXT 'M12_009' time 4000 flag 1  // (Joanna) Pull the plug on it, NOW!
        05C4: AS_actor Scientist2 hands_up 8000 ms
        wait 4000 ms
        00BC: show_text_highpriority GXT 'M12_010' time 4000 flag 1  // (Lab Technician) Please, just don't hurt me.
        wait 1000 ms
        //05F5: AS_actor Scientist2 goto_point 2540.1787 -1293.551 1044.125 mode 6 -1 ms  // caminata inteligente
        05D6: clear_scmpath
        05D7: add_point_to_scmpath  2560.7231 -1289.1796 1044.125
        05D7: add_point_to_scmpath  2538.7981 -1289.6267 1044.125
        05D7: add_point_to_scmpath  2538.1218 -1293.0355 1044.125
        05D7: add_point_to_scmpath  2540.2437 -1293.3531 1044.125
        05D8: AS_assign_scmpath to_actor Scientist2 flags 6 0 
        112@=3
    end
    if and
    00FE:   actor Scientist2 sphere 0 in_sphere 2540.1787 -1293.551 1044.125 radius 1.0 1.0 1.0
    0038:   112@ == 3
    then
        0687: clear_actor Scientist2 task
        04ED: load_animation "bd_fire" 
        repeat
        wait 0 ms
        until 04EE: animation "bd_fire" loaded
        0605: actor Scientist2 perform_animation_sequence "wash_up" IFP_file "bd_fire" 10.0 loop 1 0 0 0 time -1
        04EF: release_animation "bd_fire"  
        03E5: show_text_box 'M12_033'//00BC: show_text_highpriority GXT 'M12_033' time 4000 flag 1  // Powering down active systems.
        112@=4
        32@=0
    end
    if and
    0029:   32@ >= 5000
    0038:   112@ == 4
    then
        03E5: show_text_box 'M12_034'//00BC: show_text_highpriority GXT 'M12_034' time 4000 flag 1  // Experiment has been shut down.
        0164: disable_marker 311@
        00BC: show_text_highpriority GXT 'M12_011' time 4000 flag 1  // (Lab Technician) There, the experiment is down.
        0687: clear_actor Scientist2 task
        05C4: AS_actor Scientist2 hands_up 8000 ms
        110@+=1
        112@=5
    end    
end

{AMENAZANDO AL CIENTÍFICO 3} 
if or
0038:   $OBJ3_STATUS == 3
8038:   not 113@ == 0
then
    if and
    0104:   actor $PLAYER_ACTOR near_actor Scientist3 radius 5.0 5.0 5.0 sphere 0
    0038:   113@ == 0
    then
        00BC: show_text_highpriority GXT 'M12_012' time 4000 flag 1  // (Lab Technician) Who the hell are you?
        074D: AS_actor Scientist3 turns_to_and_look_at_actor $PLAYER_ACTOR timelimit -1
        wait 1000 ms
        113@=2
        $OBJ3_STATUS = 4
    end
    if and
    0457:   player $PLAYER_CHAR aiming_at_actor Scientist3
    0038:   113@ == 2
    then
        00BC: show_text_highpriority GXT 'M12_013' time 4000 flag 1  // (Joanna) Switch this thing off!
        wait 4000 ms
        00BC: show_text_highpriority GXT 'M12_014' time 4000 flag 1  // (Lab Technician) Allow me to assist you.
        wait 1000 ms
       //05F5: AS_actor Scientist3 goto_point 2561.6721 -1293.316 1044.125 mode 6 -1 ms  // caminata inteligente
        05D6: clear_scmpath
        05D7: add_point_to_scmpath  2541.3303 -1289.6598 1044.125
        05D7: add_point_to_scmpath  2562.793 -1290.0226 1044.125
        05D7: add_point_to_scmpath  2563.282 -1293.311 1044.125
        05D7: add_point_to_scmpath  2561.6401 -1293.4718 1044.125
        05D8: AS_assign_scmpath to_actor Scientist3 flags 6 0 
        113@=3
    end
    if and
    00FE:   actor Scientist3 sphere 0 in_sphere 2561.6721 -1293.316 1044.125 radius 1.0 1.0 1.0
    0038:   113@ == 3
    then
        0687: clear_actor Scientist3 task
        04ED: load_animation "bd_fire" 
        repeat
        wait 0 ms
        until 04EE: animation "bd_fire" loaded
        0605: actor Scientist3 perform_animation_sequence "wash_up" IFP_file "bd_fire" 10.0 loop 1 0 0 0 time -1
        04EF: release_animation "bd_fire"  
        113@=4
        32@=0
    end
    if and
    0029:   32@ >= 5000
    0038:   113@ == 4
    then
        00BC: show_text_highpriority GXT 'M12_015' time 4000 flag 1  // (Lab Technician) Accidents will happen.
        // Alarma y crear enemigos
        0164: disable_marker 312@
        0687: clear_actor Scientist3 task
        03BC: 313@ = create_sphere_at 2560.6064 -1291.9163 1044.125 radius 1.0
        018A: 314@ = create_checkpoint_at 2560.6064 -1291.9163 1044.125 
        113@=5
    end
    if and
    00FE: actor $PLAYER_ACTOR 0 near_point_3d 2560.6064 -1291.9163 1044.125 radius 2.0 2.0 2.0
    0038:   113@ == 5
    then
        if
        00E1:   player 0 pressed_key 15
        then
            03E6: remove_text_box
            0687: clear_actor $PLAYER_ACTOR task
            04ED: load_animation "bd_fire" 
            repeat
            wait 0 ms
            until 04EE: animation "bd_fire" loaded
            0605: actor $PLAYER_ACTOR perform_animation_sequence "wash_up" IFP_file "bd_fire" 10.0 loop 0 0 0 0 time 2000
            04EF: release_animation "bd_fire"  
            0173: set_actor $PLAYER_ACTOR Z_angle_to 174.215   
            03E5: show_text_box 'M12_033'//00BC: show_text_highpriority GXT 'M12_033' time 4000 flag 1  // Powering down active systems.
            wait 4000 ms
            00BC: show_text_highpriority GXT 'M12_034' time 4000 flag 1  // Experiment has been shut down.
            03BD: destroy_sphere 313@
            0164: disable_marker 314@
            110@+=1
        else
            if and
            0029:   33@ >= 10000
            88FE: not text_box_displayed
            then
            03E5: show_text_box 'M12_049' // Press ~s~ENTER~w~ to access Experiment Control Panel.
            33@=0
            end
        end
    end
    if
    0038:   110@ == 3
    then
    00BA: show_text_styled GXT 'MOBJ3' time 2000 style 2      //Objective 3: Completed
    $MOBJ3 = 1
    end    
end
0051: return

{=================================================================
OBJECTIVE 4 - Obtain experimental technologies
==================================================================}
:M1_2_OBJECTIVE4    
if and
0039: $OBJ4_STATUS == 0
00FE: actor $PLAYER_ACTOR 0 near_point_3d 2575.6697 -1289.6407 1044.125 radius 2.0 2.0 2.0
then
    0247: load_model #wmosci 
    0247: load_model #swat
    0247: load_model #m4
    038B: load_requested_models
    repeat
    wait 0 ms
    until 0248: model #wmosci available //and 0248: model #swat available and 0248: model #m4 available
    0213: 220@ = create_pickup #nvgoggles type 3 at 2581.2241 -1284.9583 1044.125 // Night Vision 
    0213: 221@ = create_pickup #BODYARMOUR type 3 at 2569.4658 -1304.8938 1031.4219 // Shield
    009A: dDScientist = create_actor_pedtype 4 model #wmosci at 2574.6968 -1283.1562 1037.7734 
    009A: dDTrooper = create_actor_pedtype 7 model #swat at 2574.5591 -1285.4468 1037.7734
    0173: set_actor dDScientist Z_angle_to 178.2747  
    0173: set_actor dDTrooper Z_angle_to 230.7164
    03DC: 320@ = create_marker_above_pickup 220@ 
    03DC: 321@ = create_marker_above_pickup 221@ 
    0187: 322@ = create_marker_above_actor dDTrooper
    01B2: give_actor dDTrooper weapon 31 ammo 100
    01B9: set_actor dDTrooper armed_weapon_to 31
    087E: set_actor dDTrooper weapon_droppable 0     
    0249: release_model #wmosci
    0249: release_model #swat
    0249: release_model #m4
    120@=0
    124@=1  
    $OBJ4_STATUS = 1  
end            

{MÁQUINA DE ESTADO DE LA CONVERSACIÓN ENTRE EL CIENTÍFICO Y EL dDTROOPER}
if
0039: $OBJ4_STATUS == 1
then
    if and
    0039: 124@ == 1
    0104:   actor $PLAYER_ACTOR near_actor dDScientist radius 15.0 15.0 1.0 sphere 0
    then
        00BC: show_text_highpriority GXT 'M12_019' time 4000 flag 1  // (Lab Technician) Try it now.
        32@=0
        124@ = 2
    end
    
    if and
    0039: 124@ == 2
    0029: 32@ >= 6000
    then
        00BC: show_text_highpriority GXT 'M12_022' time 4000 flag 1  // (dataDyne Trooper) I'm not sure.
        32@=0
        124@ = 3
    end
    
    if and
    0039: 124@ == 3
    0029: 32@ >= 6000
    then
        00BC: show_text_highpriority GXT 'M12_020' time 4000 flag 1  // (Lab Technician) And again.
        32@=0
        124@ = 4
    end

    if and
    0039: 124@ == 4
    0029: 32@ >= 6000
    then
        00BC: show_text_highpriority GXT 'M12_025' time 4000 flag 1  // (dataDyne Trooper) Come on!
        32@=0
        124@ = 5
    end
    
    if and
    0039: 124@ == 5
    0029: 32@ >= 6000
    then
        00BC: show_text_highpriority GXT 'M12_021' time 4000 flag 1  // (Lab Technician) Any better?
        32@=0
        124@ = 6
    end

    if and
    0039: 124@ == 6
    0029: 32@ >= 6000
    then
        00BC: show_text_highpriority GXT 'M12_024' time 4000 flag 1  // (dataDyne Trooper) I dunno.
        32@=0
        124@ = 7
    end

    if and
    0039: 124@ == 7
    0029: 32@ >= 6000
    then
        00BC: show_text_highpriority GXT 'M12_019' time 4000 flag 1  // (Lab Technician) Try it now.
        32@=0
        124@ = 2
    end

    if
    0104:   actor $PLAYER_ACTOR near_actor dDScientist radius 10.0 10.0 1.0 sphere 0
    then
        00BC: show_text_highpriority GXT 'M12_026' time 4000 flag 1  // (Lab Technician) Oh, oh no! She's got a gun!
        05DA: AS_actor dDScientist run_away_in_panic_from 2575.5054 -1281.3318 1037.7734 away_radius 20.0 timelimit 15000
        05E2: AS_actor dDTrooper kill_actor $PLAYER_ACTOR
        124@ = 8
        $OBJ4_STATUS = 2
    end
end     

if and
0039: 124@ == 8
0118:   actor dDTrooper dead
then
    0164: disable_marker 322@
    00A0: store_actor dDTrooper position_to 121@ 122@ 123@
    032B: 222@ = create_weapon_pickup #m4 group 15 ammo 60 at 121@ 122@ 123@
    03DC: 323@ = create_marker_above_pickup 222@
    124@ = 9
end
    
if  
0039: 124@ == 9
then
    if
    0214:   pickup 222@ picked_up
    then
        017B: set_actor $PLAYER_ACTOR weapon 31 ammo_to 15 
        0164: disable_marker 323@ 
        120@+=1
        0050: gosub @M1_2_OBJECTIVE4_CHECK
        124@ = 10
    end
end     

{CONDICIONES QUE QUEREMOS QUE SE CHEQUEEN SIEMPRE QUE ESTÉ ACTIVA LA MISIÓN 4}
if or
0039: $OBJ4_STATUS == 1
0039: $OBJ4_STATUS == 2
then
    if
    0214:   pickup 220@ picked_up
    then
        01B2: give_actor $PLAYER_ACTOR weapon 44 ammo 1
        120@+=1
        0050: gosub @M1_2_OBJECTIVE4_CHECK
    end     

    if
    0214:   pickup 221@ picked_up
    then
        120@+=1
        0050: gosub @M1_2_OBJECTIVE4_CHECK
    end
   
    if
    0118:   actor dDScientist dead
    then
    $OTHER_TASKS_STATUS = 99         // jump to fail mission routine
    end     
end

0051: return

:M1_2_OBJECTIVE4_CHECK
if
0039: 120@ == 1
then
    00BC: show_text_highpriority GXT 'M12_035' time 4000 flag 1  // Experimental item 1 acquired.
end
if
0039: 120@ == 2
then
    00BC: show_text_highpriority GXT 'M12_036' time 4000 flag 1  // Experimental item 2 acquired.
end
if
0039: 120@ == 3
then
    00BC: show_text_highpriority GXT 'M12_037' time 4000 flag 1  // Experimental item 3 acquired.
    00BA: show_text_styled GXT 'MOBJ4' time 2000 style 2       //Objective 4: Completed
    $MOBJ4 = 1    
end
0051: return

{=================================================================
OBJECTIVE 5 - Locate Dr. Caroll
==================================================================}
:RESEARCH_OUTRO
01B4: toggle_player $PLAYER_CHAR can_move 0
0050: gosub @SUB_FADE_500MS
023C: load_special_actor 'caroll' as 1
038B: load_requested_models
repeat
wait 0 ms
until 023D:   special_actor 1 loaded
009A: DrCaroll = create_actor_pedtype 4 model #SPECIAL01 at 2528.3015 -1289.611 1031.4219
0173: set_actor DrCaroll Z_angle_to 90.0
0296: unload_special_actor 1
 
{Cutscene de Joanna encontrando al Doctor Caroll}

00A1: put_actor DrCaroll at 2533.2874 -1298.2233 1031.4219
0173: set_actor DrCaroll Z_angle_to 270.0
00A1: put_actor $PLAYER_ACTOR at 2530.9272 -1294.1066 1031.4219
0173: set_actor $PLAYER_ACTOR Z_angle_to 270.0
if
0039: $MISSION_DIFFICULTY == 3  // Perfect Agent
then
    0187: 340@ = create_marker_above_actor DrCaroll
    07E0: set_marker 340@ type_to 1  //0: red / 1: blue
    018A: 341@ = create_checkpoint_at 2578.4309 -1300.4418 1060.9922 // ascensor
    Marker.SetColor(341@,0) //red
    0850: AS_actor DrCaroll follow_actor $PLAYER_ACTOR
    $OTHER_TASKS_STATUS = 3 
end
Camera.SetBehindPlayer()  
0001: wait 1000 ms 
016A: fade 1 time 1500
01B4: toggle_player $PLAYER_CHAR can_move 1

0051: return

{=================================================================
Final routines
==================================================================}
//––––––––––––-
:RESEARCH_PASSED
0394: play_music 1
$TUT_MISSIONS_PASSED += 1
Player.Money($PLAYER_CHAR) += 10000
01E3: show_text_1number_styled GXT 'M_PASS' number 10000 time 5000 style 1 // MISSION PASSED!~n~~w~$~1~
return

//––––––––––––-
:RESEARCH_FAIL
00BA: show_text_styled GXT 'M_FAIL' time 5000 style 1 // ~r~MISSION FAILED!
//0555: remove_weapon 31 from_actor $PLAYER_ACTOR
return

//––––––––––––-
:RESEARCH_CLEANUP
$ONMISSION = 0
{CONDICIONES GENERALES}
010D: set_player $PLAYER_CHAR wanted_level_to 0
01F0: set_max_wanted_level_to 6 
{ACTORES}
01C2: remove_references_to_actor Scientist1
01C2: remove_references_to_actor Scientist2
01C2: remove_references_to_actor Scientist3
01C2: remove_references_to_actor dDScientist
01C2: remove_references_to_actor dDTrooper
01C2: remove_references_to_actor DrCaroll
{OBJECTS}
01C4: remove_references_to_object 200@ 
01C4: remove_references_to_object 201@ 
01C4: remove_references_to_object 202@ 
01C4: remove_references_to_object 203@ 
01C4: remove_references_to_object 204@ 
01C4: remove_references_to_object 205@ 
01C4: remove_references_to_object 206@ 
01C4: remove_references_to_object 207@ 
{MARKERS / CHECKPOINTS}
0A41: destroy_entrance_marker BuildingDoor
08E7: disable_entrance_markers 0
0164: disable_marker 304@
0164: disable_marker 305@
0164: disable_marker 302@
0164: disable_marker 303@
0164: disable_marker 310@
0164: disable_marker 311@
0164: disable_marker 312@
0164: disable_marker 314@
0164: disable_marker 320@
0164: disable_marker 321@
0164: disable_marker 322@
0164: disable_marker 323@
0164: disable_marker 340@
0164: disable_marker 341@
0164: disable_marker 330@
{SPHERES}
03BD: destroy_sphere 300@ 
03BD: destroy_sphere 301@ 
03BD: destroy_sphere 313@ 
{DECISIONS}
065C: release_decision_maker 400@  // Comportamiento limpio para actores especiales
{VARIABLES GLOBALES}
$MOBJ1 = 0
$MOBJ2 = 0
$MOBJ3 = 0
$MOBJ4 = 0
$MOBJ5 = 0
$OBJ1_STATUS=0
$OBJ2_STATUS=0
$OBJ3_STATUS=0
$OBJ4_STATUS=0
$OBJ5_STATUS=0
$OTHER_TASKS_STATUS=0
$ALLOBJECTIVES=0
mission_cleanup
return







//––––-External script 0–––––
// put your external scripts here