DEFINE MISSIONS 10
DEFINE MISSION 0 AT @INITIAL
DEFINE MISSION 1 AT @DEFECTION
DEFINE MISSION 2 AT @RESEARCH
DEFINE MISSION 3 AT @EXTRACTION
DEFINE MISSION 4 AT @VILLA
DEFINE MISSION 5 AT @STEALTH
DEFINE MISSION 6 AT @BUILDING
DEFINE MISSION 7 AT @INFILTRATION
DEFINE MISSION 8 AT @RESCUE
DEFINE MISSION 9 AT @ESCAPE


DEFINE EXTERNAL_SCRIPTS -1 // Use -1 in order not to compile AAA script
//DEFINE SCRIPT {NAME} AT {LABEL} @

DEFINE UNKNOWN_EMPTY_SEGMENT 0

DEFINE UNKNOWN_THREADS_MEMORY 0

//{$VERSION 3.0.0000}
var
$PLAYER_CHAR: Player
end // var
03A4: name_thread 'MAIN'
01F0: set_max_wanted_level_to 6
0111: toggle_wasted_busted_check 0
00C0: set_current_time_hours_to 8 minutes_to 0
04E4: unknown_refresh_game_renderer_at 1049.9067 1031.3267
03CB: set_rendering_origin_at 1049.9067 1031.3267 10.685
0053: $PLAYER_CHAR = create_player #NULL at 1049.9067 1031.3267 10.685 //1453.0439 -1841.0349 13.5469
01F5: $PLAYER_ACTOR = create_player_actor $PLAYER_CHAR
07AF: $PLAYER_GROUP = player $PLAYER_CHAR group
0373: set_camera_directly_behind_player
01B6: set_weather 0
0001: wait 0 ms
087B: set_player $PLAYER_CHAR clothes_texture "PLAYER_FACE" model "HEAD" body_part 1
087B: set_player $PLAYER_CHAR clothes_texture "JEANSDENIM" model "JEANS" body_part 2
087B: set_player $PLAYER_CHAR clothes_texture "SNEAKERBINCBLK" model "SNEAKER" body_part 3
087B: set_player $PLAYER_CHAR clothes_texture "VEST" model "VEST" body_part 0
070D: rebuild_player $PLAYER_CHAR
01B4: toggle_player $PLAYER_CHAR can_move 0
016A: fade 0 time 0
04BB: select_interior 0
0629: change_integer_stat 181 to 4
016C: restart_if_wasted_at 2027.77 -1420.52 15.99 angle 137.0 town_number 0
016D: restart_if_busted_at 1550.68 -1675.49 14.51 angle 90.0 town_number 0
0180: set_on_mission_flag_to $ONMISSION // Note: your missions have to use the variable defined here
0004: $DEFAULT_WAIT_TIME = 250
03E6: remove_text_box

023C: load_special_actor 'reimu' as 1
038B: load_requested_models
repeat
wait 0 ms
until 023D:   special_actor 1 loaded
09C7: change_player $PLAYER_CHAR model_to #SPECIAL01
0296: unload_special_actor 1

wait 1000 ms
016A: fade 1 time 2000
01B4: toggle_player $PLAYER_CHAR can_move 1
0417: start_mission 0 // Initial
wait 0
// put your create_thread commands here
00D7: create_thread @CI_MISSIONS_SNIFFER

:MAIN_LOOP
0001: wait $DEFAULT_WAIT_TIME ms
0002: jump @MAIN_LOOP




{==============================================================
GLOBAL SUBROUTINES
===============================================================}
:SUB_FADE_500MS
0169: set_fade_color_RGB 0 0 0 
016A: fade 0 time 500 

:LITCAS_282
00D6: if 
016B:   fading 
004D: jump_if_false @LITCAS_306 
0001: wait 0 ms 
0002: jump @LITCAS_282 

:LITCAS_306
0051: return 
//------------------------
:TELEPORT
// Seleccionando Interior y colocándo al jugador en él
0050: gosub @SUB_FADE_500MS 
01B4: toggle_player $PLAYER_CHAR can_move 0
04BB: select_interior $ACTIVE_INTERIOR 
0860: link_actor $PLAYER_ACTOR to_interior $ACTIVE_INTERIOR 
04E4: unknown_refresh_game_renderer_at $PLAYER_X $PLAYER_Y 
03CB: set_rendering_origin_at $PLAYER_X $PLAYER_Y $PLAYER_Z 
//04F9: set_interior 1 color_to 0 
00A1: put_actor $PLAYER_ACTOR at $PLAYER_X $PLAYER_Y $PLAYER_Z 
0173: set_actor $PLAYER_ACTOR Z_angle_to $PLAYER_ANGLE 
0373: set_camera_directly_behind_player 
//02EB: restore_camera_with_jumpcut
0051: return
//------------------------
:FADEOUT
0001: wait 1000 ms 
016A: fade 1 time 1000 
01B4: set_player $PLAYER_CHAR can_move 1
0051: return
//------------------------
:JOANNA_RESPONSE
0209: $RAND = random_int_in_ranges 0 3
00D6: if
0039:   $RAND == 0
004D: jump_if_false @J01 
00BC: show_text_highpriority GXT 'MJO_1' time 4000 flag 1  // (Joanna) Received.
:J01
00D6: if
0039:   $RAND == 1
004D: jump_if_false @J02 
00BC: show_text_highpriority GXT 'MJO_2' time 4000 flag 1  // (Joanna) Understood.
:J02
00D6: if
0039:   $RAND == 2
004D: jump_if_false @J03 
00BC: show_text_highpriority GXT 'MJO_3' time 4000 flag 1  // (Joanna) Confirmed.
:J03
00D6: if
0039:   $RAND == 3
004D: jump_if_false @J04 
00BC: show_text_highpriority GXT 'MJO_4' time 4000 flag 1  // (Joanna) Affirmative.
:J04
0051: return  
//------------------------
:MAIN_MENU
01B4: set_player $PLAYER_CHAR can_move 0
0512:  show_permanent_text_box 'MHLP01' // ~b~~k~~GO_FORWARD~ / ~k~~GO_BACK~~w~: Change option ~b~~k~~PED_SPRINT~~w~: Accept
08D4: $MENU = create_panel_with_title 'PD' position 29.0 145.0 width 300.0 columns 1 interactive 1 background 1 alignment 1 // Perfect Dark
08DB:  set_panel $MENU column 0 header 'MTBL02' data 'CI' 'MTBL_1' 'MTBL_2' 'MTBL_3' 'MTBL_4' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' // Perfect Menu
repeat
wait 0 ms
until 00E1: player 0 pressed_key 16 
repeat
wait 0 ms
until 80E1: not player 0 pressed_key 16 
08D7: $choice1 = panel $MENU active_row
08DA: remove_panel $MENU
03E6: remove text box
if or
0038: $choice1 == 0 // Carrington Institute
0038: $choice1 == 2 // Combat Simulators
0038: $choice1 == 3 // Co-Operative
0038: $choice1 == 4 // Counter-Operative
then
    00BC: show_text_highpriority GXT 'M_ERR2' time 4000 flag 1  // This option is not ~b~suported~w~ yet...
    wait 4000 ms
    jump @MAIN_MENU
end
if $choice1 == 1 // Solo Missions
then gosub @MISSION_SELECT
end
0051: return  
//------------------------
:MISSION_SELECT
0512:  show_permanent_text_box 'MHLP01' // ~b~~k~~GO_FORWARD~ / ~k~~GO_BACK~~w~: Change option ~b~~k~~PED_SPRINT~~w~: Accept
08D4: $MENU = create_panel_with_title 'PD' position 29.0 145.0 width 300.0 columns 1 interactive 1 background 1 alignment 1 // Perfect Dark
08DB:  set_panel $MENU column 0 header 'MTBL03' data 'M1_1' 'M1_2' 'M1_3' 'M2_1' 'M3_1' 'M3_2' 'M4_1' 'M4_2' 'M4_3' 'M5_1' 'M5_2' 'M5_3' 'M6_1' 'M6_2' 'M7_1' 'M8_1' 'M9_1'// Mission Select
repeat
wait 0 ms
    if 00E1: player 0 pressed_key 15
    then
        repeat
        wait 0 ms
        until 80E1: not player 0 pressed_key 15
        08DA: remove_panel $SEL_DIF_MENU
        03E6: remove text box
        jump @MAIN_MENU
    end
until 00E1: player 0 pressed_key 16 
repeat
wait 0 ms
until 80E1: not player 0 pressed_key 16 
08D7: $choice2 = panel $MENU active_row
08DA: remove_panel $MENU
03E6: remove text box
if $choice2 == 0
then
    $ONMISSION = 1
    s$MISSION = 'M1_1' // dataDyne Central: Defection
    $M_NUM=1
    gosub @SELECT_DIFFICULTY
end
if $choice2 == 1
then
    $ONMISSION = 1
    s$MISSION = 'M1_2' // dataDyne Research: Investigation
    $M_NUM=2
    gosub @SELECT_DIFFICULTY
end
if $choice2 == 2
then
    $ONMISSION = 1
    s$MISSION = 'M1_3' // dataDyne Central: Extraction
    $M_NUM=3
    gosub @SELECT_DIFFICULTY
end
if $choice2 == 3
then
    $ONMISSION = 1
    s$MISSION = 'M2_1' // Carrington Villa: Hostage One
    $M_NUM=4
    gosub @SELECT_DIFFICULTY
end
if $choice2 == 4
then
    $ONMISSION = 1
    s$MISSION = 'M3_1' // Chicago: Stealth
    $M_NUM=5
    gosub @SELECT_DIFFICULTY
end
if $choice2 == 5
then
    $ONMISSION = 1
    s$MISSION = 'M3_2' // G5 Building: Reconnaissance
    $M_NUM=6
    gosub @SELECT_DIFFICULTY
end
if $choice2 == 6
then
    $ONMISSION = 1
    s$MISSION = 'M4_1' // Area 51: Infiltration
    $M_NUM=7
    gosub @SELECT_DIFFICULTY
end
if $choice2 == 7
then
    $ONMISSION = 1
    s$MISSION = 'M4_2' // Area 51: Rescue
    $M_NUM=8
    gosub @SELECT_DIFFICULTY
end
if $choice2 == 8
then
    $ONMISSION = 1
    s$MISSION = 'M4_3' // Area 51: Escape
    $M_NUM=9
    gosub @SELECT_DIFFICULTY
end
if or
0038: $choice2 == 9
0038: $choice2 == 10
0038: $choice2 == 11
0038: $choice2 == 12
0038: $choice2 == 13
0038: $choice2 == 14
0038: $choice2 == 15
0038: $choice2 == 16
then
    00BC: show_text_highpriority GXT 'M_ERR1' time 4000 flag 1  // The Mission you ~y~selected~w~ is not ready yet...
    wait 4000 ms
    jump @MISSION_SELECT
end
0051: return  
//------------------------
:SELECT_DIFFICULTY
01B4: set_player $PLAYER_CHAR can_move 0
0512:  show_permanent_text_box 'MHLP01' // ~b~~k~~GO_FORWARD~ / ~k~~GO_BACK~~w~: Change option ~b~~k~~PED_SPRINT~~w~: Accept
08D4: $MENU = create_panel_with_title 'MTBL01' position 29.0 145.0 width 300.0 columns 1 interactive 1 background 1 alignment 1 // Enter Mission
08DB:  set_panel $MENU column 0 header s$MISSION data 'MDIF1' 'MDIF2' 'MDIF3' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY'
repeat
wait 0 ms
    if 00E1: player 0 pressed_key 15
    then
        repeat
        wait 0 ms
        until 80E1: not player 0 pressed_key 15
        08DA: remove_panel $SEL_DIF_MENU
        03E6: remove text box
        jump @MISSION_SELECT
    end
until 00E1: player 0 pressed_key 16 
repeat
wait 0 ms
until 80E1: not player 0 pressed_key 16 
08D7: $choice3 = panel $SEL_DIF_MENU active_row
08DA: remove_panel $SEL_DIF_MENU
03E6: remove text box
if $choice3 == 0
then
    $MISSION_DIFFICULTY = 1     // Agent
    $SubMachineAmmo     = 35
    $MissionAccuracy    = 45
    $MissionHealth      = 45
end
if $choice3 == 1
then
    $MISSION_DIFFICULTY = 2     // Special Agent
    $SubMachineAmmo     = 25
    $MissionAccuracy    = 60
    $MissionHealth      = 70
end
if $choice3 == 2
then
    $MISSION_DIFFICULTY = 3     // Perfect Agent
    $SubMachineAmmo     = 18
    $MissionAccuracy    = 75
    $MissionHealth      = 100
end
0051: return  
//------------------------
:MISSION_BAR
if 00E1:   player 0 pressed_key 11    //~k~~CONVERSATION_YES~
then
    015D: set_gamespeed  0.001 // 0.3 o 1.0 slow motion
    01B4: set_player $PLAYER_CHAR can_move 0 

    038E: draw_box_position 400.0 270.0 size 400.0 220.0 RGBA 123 34 111 75
    038E: draw_box_position 400.0 185.0 size 340.0 1.2 RGBA 255 255 255 60

    033F: set_text_draw_letter_size 0.5 1.2
    0343: set_text_draw_linewidth 600.0 
    033E: set_draw_text_position 225.0 170.0 GXT s$MISSION    // Mission Name

    if 85AD:   not s$OBJ1 == 'DUMMY'
    then
        // Objective 1
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        033E: set_draw_text_position 225.0 200.0 GXT 'OBJ1'
        
        // Objective 1 Entry
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        033E: set_draw_text_position 315.0 200.0 GXT s$OBJ1
        
        // Objective 1 Status (Complete / Incomplete)
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        03E4: enable_text_draw_align_right 1
        if 0038: $MOBJ1 == 0
        then
        0340: set_text_draw_RGBA 250 164 96 255 
        033E: set_draw_text_position 540.0 210.0 GXT 'OBJINC'
        else
        0340: set_text_draw_RGBA 135 206 235 255 
        033E: set_draw_text_position 540.0 210.0 GXT 'OBJCMP' 
        end
    end 

    if 85AD:   not s$OBJ2 == 'DUMMY'
    then
        // Objective 2
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        033E: set_draw_text_position 225.0 225.0 GXT 'OBJ2'
        
        // Objective 2 Entry
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        033E: set_draw_text_position 315.0 225.0 GXT s$OBJ2
        
        // Objective 2 Status (Complete / Incomplete)
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        03E4: enable_text_draw_align_right 1
        if 0038: $MOBJ2 == 0
        then
        0340: set_text_draw_RGBA 250 164 96 255 
        033E: set_draw_text_position 540.0 235.0 GXT 'OBJINC'
        else
        0340: set_text_draw_RGBA 135 206 235 255 
        033E: set_draw_text_position 540.0 235.0 GXT 'OBJCMP' 
        end
    end 

    if 85AD:   not s$OBJ3 == 'DUMMY'
    then
        // Objective 3
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        033E: set_draw_text_position 225.0 250.0 GXT 'OBJ3'
        
        // Objective 3 Entry
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        033E: set_draw_text_position 315.0 250.0 GXT s$OBJ3
        
        // Objective 3 Status (Complete / Incomplete)
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        03E4: enable_text_draw_align_right 1
        if 0038: $MOBJ3 == 0
        then
        0340: set_text_draw_RGBA 250 164 96 255 
        033E: set_draw_text_position 540.0 260.0 GXT 'OBJINC'
        else
        0340: set_text_draw_RGBA 135 206 235 255 
        033E: set_draw_text_position 540.0 260.0 GXT 'OBJCMP' 
        end
    end 

    if 85AD:   not s$OBJ4 == 'DUMMY'
    then
        // Objective 4
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        033E: set_draw_text_position 225.0 275.0 GXT 'OBJ4'
        
        // Objective 4 Entry
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        033E: set_draw_text_position 315.0 275.0 GXT s$OBJ4
        
        // Objective 4 Status (Complete / Incomplete)
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        03E4: enable_text_draw_align_right 1
        if 0038: $MOBJ4 == 0
        then
        0340: set_text_draw_RGBA 250 164 96 255 
        033E: set_draw_text_position 540.0 285.0 GXT 'OBJINC'
        else
        0340: set_text_draw_RGBA 135 206 235 255 
        033E: set_draw_text_position 540.0 285.0 GXT 'OBJCMP' 
        end
    end 

    if 85AD:   not s$OBJ5 == 'DUMMY'
    then
        // Objective 5
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        033E: set_draw_text_position 225.0 300.0 GXT 'OBJ5'
        
        // Objective 5 Entry
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        033E: set_draw_text_position 315.0 300.0 GXT s$OBJ5
        
        // Objective 5 Status (Complete / Incomplete)
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        03E4: enable_text_draw_align_right 1
        if 0038: $MOBJ5 == 0
        then
        0340: set_text_draw_RGBA 250 164 96 255 
        033E: set_draw_text_position 540.0 310.0 GXT 'OBJINC'
        else
        0340: set_text_draw_RGBA 135 206 235 255 
        033E: set_draw_text_position 540.0 310.0 GXT 'OBJCMP' 
        end
    end 

    038E: draw_box_position 400.0 337.0 size 340.0 1.2 RGBA 255 255 255 60

    033F: set_text_draw_letter_size 0.4 1.0
    0343: set_text_draw_linewidth 600.0 
    03E4: enable_text_draw_align_right 1
    033E: set_draw_text_position 540.0 340.0 GXT 'OBTB01'     // Abort Mission

    repeat
    wait 0 ms
    until 80E1: not player 0 pressed_key 11
        
    repeat
    wait 0 ms
    until 00E1:   player 0 pressed_key 11

    015D: set_gamespeed  1.0 // 0.3 o 1.0 slow motion
    01B4: set_player $PLAYER_CHAR can_move 1
    03F0: enable_text_draw 0 

    repeat
    wait 0 ms
    until 80E1: not player 0 pressed_key 11
end
0051: return  
//------------------------
:ENABLE_CAMSPY
if and
00E1:   player 0 pressed_key 15 // Enter/Intro 
02D8:   actor $PLAYER_ACTOR current_weapon == 43 // Cam
0038:   $CAMSPY_STATUS == 0 // Player has not released camspy
then
    repeat
    wait 0 ms
    until 80E1: not player 0 pressed_key 15 // Wait for release key
    00A0: store_actor $PLAYER_ACTOR position_to $oldposition[0] $oldposition[1] $oldposition[2]
    0172: $old_angle = actor $PLAYER_ACTOR Z_angle
    077E: get_active_interior_to $player_interior // the interior where player lies
    0086: $newposition[0] = $oldposition[0] // (float) 
    0009: $newposition[0] += 1.0
    0086: $newposition[1] = $oldposition[1] // (float) 
    0009: $newposition[1] += 1.0
    0086: $newposition[2] = $oldposition[2] // (float) 
    
    023C: load_special_actor 'TENPEN' as 1
    023C: load_special_actor 'moko' as 2
    038B: load_requested_models 
    repeat
    wait 0 ms
    until 023D: special_actor 1 loaded and 023D: special_actor 2 loaded
    00A1: put_actor $PLAYER_ACTOR at $newposition[0] $newposition[1] $newposition[2]
    09C7: change_player $PLAYER_CHAR model_to #SPECIAL01
    009A: $CAMSPY = create_actor_pedtype 23 model #SPECIAL02 at $oldposition[0] $oldposition[1] $oldposition[2]
    0173: set_actor $CAMSPY Z_angle_to $old_angle
    060A: create_decision_maker_type 0 store_to $clean
    060B: set_actor $CAMSPY decision_maker_to $clean
    0296: unload_special_actor 1
    0296: unload_special_actor 2
    01B9: set_actor $PLAYER_ACTOR armed_weapon_to 0 // Unarmed
    $CAMSPY_STATUS=1
    0086: $old_area_status = $AREA_STATUS // (float)
end

if and
00E1:   player 0 pressed_key 15 // Enter/Intro 
0039:   $CAMSPY_STATUS == 1 // Player IS camspy
then
    repeat
    wait 0 ms
    until 80E1: not player 0 pressed_key 15 // Wait for release key
    0086: $newposition[0] = $oldposition[0] // (float) 
    0086: $newposition[1] = $oldposition[1] // (float) 
    0086: $newposition[2] = $oldposition[2] // (float) 
    0086: $new_angle = $old_angle // (float) 
    00A0: store_actor $PLAYER_ACTOR position_to $oldposition[0] $oldposition[1] $oldposition[2]
    0172: $old_angle = actor $PLAYER_ACTOR Z_angle
    077E: get_active_interior_to $camspy_interior // the interior where camspy lies

    023C: load_special_actor 'TENPEN' as 1
    023C: load_special_actor 'moko' as 2
    038B: load_requested_models 
    repeat
    wait 0 ms
    until 023D: special_actor 1 loaded and 023D: special_actor 2 loaded

    04BB: select_interior $player_interior // the interior where player lies
    0860: link_actor $PLAYER_ACTOR to_interior $player_interior 
    04E4: unknown_refresh_game_renderer_at $newposition[0] $newposition[1]
    03CB: set_rendering_origin_at $newposition[0] $newposition[1] $newposition[2]
    009B: destroy_actor $CAMSPY
    00A1: put_actor $PLAYER_ACTOR at $newposition[0] $newposition[1] $newposition[2]
    09C7: change_player $PLAYER_CHAR model_to #SPECIAL02
    if 003A:   $player_interior == $camspy_interior // (int)
    then
        009A: $CAMSPY = create_actor_pedtype 23 model #SPECIAL01 at $oldposition[0] $oldposition[1] $oldposition[2]
        0173: set_actor $CAMSPY Z_angle_to $old_angle
        060A: create_decision_maker_type 0 store_to $clean
        060B: set_actor $CAMSPY decision_maker_to $clean
    end
    0173: set_actor $PLAYER_ACTOR Z_angle_to $new_angle
    01B9: set_actor $PLAYER_ACTOR armed_weapon_to 43 // Cam
    0296: unload_special_actor 1
    0296: unload_special_actor 2
    $CAMSPY_STATUS=2
    0086: $current_area_status = $AREA_STATUS // (float)
    0086: $AREA_STATUS = $old_area_status // (float)
    0086: $old_area_status = $current_area_status // (float)
    $m_entrance_created=0
end

if and
0039:   $CAMSPY_STATUS == 1 // Player IS camspy
82D8:   not actor $PLAYER_ACTOR current_weapon == 43 // Cam
then 01B9: set_actor $PLAYER_ACTOR armed_weapon_to 43 // Cam
end

if and
0039:   $CAMSPY_STATUS == 2 // Player released camspy and has not taken it
003B:   $player_interior == $camspy_interior // (int)
then
    if 00F2:   actor $PLAYER_ACTOR near_actor $CAMSPY radius 1.0 1.0 1.0
    then
        01C2: remove_references_to_actor $CAMSPY // Like turning an actor into a random pedestrian 
        009B: destroy_actor $CAMSPY
        00BC: show_text_highpriority GXT 'CAMSPY1' time 4000 flag 1  // Picked up your CamSpy.
        017B: set_actor $PLAYER_ACTOR weapon 43 ammo_to 20 
        $CAMSPY_STATUS=0
    end
end

if and
0039:   $CAMSPY_STATUS == 2 // Player released camspy and has not taken it
02D8:   actor $PLAYER_ACTOR current_weapon == 43 // Cam
00E1:   player 0 pressed_key 15 // Enter/Intro 
then
    repeat
    wait 0 ms
    until 80E1: not player 0 pressed_key 15 // Wait for release key
    0086: $newposition[0] = $oldposition[0] // (float) 
    0086: $newposition[1] = $oldposition[1] // (float) 
    0086: $newposition[2] = $oldposition[2] // (float) 
    0086: $new_angle = $old_angle // (float) 
    00A0: store_actor $PLAYER_ACTOR position_to $oldposition[0] $oldposition[1] $oldposition[2]
    0172: $old_angle = actor $PLAYER_ACTOR Z_angle
    077E: get_active_interior_to $player_interior // the interior where player lies

    023C: load_special_actor 'TENPEN' as 1
    023C: load_special_actor 'moko' as 2
    038B: load_requested_models 
    repeat
    wait 0 ms
    until 023D: special_actor 1 loaded and 023D: special_actor 2 loaded

    04BB: select_interior $camspy_interior // the interior where camspy lies
    0860: link_actor $PLAYER_ACTOR to_interior $camspy_interior 
    04E4: unknown_refresh_game_renderer_at $newposition[0] $newposition[1]
    03CB: set_rendering_origin_at $newposition[0] $newposition[1] $newposition[2]
    009B: destroy_actor $CAMSPY
    00A1: put_actor $PLAYER_ACTOR at $newposition[0] $newposition[1] $newposition[2]
    09C7: change_player $PLAYER_CHAR model_to #SPECIAL01
    0173: set_actor $PLAYER_ACTOR Z_angle_to $new_angle
    if 003B:   $player_interior == $camspy_interior // (int) 
    then
        009A: $CAMSPY = create_actor_pedtype 23 model #SPECIAL02 at $oldposition[0] $oldposition[1] $oldposition[2]
        0173: set_actor $CAMSPY Z_angle_to $old_angle
        060A: create_decision_maker_type 0 store_to $clean
        060B: set_actor $CAMSPY decision_maker_to $clean
    end
    0296: unload_special_actor 1
    0296: unload_special_actor 2
    $CAMSPY_STATUS=1
    0086: $current_area_status = $AREA_STATUS // (float)
    0086: $AREA_STATUS = $old_area_status // (float)
    0086: $old_area_status = $current_area_status // (float)
    $m_entrance_created=0
end
0051: return  







// put your mods (threads) here
{==============================================================
MISSION SNIFFER
===============================================================}
:CI_MISSIONS_SNIFFER
thread "CI_SNIFF"
$MARKER_CARRINGTON_INSTITUTE = Marker.CreateIconAndSphere(42, $X_CARRINGTON_INSTITUTE, $Y_CARRINGTON_INSTITUTE, $Z_CARRINGTON_INSTITUTE)
repeat
wait $DEFAULT_WAIT_TIME
if and
    player.Defined($PLAYER_CHAR)
    $ONMISSION == 0
then
    if and
        00FF: actor $PLAYER_ACTOR sphere 0 in_sphere $X_CARRINGTON_INSTITUTE $Y_CARRINGTON_INSTITUTE $Z_CARRINGTON_INSTITUTE radius 1.0 1.0 2.0 on_foot
        Player.Controllable($PLAYER_CHAR)
        $TUT_MISSIONS_PASSED == 0
    then
        054C: use_GXT_table 'PD'
        0050: gosub @MAIN_MENU
        00BA: show_text_styled GXT s$MISSION time 1000 style 2
        0050: gosub @SUB_FADE_500MS 
        start_mission $M_NUM
    end
end
until $TUT_MISSIONS_PASSED == 1
marker.Disable($MARKER_CARRINGTON_INSTITUTE)
end_thread

//––––-Mission 0–––––
{==============================================================
GLOBAL INITIALIZATIONS
===============================================================}
:INITIAL
$TUT_MISSIONS_PASSED = 0
$ONMISSION = 0
     
$X_CARRINGTON_INSTITUTE = 1046.3639
$Y_CARRINGTON_INSTITUTE = 1015.7791
$Z_CARRINGTON_INSTITUTE = 11.0
//0629: change_integer_stat 225 to 999 // capacidad pulmonar al máximo jolines tío no la empalmeis hostia!

model.Load(#FCR900)
038B: load_requested_models

:INITIAL_LOAD
wait 0 ms
if
model.Available(#FCR900)
004D: jump_if_false @INITIAL_LOAD

0674: set_car_model #FCR900 numberplate "DARK_"
$darkbike = car.Create(#FCR900,1452.9561, -1837.4128, 13.5469)
end_thread






//––––-Mission 1–––––
{$INCLUDE M1.1 - dataDyne Defection.txt}

//––––-Mission 2–––––
{$INCLUDE M1.2 - dataDyne Research.txt}

//––––-Mission 3–––––
{$INCLUDE M1.3 - dataDyne Extraction.txt}

//––––-Mission 4–––––
{$INCLUDE M2.1 - Carrington Villa.txt}

//––––-Mission 5–––––
{$INCLUDE M3.1 - Chicago Stealth.txt}

//––––-Mission 6–––––
{$INCLUDE M3.2 - G5 Building.txt}

//––––-Mission 7–––––
{$INCLUDE M4.1 - Area 51 Infiltration.txt}

//––––-Mission 8–––––
{$INCLUDE M4.2 - Area 51 Rescue.txt}

//––––-Mission 9–––––
{$INCLUDE M4.3 - Area 51 Escape.txt}


//––––-External script 0–––––
// put your external scripts here