DEFINE MISSIONS 2
DEFINE MISSION 0 AT @INITIAL
DEFINE MISSION 1 AT @STEALTH


DEFINE EXTERNAL_SCRIPTS -1 // Use -1 in order not to compile AAA script
//DEFINE SCRIPT {NAME} AT {LABEL} @

DEFINE UNKNOWN_EMPTY_SEGMENT 0

DEFINE UNKNOWN_THREADS_MEMORY 0

{$VERSION 3.0.0000}
var
$PLAYER_CHAR: Player
end // var
03A4: name_thread 'MAIN'
01F0: set_max_wanted_level_to 6
0111: toggle_wasted_busted_check 0
00C0: set_current_time_hours_to 8 minutes_to 0
04E4: unknown_refresh_game_renderer_at 1049.9067 1031.3267
03CB: set_rendering_origin_at 1049.9067 1031.3267 10.685
0053: $PLAYER_CHAR = create_player #NULL at 1049.9067 1031.3267 10.685 //1453.0439 -1841.0349 13.5469
01F5: $PLAYER_ACTOR = create_player_actor $PLAYER_CHAR
07AF: $PLAYER_GROUP = player $PLAYER_CHAR group
0373: set_camera_directly_behind_player
01B6: set_weather 0
0001: wait 0 ms
087B: set_player $PLAYER_CHAR clothes_texture "PLAYER_FACE" model "HEAD" body_part 1
087B: set_player $PLAYER_CHAR clothes_texture "JEANSDENIM" model "JEANS" body_part 2
087B: set_player $PLAYER_CHAR clothes_texture "SNEAKERBINCBLK" model "SNEAKER" body_part 3
087B: set_player $PLAYER_CHAR clothes_texture "VEST" model "VEST" body_part 0
070D: rebuild_player $PLAYER_CHAR
01B4: toggle_player $PLAYER_CHAR can_move 0
016A: fade 0 time 0
04BB: select_interior 0
0629: change_integer_stat 181 to 4
016C: restart_if_wasted_at 2027.77 -1420.52 15.99 angle 137.0 town_number 0
016D: restart_if_busted_at 1550.68 -1675.49 14.51 angle 90.0 town_number 0
0180: set_on_mission_flag_to $ONMISSION // Note: your missions have to use the variable defined here
0004: $DEFAULT_WAIT_TIME = 250
03E6: remove_text_box

023C: load_special_actor 'reimu' as 1
038B: load_requested_models
repeat
wait 0 ms
until 023D:   special_actor 1 loaded
09C7: change_player $PLAYER_CHAR model_to #SPECIAL01
0296: unload_special_actor 1

wait 1000 ms
016A: fade 1 time 2000
01B4: toggle_player $PLAYER_CHAR can_move 1
0417: start_mission 0 // Initial
wait 0
// put your create_thread commands here
00D7: create_thread @CI_MISSIONS_SNIFFER

:MAIN_LOOP
0001: wait $DEFAULT_WAIT_TIME ms
0002: jump @MAIN_LOOP




{==============================================================
GLOBAL SUBROUTINES
===============================================================}
:SUB_FADE_500MS
0169: set_fade_color_RGB 0 0 0 
016A: fade 0 time 500 

:LITCAS_282
00D6: if 
016B:   fading 
004D: jump_if_false @LITCAS_306 
0001: wait 0 ms 
0002: jump @LITCAS_282 

:LITCAS_306
0051: return 
//------------------------
:TELEPORT
// Seleccionando Interior y colocándo al jugador en él
0050: gosub @SUB_FADE_500MS 
01B4: toggle_player $PLAYER_CHAR can_move 0
04BB: select_interior $ACTIVE_INTERIOR 
0860: link_actor $PLAYER_ACTOR to_interior $ACTIVE_INTERIOR 
04E4: unknown_refresh_game_renderer_at $PLAYER_X $PLAYER_Y 
03CB: set_rendering_origin_at $PLAYER_X $PLAYER_Y $PLAYER_Z 
//04F9: set_interior 1 color_to 0 
00A1: put_actor $PLAYER_ACTOR at $PLAYER_X $PLAYER_Y $PLAYER_Z 
0173: set_actor $PLAYER_ACTOR Z_angle_to $PLAYER_ANGLE 
0373: set_camera_directly_behind_player 
//02EB: restore_camera_with_jumpcut
0051: return
//------------------------
:FADEOUT
0001: wait 1000 ms 
016A: fade 1 time 1000 
01B4: set_player $PLAYER_CHAR can_move 1
0051: return
//------------------------
:JOANNA_RESPONSE
0209: $RAND = random_int_in_ranges 0 3
00D6: if
0039:   $RAND == 0
004D: jump_if_false @J01 
00BC: show_text_highpriority GXT 'MJO_1' time 4000 flag 1  // (Joanna) Received.
:J01
00D6: if
0039:   $RAND == 1
004D: jump_if_false @J02 
00BC: show_text_highpriority GXT 'MJO_2' time 4000 flag 1  // (Joanna) Understood.
:J02
00D6: if
0039:   $RAND == 2
004D: jump_if_false @J03 
00BC: show_text_highpriority GXT 'MJO_3' time 4000 flag 1  // (Joanna) Confirmed.
:J03
00D6: if
0039:   $RAND == 3
004D: jump_if_false @J04 
00BC: show_text_highpriority GXT 'MJO_4' time 4000 flag 1  // (Joanna) Affirmative.
:J04
0051: return  
//------------------------
:SELECT_DIFFICULTY
01B4: set_player $PLAYER_CHAR can_move 0
0512:  show_permanent_text_box 'MHLP01'
08D4: $SEL_DIF_MENU = create_panel_with_title 'MTBL01' position 29.0 145.0 width {220.0} 300.0 columns 1 interactive 1 background 1 alignment 1
08DB:  set_panel $SEL_DIF_MENU column 0 header s$MISSION data 'MDIF1' 'MDIF2' 'MDIF3' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY'// 'M3_2' 'M4_1' 'M4_2' 'M4_3' 'M5_1' 'M5_2'
repeat
wait 0 ms
until 00E1: player 0 pressed_key 16 
08D7: $choice = panel $SEL_DIF_MENU active_row
08DA: remove_panel $SEL_DIF_MENU
03E6: remove text box
if $choice == 0
then $MISSION_DIFFICULTY = 1          // Agent
end
if $choice == 1
then $MISSION_DIFFICULTY = 2          // Special Agent
end
if $choice == 2
then $MISSION_DIFFICULTY = 3          // Perfect Agent
end
0051: return  
//------------------------
:MISSION_BAR
if 00E1:   player 0 pressed_key 11    //~k~~CONVERSATION_YES~
then
    015D: set_gamespeed  0.001 // 0.3 o 1.0 slow motion
    01B4: set_player $PLAYER_CHAR can_move 0 

    038E: draw_box_position 400.0 270.0 size 400.0 220.0 RGBA 123 34 111 75
    038E: draw_box_position 400.0 185.0 size 340.0 1.2 RGBA 255 255 255 60

    033F: set_text_draw_letter_size 0.5 1.2
    0343: set_text_draw_linewidth 600.0 
    033E: set_draw_text_position 225.0 170.0 GXT s$MISSION    // Mission Name

    if 85AD:   not s$OBJ1 == 'DUMMY'
    then
        // Objective 1
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        033E: set_draw_text_position 225.0 200.0 GXT 'OBJ1'
        
        // Objective 1 Entry
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        033E: set_draw_text_position 315.0 200.0 GXT s$OBJ1
        
        // Objective 1 Status (Complete / Incomplete)
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        03E4: enable_text_draw_align_right 1
        if 0038: $MOBJ1 == 0
        then
        0340: set_text_draw_RGBA 250 164 96 255 
        033E: set_draw_text_position 540.0 210.0 GXT 'OBJINC'
        else
        0340: set_text_draw_RGBA 135 206 235 255 
        033E: set_draw_text_position 540.0 210.0 GXT 'OBJCMP' 
        end
    end 

    if 85AD:   not s$OBJ2 == 'DUMMY'
    then
        // Objective 2
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        033E: set_draw_text_position 225.0 225.0 GXT 'OBJ2'
        
        // Objective 2 Entry
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        033E: set_draw_text_position 315.0 225.0 GXT s$OBJ2
        
        // Objective 2 Status (Complete / Incomplete)
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        03E4: enable_text_draw_align_right 1
        if 0038: $MOBJ2 == 0
        then
        0340: set_text_draw_RGBA 250 164 96 255 
        033E: set_draw_text_position 540.0 235.0 GXT 'OBJINC'
        else
        0340: set_text_draw_RGBA 135 206 235 255 
        033E: set_draw_text_position 540.0 235.0 GXT 'OBJCMP' 
        end
    end 

    if 85AD:   not s$OBJ3 == 'DUMMY'
    then
        // Objective 3
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        033E: set_draw_text_position 225.0 250.0 GXT 'OBJ3'
        
        // Objective 3 Entry
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        033E: set_draw_text_position 315.0 250.0 GXT s$OBJ3
        
        // Objective 3 Status (Complete / Incomplete)
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        03E4: enable_text_draw_align_right 1
        if 0038: $MOBJ3 == 0
        then
        0340: set_text_draw_RGBA 250 164 96 255 
        033E: set_draw_text_position 540.0 260.0 GXT 'OBJINC'
        else
        0340: set_text_draw_RGBA 135 206 235 255 
        033E: set_draw_text_position 540.0 260.0 GXT 'OBJCMP' 
        end
    end 

    if 85AD:   not s$OBJ4 == 'DUMMY'
    then
        // Objective 4
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        033E: set_draw_text_position 225.0 275.0 GXT 'OBJ4'
        
        // Objective 4 Entry
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        033E: set_draw_text_position 315.0 275.0 GXT s$OBJ4
        
        // Objective 4 Status (Complete / Incomplete)
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        03E4: enable_text_draw_align_right 1
        if 0038: $MOBJ4 == 0
        then
        0340: set_text_draw_RGBA 250 164 96 255 
        033E: set_draw_text_position 540.0 285.0 GXT 'OBJINC'
        else
        0340: set_text_draw_RGBA 135 206 235 255 
        033E: set_draw_text_position 540.0 285.0 GXT 'OBJCMP' 
        end
    end 

    if 85AD:   not s$OBJ5 == 'DUMMY'
    then
        // Objective 5
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        033E: set_draw_text_position 225.0 300.0 GXT 'OBJ5'
        
        // Objective 5 Entry
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        033E: set_draw_text_position 315.0 300.0 GXT s$OBJ5
        
        // Objective 5 Status (Complete / Incomplete)
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        03E4: enable_text_draw_align_right 1
        if 0038: $MOBJ5 == 0
        then
        0340: set_text_draw_RGBA 250 164 96 255 
        033E: set_draw_text_position 540.0 310.0 GXT 'OBJINC'
        else
        0340: set_text_draw_RGBA 135 206 235 255 
        033E: set_draw_text_position 540.0 310.0 GXT 'OBJCMP' 
        end
    end 

    038E: draw_box_position 400.0 337.0 size 340.0 1.2 RGBA 255 255 255 60

    033F: set_text_draw_letter_size 0.4 1.0
    0343: set_text_draw_linewidth 600.0 
    03E4: enable_text_draw_align_right 1
    033E: set_draw_text_position 540.0 340.0 GXT 'OBTB01'     // Abort Mission

    repeat
    wait 0 ms
    until 80E1: not player 0 pressed_key 11
        
    repeat
    wait 0 ms
    until 00E1:   player 0 pressed_key 11

    015D: set_gamespeed  1.0 // 0.3 o 1.0 slow motion
    01B4: set_player $PLAYER_CHAR can_move 1
    03F0: enable_text_draw 0 

    repeat
    wait 0 ms
    until 80E1: not player 0 pressed_key 11
end
0051: return  






{==============================================================
MISSION SNIFFER
===============================================================}
// put your mods (threads) here
:CI_MISSIONS_SNIFFER
thread "CI_SNIFF"
$MARKER_CARRINGTON_INSTITUTE = Marker.CreateIconAndSphere(42, $X_CARRINGTON_INSTITUTE, $Y_CARRINGTON_INSTITUTE, $Z_CARRINGTON_INSTITUTE)
repeat
wait $DEFAULT_WAIT_TIME
if and
player.Defined($PLAYER_CHAR)
$ONMISSION == 0
then
    if and
        00FF: actor $PLAYER_ACTOR sphere 0 in_sphere $X_CARRINGTON_INSTITUTE $Y_CARRINGTON_INSTITUTE $Z_CARRINGTON_INSTITUTE radius 1.0 1.0 2.0 on_foot
        Player.Controllable($PLAYER_CHAR)
        $TUT_MISSIONS_PASSED == 0
    then
        054C: use_GXT_table 'PD'
        $ONMISSION = 1
        s$MISSION = 'M3_1' // Chicago: Stealth
        0050: gosub @SELECT_DIFFICULTY
        00BA: show_text_styled GXT 'M3_1' time 1000 style 2 // Chicago: Stealth
        01B4: toggle_player $PLAYER_CHAR can_move 0
        0050: gosub @SUB_FADE_500MS 
        start_mission 1
    end
end
until $TUT_MISSIONS_PASSED == 1
marker.Disable($MARKER_CARRINGTON_INSTITUTE)
end_thread

//––––-Mission 0–––––
{==============================================================
GLOBAL INITIALIZATIONS
===============================================================}
:INITIAL
$TUT_MISSIONS_PASSED = 0
$ONMISSION = 0
     
$X_CARRINGTON_INSTITUTE = 1046.3639
$Y_CARRINGTON_INSTITUTE = 1015.7791
$Z_CARRINGTON_INSTITUTE = 11.0
//0629: change_integer_stat 225 to 999 // capacidad pulmonar al máximo jolines tío no la empalmeis hostia!

model.Load(#FCR900)
038B: load_requested_models

:INITIAL_LOAD
wait 0 ms
if
model.Available(#FCR900)
004D: jump_if_false @INITIAL_LOAD

0674: set_car_model #FCR900 numberplate "DARK_"
$darkbike = car.Create(#FCR900,1452.9561, -1837.4128, 13.5469)
end_thread






//––––-Mission 1–––––
{==============================================================
CHICAGO: STEALTH
===============================================================}
// Mission wrapper
:STEALTH
thread 'M2_1'
gosub @STEALTH_MAIN
if wasted_or_busted
then
gosub @STEALTH_FAIL
end
gosub @STEALTH_CLEANUP
end_thread

//––––––––––––-
:STEALTH_MAIN
const         
{==============================================================================
Nomenclatura propuesta:     0-99: actors,vehicles 
                            100-199: flags y variables numéricas de uso general
                            200-299: objects or pickups
                            300-399: markers/spheres/checkpoints
                            400-499: decision makers, rutas y comportamientos
===============================================================================     }
    // ======= Obj1
    // 230@: Equipment Pickup
    // 310@: Marker above Pickup
    // ======= Obj2
    // 10@: Limousine
    // 320@: Marker above Limousine
    // 200@: Object attached to Limousine
    // 11@: Actor (G5 excecutive - Limousine) - 12@: Actor (driver)
    // ======= Obj3
    // 330@: Marker at escape point
    // 331@, 332@: Spheres in points
    // 100@ - 101@: flags de comprobación de explosivos
    // 210@, 211@: explosivos
    // ======= Obj4
    // 40@: Taxi
    // 340@: Marker above Taxi
    // 30@: Invisible actor for driving taxi
    // 103@: auxiliar number
    // ======= Obj5
    // 350@: Marker entrance
    // 102@: flag
    // 351@: Sphere entry point - 352@: Marker entry point
    // ======= General
    Helicopter = 0@
    // 300@: Helicopter Marker
    // 220@: Pickup
    // 301@: Pickup Marker
    // 302@: Sphere Inicial de la Misión - 303@: Su respectivo Checkpoint
    // 304@: Marker above bike - 112@: flag
    // 110@: variable auxiliar para estado de actores fbi
    // 111@: variable auxiliar para verificar qué actor descubrió al jugador
    // 2@, 3@, 4@: FBI Actors
    Bike = 1@
end                                                                                                                                                            // ======= Obj1

04BB: select_interior 0 
0860: link_actor $PLAYER_ACTOR to_interior 0 

// Wanted level of the mission
010D: set_player $PLAYER_CHAR wanted_level_to 0
010E: set_player $PLAYER_CHAR minimum_wanted_level_to 0
if 0038:   $MISSION_DIFFICULTY == 1  // Agent
then 01F0: set_max_wanted_level_to 3
end 
if 0038:   $MISSION_DIFFICULTY == 2  // Special Agent
then 01F0: set_max_wanted_level_to 4
end 
if 0038:   $MISSION_DIFFICULTY == 3  // Perfect Agent
then 01F0: set_max_wanted_level_to 6
end 


// Initialize Mission Time
00C0: set_current_time_hours_to 21 minutes_to 0

// Initialize global variables for Mission Status Bar
if 0038:   $MISSION_DIFFICULTY == 1  // Agent
then s$OBJ1='M31OB1'  // Retrieve Equipment
s$OBJ2='M31OB4'       // Create vehicular diversion
s$OBJ3='M31OB5'       // Gain access to building
s$OBJ4='DUMMY'
s$OBJ5='DUMMY'
end
if 0038:   $MISSION_DIFFICULTY == 2  // Special Agent
then s$OBJ1='M31OB1'  // Retrieve Equipment
s$OBJ2='M31OB3'       // Create vehicular diversion
s$OBJ3='M31OB4'       // Prepare escape route
s$OBJ4='M31OB5'       // Gain access to building
s$OBJ5='DUMMY'
end
if 0038:   $MISSION_DIFFICULTY == 3  // Perfect Agent
then s$OBJ1='M31OB1'  // Retrieve Equipment
s$OBJ2='M31OB2'       // Install Tracking Bug
s$OBJ3='M31OB3'       // Create vehicular diversion
s$OBJ4='M31OB4'       // Prepare escape route
s$OBJ5='M31OB5'       // Gain access to building
end

//Spawn Objects

//Spawn actor & Load models
04E4: unknown_refresh_game_renderer_at 204.9006 -235.7196
03CB: set_rendering_origin_at 204.9006 -235.7196 1.7812
00A1: put_actor $PLAYER_ACTOR at 204.9006 -235.7196 1.7812
0173: set_actor $PLAYER_ACTOR Z_angle_to 180.0
0373: set_camera_directly_behind_player 
    
0247: load_model #colt45
0247: load_model #maverick
038B: load_requested_models                                             
    
repeat
wait 0 ms
until 0248: model #colt45 available and 0248: model #maverick available
    
032B: 220@ = create_weapon_pickup #colt45 group 15 ammo 60 at 200.3756 -242.4597 1.5781
00A5: Helicopter = create_car #maverick at 207.006 -254.6053 1.5781
0175: set_car Helicopter Z_angle_to 0.0
0229: set_car Helicopter primary_color_to 0 secondary_color_to 54
0186: 300@ = create_marker_above_car Helicopter
0165: set_marker 300@ color_to 0 // Red
03DC: 301@ = create_marker_above_pickup 220@ 
0165: set_marker 301@ color_to 0 // Red
00BC: show_text_highpriority GXT 'M31_033' time 4000 flag 1  // Now it's time to take a ~b~new journey~w~.
0249: release_model #colt45
0249: release_model #maverick

0001: wait 1000 ms 
016A: fade 1 time 500
01B4: set_player $PLAYER_CHAR can_move 1 

33@=58000
    
repeat
wait 0 ms
    if 0214: pickup 220@ picked_up
    then 0164: disable_marker 301@
    end
        
    //-----------SKIP TRIP---------------
    if 00E1:   player 0 pressed_key 11  // ~k~~CONVERSATION_YES~
    then 0050: gosub @SUB_FADE_500MS
        00A1: put_actor $PLAYER_ACTOR at 1291.8507 -1238.179 13.5967
        0173: set_actor $PLAYER_ACTOR Z_angle_to 270.0
        0373: set_camera_directly_behind_player 
        00A6: destroy_car Helicopter
        0247: load_model #MAVERICK
        038B: load_requested_models
        repeat
        wait 0 ms
        until 0248:   model #MAVERICK available 
        00A5: Helicopter = create_car #MAVERICK at 1292.9449 -1244.9403 13.5469
        0249: release_model #MAVERICK
        0175: set_car Helicopter Z_angle_to 0.0
        0229: set_car Helicopter primary_color_to 0 secondary_color_to 54
        0164: disable_marker 300@
        0164: disable_marker 301@
        0050: gosub @FADEOUT
        03E6: remove_text_box 
        jump @STEALTH_START
    else
        if and
        0029:   33@ >= 60000
        88FE: not text_box_displayed
        // 0039: $M1_1_ATTEMPTS == 1
        then
        03E5: show_text_box 'MSKIP' // You can ~b~skip~w~ the Intro by pressing ~k~~CONVERSATION_YES~.
        33@=0
        end
    end
until 00DB: actor $PLAYER_ACTOR in_car Helicopter
    
0164: disable_marker 300@
0164: disable_marker 301@
041E: set_radio_station 11 // turn off radio
03BC: 302@ = create_sphere_at 1293.002 -1245.2837 13.5469 radius 10.0 
018A: 303@ = create_checkpoint_at 1293.002 -1245.2837 13.5469
0165: set_marker 303@ color_to 0 // Red
    
repeat
wait 0 ms
    if 0039: $OTHER_TASKS_STATUS == 0
    then 00BC: show_text_highpriority GXT 'M31_001' time 4000 flag 1  // (Carrington) We suspect the G5 corporation is just a front for dataDyne and whoever else is involved in this plot.
    $OTHER_TASKS_STATUS = 1
    32@=0
    end
        
    if and
    0039: $OTHER_TASKS_STATUS == 1
    0029: 32@ >= 4000
    then 00BC: show_text_highpriority GXT 'M31_002' time 4000 flag 1  // (Carrington) I know Cassandra de Vries is going to be here, but I need to know who else is attending and what they discuss.
    $OTHER_TASKS_STATUS = 2
    32@=0
    end
        
    if and
    0039: $OTHER_TASKS_STATUS == 2
    0029: 32@ >= 4000
    then 00BC: show_text_highpriority GXT 'M31_003' time 4000 flag 1  // (Carrington) By the time you get out, the help I requested should have arrived.
    $OTHER_TASKS_STATUS = 3
    32@=0
    end
                
    if and
    0039: $OTHER_TASKS_STATUS == 3
    0029: 32@ >= 4000
    then 00BC: show_text_highpriority GXT 'M31_004' time 4000 flag 1  // (Joanna) Just who is this help you keep referring to?
    $OTHER_TASKS_STATUS = 4
    32@=0
    end
                
    if and
    0039: $OTHER_TASKS_STATUS == 4
    0029: 32@ >= 4000
    then 00BC: show_text_highpriority GXT 'M31_005' time 4000 flag 1  // (Carrington) Not now, Joanna, time for radio silence. Good luck!
    $OTHER_TASKS_STATUS = 5
    32@=0
    end
                
    if and
    0039: $OTHER_TASKS_STATUS == 5
    0029: 32@ >= 4000
    then 00BC: show_text_highpriority GXT 'M31_006' time 4000 flag 1  // (Joanna) Thanks a lot.
    $OTHER_TASKS_STATUS = 6
    end
        
    //-----------SKIP TRIP---------------
    if 00E1:   player 0 pressed_key 11  // ~k~~CONVERSATION_YES~
    then 0050: gosub @SUB_FADE_500MS
        if 00DB:   actor $PLAYER_ACTOR in_car Helicopter 
        then 0362: remove_actor $PLAYER_ACTOR from_car_and_place_at 1291.8507 -1238.179 13.5967
        else 00A1: put_actor $PLAYER_ACTOR at 1291.8507 -1238.179 13.5967
        end
        0173: set_actor $PLAYER_ACTOR Z_angle_to 270.0
        0373: set_camera_directly_behind_player
        00A6: destroy_car Helicopter
        0247: load_model #MAVERICK
        038B: load_requested_models
        repeat
        wait 0 ms
        until 0248:   model #MAVERICK available 
        00A5: Helicopter = create_car #MAVERICK at 1292.9449 -1244.9403 13.5469
        0249: release_model #MAVERICK
        0175: set_car Helicopter Z_angle_to 0.0
        0229: set_car Helicopter primary_color_to 0 secondary_color_to 54
        03BD: destroy_sphere 302@
        0164: disable_marker 303@
        0050: gosub @FADEOUT
        03E6: remove_text_box 
        jump @STEALTH_START
    else
        if and
        0029:   33@ >= 60000
        88FE: not text_box_displayed
        // 0039: $M1_1_ATTEMPTS == 1
        then
        03E5: show_text_box 'MSKIP' // You can ~b~skip~w~ the Intro by pressing ~k~~CONVERSATION_YES~.
        33@=0
        end
    end
until 00FE: actor $PLAYER_ACTOR sphere 0 in_sphere 1293.002 -1245.2837 13.5469 radius 10.0 10.0 10.0 

0050: gosub @SUB_FADE_500MS
03BD: destroy_sphere 302@ 
0164: disable_marker 303@
00AB: put_car Helicopter at 1292.9449 -1244.9403 13.5469
0175: set_car Helicopter Z_angle_to 0.0
0362: remove_actor $PLAYER_ACTOR from_car_and_place_at 1291.8507 -1238.179 13.5967
0173: set_actor $PLAYER_ACTOR Z_angle_to 270.0

:STEALTH_START
0247: load_model #colt45
0247: load_model #fcr900
038B: load_requested_models                                             
repeat
wait 0 ms
until 0248: model #colt45 available and 0248: model #fcr900 available
01B2: give_actor $PLAYER_ACTOR weapon 22 ammo 60
01B9: set_actor $PLAYER_ACTOR armed_weapon_to 22 
00A5: Bike = create_car #fcr900 at 1332.8163 -1238.3218 13.5469
0175: set_car Bike Z_angle_to 306.7328
053F: set_car Bike tires_vulnerability 0
//0674: set_car_model #fcr900 numberplate "PD_DARK_"
0249: release_model #colt45
0249: release_model #fcr900
0186: 304@ = create_marker_above_car Bike 
0165: set_marker 304@ color_to 0 // Red
00C0: set_current_time_hours_to 0 minutes_to 0
01B6: set_weather 8 // Rainy (Countryside) 

0050: gosub @FADEOUT

//Debug

// Initializing
060A: create_decision_maker_type 0 store_to 400@    // clean behavior for special actors
$MOBJ1=0
$MOBJ2=0
$MOBJ3=0
$MOBJ4=0
$MOBJ5=0
$OBJ1_STATUS=0
$OBJ2_STATUS=0
$OBJ3_STATUS=0
$OBJ4_STATUS=0
$OBJ5_STATUS=0
$AREA_STATUS=0
$OTHER_TASKS_STATUS=0
$ALLOBJECTIVES=0
$globaltimer=0
08E7: disable_entrance_markers 1

{==============================================================
CHICAGO: STEALTH - MAIN LOOP
===============================================================}
repeat
wait 0 ms
    //--ENTRAR/SALIR DE LAS ZONAS--
    //0050: gosub @STEALTH_CHECK_DOORS
    
    //--AGENT--
    if
    0038: $MISSION_DIFFICULTY == 1
    then        
        if 0038:   $MOBJ1 == 0
        then 0050: gosub @M3_1_OBJECTIVE1  // Retrieve drop point equipment
        end

        if 0038:   $MOBJ2 == 0
        then 0050: gosub @M3_1_OBJECTIVE4  // Create vehicular diversion
        end

        if 0038:   $MOBJ3 == 0
        then 0050: gosub @M3_1_OBJECTIVE5  // Gain entry to G5 building
        end
    end
     
    //--SPECIAL AGENT--
    if
    0038: $MISSION_DIFFICULTY == 2
    then        
        if 0038:   $MOBJ1 == 0
        then 0050: gosub @M3_1_OBJECTIVE1  // Retrieve drop point equipment
        end

        if 0038:   $MOBJ2 == 0
        then 0050: gosub @M3_1_OBJECTIVE3  // Prepare escape route
        end
     
        if 0038:   $MOBJ3 == 0
        then 0050: gosub @M3_1_OBJECTIVE4  // Create vehicular diversion
        end

        if 0038:   $MOBJ4 == 0
        then 0050: gosub @M3_1_OBJECTIVE5  // Gain entry to G5 building
        end
    end
 
    //--PERFECT AGENT--
    if
    0038: $MISSION_DIFFICULTY == 3
    then        
        if 0038:   $MOBJ1 == 0
        then 0050: gosub @M3_1_OBJECTIVE1  // Retrieve drop point equipment
        end

        if 0038:   $MOBJ2 == 0
        then 0050: gosub @M3_1_OBJECTIVE2  // Attach tracer to limousine
        end
    
        if 0038:   $MOBJ3 == 0
        then 0050: gosub @M3_1_OBJECTIVE3  // Prepare escape route
        end
     
        if 0038:   $MOBJ4 == 0
        then 0050: gosub @M3_1_OBJECTIVE4  // Create vehicular diversion
        end

        if 0038:   $MOBJ5 == 0
        then 0050: gosub @M3_1_OBJECTIVE5  // Gain entry to G5 building
        end
    end

    //--CONDICIÓN DE VICTORIA--
    if and
    0038: $MISSION_DIFFICULTY == 1  // Agent
    0038: $MOBJ1 == 1   // Retrieve drop point equipment
    0038: $MOBJ2 == 1   // Create vehicular diversion
    0038: $MOBJ3 == 1   // Gain entry to G5 building
    then
    0050: gosub @STEALTH_OUTRO // cutscene
    $ALLOBJECTIVES = 1
    end
    if and
    0038: $MISSION_DIFFICULTY == 2  // Special Agent
    0038: $MOBJ1 == 1   // Retrieve drop point equipment
    0038: $MOBJ2 == 1   // Prepare escape route
    0038: $MOBJ3 == 1   // Create vehicular diversion
    0038: $MOBJ4 == 1   // Gain entry to G5 building
    then
    0050: gosub @STEALTH_OUTRO // cutscene
    $ALLOBJECTIVES = 1
    end
    if and
    0038: $MISSION_DIFFICULTY == 3  // Perfect Agent
    0038: $MOBJ1 == 1   // Retrieve drop point equipment
    0038: $MOBJ2 == 1   // Attach tracer to limousine
    0038: $MOBJ3 == 1   // Prepare escape route
    0038: $MOBJ4 == 1   // Create vehicular diversion
    0038: $MOBJ5 == 1   // Gain entry to G5 building
    then
    0050: gosub @STEALTH_OUTRO // cutscene
    $ALLOBJECTIVES = 1
    end

    //ATENDER OTRAS TAREAS INDEPENDIENTES DE LOS OBJETIVOS
    0050: gosub @M3_1_OTHER_TASKS

    //--CONDICIÓN DE FRACASO--    
    00D6: if 
    8038: not $OTHER_TASKS_STATUS == 99
    004D: jump_if_false @STEALTH_FAIL 

    //DESPLEGAR BARRA DE STATUS DE MISIÓN TRAS PULSACIÓN DE TECLA ESPECIAL -OBJETIVOS COMPLETADOS Y OBJETIVOS FALTANTES-
    0050: gosub @MISSION_BAR
    
until 0038: $ALLOBJECTIVES == 1
0002: jump @STEALTH_PASSED 

{==============================================================
Maquina de estado de los teletransportadores
===============================================================}
:STEALTH_CHECK_DOORS   
0051: return

{=================================================================
OTHER TASKS
==================================================================}
:M3_1_OTHER_TASKS
if 0039:   112@ == 0
then
    if 00DB:   actor $PLAYER_ACTOR in_car Bike
    then 0164: disable_marker 304@
        112@=1
    end
end

if and
0038: $OTHER_TASKS_STATUS == 0
0038: $MISSION_DIFFICULTY == 3 // Perfect Agent
0029: 33@ >= 240000 // 4 minutes
then
    0247: load_model #wmybu
    038B: load_requested_models
    repeat
    wait 0 ms
    until 0248: model #wmybu available
    009A: 11@ = create_actor_pedtype 4 model #wmybu at 1767.1794 -1397.8701 15.7578
    05CA: AS_actor 11@ enter_car 10@ passenger_seat 2 10000 ms // 0: copiloto, 1: left back, 2: right back 3: crash
    $OTHER_TASKS_STATUS=1 
end

if
0038: $OTHER_TASKS_STATUS == 1
then
    if
    00DB:   actor 11@ in_car 10@
    then
        05D6: clear_scmpath
        05D7: add_point_to_scmpath 1778.7867 -1391.2285 15.4198
        05D7: add_point_to_scmpath 1749.7369 -1440.3352 13.0248
        07E7: AS_assign_scmpath_to_actor 11@ in_car 10@ speed 20.0 flags 0 0 5
        $OTHER_TASKS_STATUS=2
    end
end

if
0038: $OTHER_TASKS_STATUS == 2
then
    if and
    0100:   actor 11@ in_sphere 1749.7369 -1440.3352 13.0248 radius 10.0 10.0 10.0 sphere 0 in_car
    8038: not $OBJ2_STATUS == 2 // Object not attached to Limousine
    then
        00BC: show_text_highpriority GXT 'M31_020' time 4000 flag 1  // dataDyne limo has escaped.
        $OTHER_TASKS_STATUS=99  // Mission Failed
    else
        if and
        0038: $OTHER_TASKS_STATUS == 2
        0100:   actor 11@ in_sphere 1749.7369 -1440.3352 13.0248 radius 10.0 10.0 10.0 sphere 0 in_car
        0038:   $OBJ2_STATUS == 2 // Object attached to Limousine
        then
            01C2: remove_references_to_actor 11@ // Like turning an actor into a random pedestrian 
            01C3: remove_references_to_car 10@ // Like turning a car into any random car
            $OTHER_TASKS_STATUS=3         
        end
    end
end

// Colocar agentes
if 0039:   110@ == 0
then
    0247: load_model #FBI
    0247: load_model #DESERT_EAGLE
    038B: load_requested_models
    repeat
    wait 0
    until 0248:   model #FBI available and 0248:   model #DESERT_EAGLE available
    
    009A: 2@ = create_actor_pedtype 4 model #FBI at 1541.6306 -1696.4427 13.5469
    0173: set_actor 2@ Z_angle_to 0.0 
    01B2: give_actor 2@ weapon 24 ammo 60 // Load the weapon model before using this
    01B9: set_actor 2@ armed_weapon_to 24
    077A: set_char_relationship 2@ relationship 3 to_ped_type 0 // dislike
    
    if or
    0038: $MISSION_DIFFICULTY == 2  // Special Agent
    0038: $MISSION_DIFFICULTY == 3  // Perfect Agent
    then
        009A: 3@ = create_actor_pedtype 4 model #FBI at 1466.5125 -1503.0657 13.5469 //1737.5032 -1397.6851 15.7578 (vieja ubicacion cerca de la limusina)
        0173: set_actor 3@ Z_angle_to 0.0 
        01B2: give_actor 3@ weapon 24 ammo 60 // Load the weapon model before using this
        01B9: set_actor 3@ armed_weapon_to 24
        077A: set_char_relationship 3@ relationship 3 to_ped_type 0 // dislike
    end
    
    if 0038: $MISSION_DIFFICULTY == 3  // Perfect Agent
    then
        009A: 4@ = create_actor_pedtype 4 model #FBI at 1406.1124 -1320.1965 8.5651
        0173: set_actor 4@ Z_angle_to 270.0 
        01B2: give_actor 4@ weapon 24 ammo 60 // Load the weapon model before using this
        01B9: set_actor 4@ armed_weapon_to 24
        077A: set_char_relationship 4@ relationship 3 to_ped_type 0 // dislike
    end
    0249: release_model #FBI
    0249: release_model #DESERT_EAGLE
    110@=1
end

// Un agente ha descubierto al jugador
if 0039:   110@ == 1
then
    if and
    09ED:   actor $PLAYER_ACTOR is_within_field_of_view_actor 2@
    8118:   not actor 2@ dead
    then
        00BC: show_text_highpriority GXT 'M31_008' time 4000 flag 1  // (FBI Agent) Base, we have an intruder!
        111@=2
        110@=2
        32@=0
    end
    if or
    0038: $MISSION_DIFFICULTY == 2  // Special Agent
    0038: $MISSION_DIFFICULTY == 3  // Perfect Agent
    then
        if and
        09ED:   actor $PLAYER_ACTOR is_within_field_of_view_actor 3@
        8118:   not actor 3@ dead
        then
            00BC: show_text_highpriority GXT 'M31_008' time 4000 flag 1  // (FBI Agent) Base, we have an intruder!
            111@=3
            110@=2
            32@=0
        end
    end
    if 0038: $MISSION_DIFFICULTY == 3  // Perfect Agent
    then
        if and
        09ED:   actor $PLAYER_ACTOR is_within_field_of_view_actor 4@
        8118:   not actor 4@ dead
        then
            00BC: show_text_highpriority GXT 'M31_008' time 4000 flag 1  // (FBI Agent) Base, we have an intruder!
            111@=4
            110@=2
            32@=0
        end
    end
end

// Cancelar búsqueda del jugador
if and
0039:   110@ == 2
8029:   not 32@ >= 8000 
then
    if 0118:   actor 2@ dead
    then 110@=1
    end
    if or
    0038: $MISSION_DIFFICULTY == 2  // Special Agent
    0038: $MISSION_DIFFICULTY == 3  // Perfect Agent
    then
        if 0118:   actor 3@ dead
        then 110@=1
        end
    end
    if 0038: $MISSION_DIFFICULTY == 3  // Perfect Agent
    then
        if 0118:   actor 4@ dead
        then 110@=1
        end
    end
end

// Establecer búsqueda al jugador - parte 1
if and
0039:   110@ == 2
0029:   32@ >= 8000 
then
    00BC: show_text_highpriority GXT 'M31_009' time 4000 flag 1  // (CIA and FBI - Communications) Attention all personnel. Locate and apprehend fugitive.
    110@=3
    32@=0
    077A: set_char_relationship 2@ relationship 4 to_ped_type 0 // hate
    if or
    0038: $MISSION_DIFFICULTY == 2  // Special Agent
    0038: $MISSION_DIFFICULTY == 3  // Perfect Agent
    then 077A: set_char_relationship 3@ relationship 4 to_ped_type 0 // hate
    end
    if 0038: $MISSION_DIFFICULTY == 3  // Perfect Agent
    then 077A: set_char_relationship 4@ relationship 4 to_ped_type 0 // hate
    end
end

// Establecer búsqueda al jugador - parte 2
if and
0039:   110@ == 3
0029:   32@ >= 3000 
then
    if 0038: $MISSION_DIFFICULTY == 1  // Agent
    then 010D: set_player $PLAYER_CHAR wanted_level_to 2
    end
    if 0038: $MISSION_DIFFICULTY == 2  // Special Agent
    then 010D: set_player $PLAYER_CHAR wanted_level_to 3
    end
    if 0038: $MISSION_DIFFICULTY == 3  // Perfect Agent
    then 010D: set_player $PLAYER_CHAR wanted_level_to 5
    end
    110@=4
end

if 0039:   102@ == 0
then
    if and
    0038: $MISSION_DIFFICULTY == 1  // Agent
    0038: $MOBJ1 == 1   // Retrieve drop point equipment
    0038: $MOBJ2 == 1   // Create vehicular diversion
    then
    018A: 350@ = create_checkpoint_at 1550.0499 -1626.5929 13.3828
    0165: set_marker 350@ color_to 0
    102@ = 1
    end
            
    if and
    0038: $MISSION_DIFFICULTY == 2  // Special Agent
    0038: $MOBJ1 == 1   // Retrieve drop point equipment
    0038: $MOBJ2 == 1   // Prepare escape route
    0038: $MOBJ3 == 1   // Create vehicular diversion
    then
    018A: 350@ = create_checkpoint_at 1550.0499 -1626.5929 13.3828
    0165: set_marker 350@ color_to 0
    102@ = 1
    end
            
    if and
    0038: $MISSION_DIFFICULTY == 3  // Perfect Agent
    0038: $MOBJ1 == 1   // Retrieve drop point equipment
    0038: $MOBJ2 == 1   // Attach tracer to limousine
    0038: $MOBJ3 == 1   // Prepare escape route
    0038: $MOBJ4 == 1   // Create vehicular diversion
    then
    018A: 350@ = create_checkpoint_at 1550.0499 -1626.5929 13.3828
    0165: set_marker 350@ color_to 0
    102@ = 1
    end
end
0051: return    

{=================================================================
OBJECTIVE 1 - Retrieve drop point equipment
==================================================================}
:M3_1_OBJECTIVE1
if and
0038: $OBJ1_STATUS == 0
00DB: actor $PLAYER_ACTOR in_car Bike
then
    0213: 230@ = create_pickup #briefcase type 3 at 1410.5262 -1304.6208 9.3882
    03DC: 310@ = create_marker_above_pickup 230@
    00BC: show_text_highpriority GXT 'M31_012' time 4000 flag 1  // (Carrington) The only place we could secure the equipment was a storm drain.
    32@=0
    $OBJ1_STATUS=1
end

if and
0038: $OBJ1_STATUS == 1
0029:   32@ >= 4000 
then
    0050: gosub @JOANNA_RESPONSE      // (Joanna) Received.
    $OBJ1_STATUS=2
end

if 0038: $OBJ1_STATUS == 2
then
    if 0214:   pickup 230@ picked_up 
    then
        0164: disable_marker 310@
        00BA: show_text_styled GXT 'MOBJ1' time 2000 style 2      //Objective 1: Completed
        00BC: show_text_highpriority GXT 'M31_021' time 3000 flag 1  // Picked up a Remote Mine.
        $OBJ1_STATUS=3
        32@=0
    end
end

if and
0038: $OBJ1_STATUS == 3
0029:   32@ >= 3000 
then
    00BC: show_text_highpriority GXT 'M31_022' time 3000 flag 1  // Picked up a Reprogrammer.
    $MOBJ1=1
    $OBJ1_STATUS=4
end
0051: return

{=================================================================
OBJECTIVE 2 - Attach tracer to limousine
==================================================================}
:M3_1_OBJECTIVE2
if
0038: $OBJ2_STATUS == 0
then
    0247: load_model #stretch
    0247: load_model #wmych
    038B: load_requested_models
    repeat
    wait 0 ms
    until 0248: model #stretch available and 0248: model #wmych available
    00A5: 10@ = create_car #stretch at 1766.3531 -1391.6848 15.7578
    0175: set_car 10@ Z_angle_to 270.0
    0186: 320@ = create_marker_above_car 10@
    0165: set_marker 320@ color_to 0 // Red
    0129: 12@ = create_actor_pedtype 4 model #wmych in_car 10@ driverseat
    0249: release_model #stretch
    0249: release_model #wmych
    33@=0
    $OBJ2_STATUS = 1
end

if and      
0038: $OBJ2_STATUS == 1
00FE: actor $PLAYER_ACTOR 0 near_point_3d 1761.7543 -1391.7382 15.7578 radius 1.0 1.0 1.0
then
    if
    00E1:   player 0 pressed_key 15 // Enter
    then
        03E6: remove_text_box
        0164: disable_marker 320@
        0107: 200@ = create_object #mobile1993b at 1833.36 -1995.45 12.5
        0681: attach_object 200@ to_car 10@ with_offset 1.0 1.0 1.0 rotation 90.0 0.0 0.0

        04ED: load_animation "crib" 
        repeat
        wait 0 ms
        until 04EE: animation "crib" loaded
        0605: actor $PLAYER_ACTOR perform_animation_sequence "CRIB_Use_Switch" IFP_file "crib" 10.0 loop 0 0 0 0 time -1
        04EF: release_animation "crib"

        00BA: show_text_styled GXT 'MOBJ2' time 2000 style 2            // Objective 2: Completed
        00BC: show_text_highpriority GXT 'M31_023' time 4000 flag 1     // Tracer Bug placed correctly.
        $MOBJ2=1
        $OBJ2_STATUS=2
    else
        if and
        0029:   32@ >= 10000
        88FE: not text_box_displayed
        then
        03E5: show_text_box 'M31_024' // Press ~r~~k~~VEHICLE_ENTER_EXIT~~w~ to install a ~r~Tracer~w~ to limousine.
        32@=0
        end
    end          
end
0051: return

{=================================================================
OBJECTIVE 3 - Prepare escape route
==================================================================}
:M3_1_OBJECTIVE3
if and
0038:   $OBJ3_STATUS == 0
0038:   $MOBJ1 == 1 // Retrieve drop point equipment
then
    018A: 330@ = create_checkpoint_at 1546.4314 -1675.5874 13.5623
    0165: set_marker 330@ color_to 0 // Red
    03BC: 331@ = create_sphere_at 1546.9165 -1679.9077 13.56 radius 1.0 
    03BC: 332@ = create_sphere_at 1546.9818 -1671.3217 13.5677 radius 1.0 
    $OBJ3_STATUS=1
end

if
0038:   $OBJ3_STATUS == 1
then
    if and
    0039:   100@ == 0
    00FF:   actor $PLAYER_ACTOR sphere 0 in_sphere 1546.9165 -1679.9077 13.56 radius 1.0 1.0 1.0 on_foot 
    then
        if 00E1:   player 0 pressed_key 15 // Enter
        then
            03E6: remove_text_box
            03BD: destroy_sphere 331@ 

            04ED: load_animation "bd_fire" 
            repeat
            wait 0 ms
            until 04EE: animation "bd_fire" loaded
            0605: actor $PLAYER_ACTOR perform_animation_sequence "wash_up" IFP_file "bd_fire" 10.0 loop 0 0 0 0 time 2000
            04EF: release_animation "bd_fire"
            0173: set_actor $PLAYER_ACTOR Z_angle_to 0.0
            
            029B: 210@ = init_object #dynamite at 1546.91 -1679.26 13.5894
            0453: set_object 210@ XY_rotation 0.0 0.0 angle 0.0
            
            wait 2000 ms
            00BC: show_text_highpriority GXT 'M31_025' time 4000 flag 1     // Mine placed correctly.
            100@=1
        else
            if and
            0029:   32@ >= 10000
            88FE: not text_box_displayed
            then
            03E5: show_text_box 'M31_026' // Press ~r~~k~~VEHICLE_ENTER_EXIT~~w~ to install an ~r~Explosive~w~.
            32@=0
            end
        end          
    end

    if
    0039:   101@ == 0
    00FF:   actor $PLAYER_ACTOR sphere 0 in_sphere 1546.9818 -1671.3217 13.5677 radius 1.0 1.0 1.0 on_foot 
    then
        if 00E1:   player 0 pressed_key 15 // Enter
        then
            03E6: remove_text_box
            03BD: destroy_sphere 332@ 

            04ED: load_animation "bd_fire" 
            repeat
            wait 0 ms
            until 04EE: animation "bd_fire" loaded
            0605: actor $PLAYER_ACTOR perform_animation_sequence "wash_up" IFP_file "bd_fire" 10.0 loop 0 0 0 0 time 2000
            04EF: release_animation "bd_fire"
            0173: set_actor $PLAYER_ACTOR Z_angle_to 180.0
            
            029B: 211@ = init_object #dynamite at 1546.91 -1671.96 13.5894
            0453: set_object 211@ XY_rotation 0.0 0.0 angle 180.0 

            wait 2000 ms
            00BC: show_text_highpriority GXT 'M31_025' time 4000 flag 1     // Mine placed correctly.
            101@=1
        else
            if and
            0029:   32@ >= 10000
            88FE: not text_box_displayed
            then
            03E5: show_text_box 'M31_026' // Press ~r~~k~~VEHICLE_ENTER_EXIT~~w~ to access ~r~Machine Control Panel~w~.
            32@=0
            end
        end          
    end
    
    if and
    0039:   100@ == 1
    0039:   101@ == 1
    then
        if 0039: $MISSION_DIFFICULTY == 2 // Special Agent
        then
            00BA: show_text_styled GXT 'MOBJ2' time 2000 style 2            // Objective 2: Completed
            $MOBJ2=1
        else // Perfect Agent
            00BA: show_text_styled GXT 'MOBJ3' time 2000 style 2            // Objective 3: Completed
            $MOBJ3=1
        end
        $OBJ3_STATUS=2
        0164: disable_marker 330@
    end
end

0051: return

{=================================================================
OBJECTIVE 4 - Create vehicular diversion
==================================================================}
:M3_1_OBJECTIVE4    
if and
0038: $OBJ4_STATUS == 0
0038: $MOBJ1 == 1 // Retrieve drop point equipment
then
    0247: load_model #cabbie // Taxi
    038B: load_requested_models
    repeat
    wait 0 ms
    until 0248: model #cabbie available
    00A5: 40@ = create_car #cabbie at 1295.0421 -985.2388 30.0
    0175: set_car 40@ Z_angle_to 180.0
    0186: 340@ = create_marker_above_car 40@ 
    0165: set_marker 340@ color_to 0 // Red
    0519: set_car 40@ locked 1 
    //020A: set_car 40@ door_status_to 4
    //0508: car 40@ close_all_doors 
    0249: release_model #cabbie
    $OBJ4_STATUS=1
end

if and
0038: $OBJ4_STATUS == 1
00FE:   actor $PLAYER_ACTOR sphere 0 in_sphere 1289.5334 -987.2733 32.6953 radius 10.0 10.0 10.0 
then
    00BC: show_text_highpriority GXT 'M31_013' time 4000 flag 1  // (Grimshaw) Reprogram that taxi – it's a Mark 2 and should be compatible.
    32@=0
    $OBJ4_STATUS=2
end

if and
0038: $OBJ4_STATUS == 2
0029:   32@ >= 4000 
then
    0050: gosub @JOANNA_RESPONSE // (Johanna) Understood.
    $OBJ4_STATUS=3
end

if
0038: $OBJ4_STATUS == 3
then
    103@=4.0
    if and
    0202:   actor $PLAYER_ACTOR near_car 40@ radius 103@ 103@ flag 1
    80DB:   not actor $PLAYER_ACTOR in_car 40@ 
    then
        if
        00E1:   player 0 pressed_key 4 // TAB - Answer Phone - Secondary Weapon
        then
            03E6: remove_text_box
            00BC: show_text_highpriority GXT 'M31_027' time 4000 flag 1     // Accessing hovercab navigation systems.
            32@=0
            $OBJ4_STATUS=4
        else
            if and
            0029:   32@ >= 10000
            88FE: not text_box_displayed
            then
            03E5: show_text_box 'M31_028' // Press ~r~~k~~PED_ANSWER_PHONE~/ ~k~~PED_FIREWEAPON_ALT~~w~ to use ~r~Reprogrammer~w~.
            32@=0
            end
        end          
    end
end

if
0038: $OBJ4_STATUS == 4
then
    if
    0029:   32@ >= 8000 
    then
        00BC: show_text_highpriority GXT 'M31_029' time 4000 flag 1  // Virus downloaded successfully.
        0519: set_car 40@ locked 0

        0247: load_model #male01
        038B: load_requested_models
        repeat
        wait 0 ms
        until 0248: model #male01 available
        0129: 30@ = create_actor_pedtype 4 model #male01 in_car 40@ driverseat 
        0337: set_actor 30@ visibility 0 
        0249: release_model #male01

        05D6: clear_scmpath
        05D7: add_point_to_scmpath 1289.4126 -992.5521 32.6953//1288.2277 -988.9572 32.6953
        05D7: add_point_to_scmpath 1287.6422 -1000.4534 31.8775
        05D7: add_point_to_scmpath 1288.6963 -1020.6276 31.1894
        05D7: add_point_to_scmpath 1302.7998 -1039.6881 30.6722
        05D7: add_point_to_scmpath 1335.4019 -1036.1852 27.5031
        05D7: add_point_to_scmpath 1352.1958 -1245.4927 13.3915
        07E7: AS_assign_scmpath_to_actor 30@ in_car 40@ speed 100.0 flags 0 0 5
        09D4: suspend_wanted_level 
        $OBJ4_STATUS = 5
        110@=4 // Cancel FBI informants activity
    end
    
    if
    8202:   not actor $PLAYER_ACTOR near_car 40@ radius 103@ 103@ flag 1 
    then
    00BC: show_text_highpriority GXT 'M31_030' time 4000 flag 1  // Connection to hovercab broken.
    $OBJ4_STATUS=3
    end
end

if
0038: $OBJ4_STATUS == 5
then
    if
    0100: actor 30@ in_sphere 1352.1958 -1245.4927 13.3915 radius 4.0 4.0 4.0 sphere 0 in_car
    then
        if 0038: $MISSION_DIFFICULTY == 1 // Agent
        then
            00BA: show_text_styled GXT 'MOBJ2' time 2000 style 2      //Objective 2: Completed
            $MOBJ2=1
        else
            if 0038: $MISSION_DIFFICULTY == 2 // Special Agent
            then
                00BA: show_text_styled GXT 'MOBJ3' time 2000 style 2      //Objective 3: Completed
                $MOBJ3=1
            else
                if 0038: $MISSION_DIFFICULTY == 3 // Perfect Agent
                then
                    00BA: show_text_styled GXT 'MOBJ4' time 2000 style 2      //Objective 4: Completed
                    $MOBJ4=1
                end
            end
        end
        00BC: show_text_highpriority GXT 'M31_031' time 4000 flag 1  // Diversion has been created.
        0164: disable_marker 340@
        020B: explode_car 40@ // versionA 
        064B: 30@ = create_particle "explosion_large" at 1352.1958 -1245.4927 13.3915 type 4 //create the particle
        064F: make_temp_particle_visible_and_remove_references 30@ //make the particle visible and remove references
        wait 400 ms 
        064B: 30@ = create_particle "explosion_large" at 1340.0 -1240.0 13.3915 type 4 //create the particle
        064F: make_temp_particle_visible_and_remove_references 30@ //make the particle visible and remove references
        wait 400 ms 
        064B: 30@ = create_particle "explosion_large" at 1350.0 -1245.0 13.3915 type 4 //create the particle
        064F: make_temp_particle_visible_and_remove_references 30@ //make the particle visible and remove references
        wait 400 ms 
        064B: 30@ = create_particle "explosion_large" at 1360.0 -1250.0 13.3915 type 4 //create the particle
        064F: make_temp_particle_visible_and_remove_references 30@ //make the particle visible and remove references
        wait 400 ms 
        064B: 30@ = create_particle "explosion_large" at 1360.0 -1235.0 13.3915 type 4 //create the particle
        064F: make_temp_particle_visible_and_remove_references 30@ //make the particle visible and remove references
    end
end

0051: return

{=================================================================
OBJECTIVE 5 - Gain entry to G5 building
==================================================================}
:M3_1_OBJECTIVE5
if
0038:   $OBJ5_STATUS == 0
then
    if
    00FE:   actor $PLAYER_ACTOR sphere 0 in_sphere 1550.0499 -1626.5929 13.3828 radius 15.0 15.0 15.0 
    then
        if
        0038: $MISSION_DIFFICULTY == 1  // Agent
        then
            if and
            0038: $MOBJ1 == 1   // Retrieve drop point equipment
            0038: $MOBJ2 == 1   // Create vehicular diversion
            then
                00BC: show_text_highpriority GXT 'M31_019' time 4000 flag 1  // (Carrington) The car park lift is your only point of ingress.
                $OBJ5_STATUS = 1
                0164: disable_marker 350@
                03BC: 351@ = create_sphere_at 1588.0896 -1640.6112 13.1563 radius 3.0
                018A: 352@ = create_checkpoint_at 1588.0896 -1640.6112 13.1563
                0165: set_marker 352@ color_to 0 // Red
                32@=0
            else
                00BC: show_text_highpriority GXT 'M31_032' time 4000 flag 1  // They have discovered you. Your chance to enter ~b~G5~w~ is lost.
                $OTHER_TASKS_STATUS=99 // Mission Failed
            end
        end
    
        if
        0038: $MISSION_DIFFICULTY == 2  // Special Agent
        then
            if and
            0038: $MOBJ1 == 1   // Retrieve drop point equipment
            0038: $MOBJ2 == 1   // Prepare escape route
            0038: $MOBJ3 == 1   // Create vehicular diversion
            then
                00BC: show_text_highpriority GXT 'M31_019' time 4000 flag 1  // (Carrington) The car park lift is your only point of ingress.
                $OBJ5_STATUS = 1
                0164: disable_marker 350@
                03BC: 351@ = create_sphere_at 1588.0896 -1640.6112 13.1563 radius 3.0
                018A: 352@ = create_checkpoint_at 1588.0896 -1640.6112 13.1563
                0165: set_marker 352@ color_to 0 // Red
                32@=0
            else
                00BC: show_text_highpriority GXT 'M31_032' time 4000 flag 1  // They have discovered you. Your chance to enter ~b~G5~w~ is lost.
                $OTHER_TASKS_STATUS=99 // Mission Failed
            end
        end
    
        if
        0038: $MISSION_DIFFICULTY == 3  // Perfect Agent
        then
            if and
            0038: $MOBJ1 == 1   // Retrieve drop point equipment
            0038: $MOBJ2 == 1   // Attach tracer to limousine
            0038: $MOBJ3 == 1   // Prepare escape route
            0038: $MOBJ4 == 1   // Create vehicular diversion
            then
                00BC: show_text_highpriority GXT 'M31_019' time 4000 flag 1  // (Carrington) The car park lift is your only point of ingress.
                $OBJ5_STATUS = 1
                0164: disable_marker 350@
                03BC: 351@ = create_sphere_at 1588.0896 -1640.6112 13.1563 radius 3.0
                018A: 352@ = create_checkpoint_at 1588.0896 -1640.6112 13.1563
                0165: set_marker 352@ color_to 0 // Red
                32@=0
            else
                00BC: show_text_highpriority GXT 'M31_032' time 4000 flag 1  // They have discovered you. Your chance to enter ~b~G5~w~ is lost.
                $OTHER_TASKS_STATUS=99 // Mission Failed
            end
        end
    end
end

if and
0038: $OBJ5_STATUS == 1
0029:   32@ >= 4000 
then
    0050: gosub @JOANNA_RESPONSE // (Johanna) Received.
    $OBJ5_STATUS=2
end

if and
0038: $OBJ5_STATUS == 2
00FE:   actor $PLAYER_ACTOR sphere 0 in_sphere 1588.0896 -1640.6112 13.1563 radius 3.0 3.0 3.0 
then
    if 0038: $MISSION_DIFFICULTY == 1 // Agent
    then 00BA: show_text_styled GXT 'MOBJ3' time 2000 style 2      //Objective 3: Completed
    else
        if 0038: $MISSION_DIFFICULTY == 2 // Special Agent
        then 00BA: show_text_styled GXT 'MOBJ4' time 2000 style 2      //Objective 4: Completed
        else
            if 0038: $MISSION_DIFFICULTY == 3 // Perfect Agent
            then 00BA: show_text_styled GXT 'MOBJ5' time 2000 style 2      //Objective 5: Completed
            end
        end
    end
    $OBJ5_STATUS=3
    32@=0
    03BD: destroy_sphere 351@ 
    0164: disable_marker 352@
end

if and
0038: $OBJ5_STATUS == 3
0029:   32@ >= 4000 
then
    if 0039: $MISSION_DIFFICULTY == 1 // Agent
    then $MOBJ3=1
    else
        if 0039: $MISSION_DIFFICULTY == 2 // Special Agent
        then $MOBJ4=1
        else
            if 0039: $MISSION_DIFFICULTY == 3 // Perfect Agent
            then $MOBJ5=1
            end
        end
    end
end
0051: return

{=================================================================
CUTSCENES
==================================================================}
:STEALTH_OUTRO
0051: return

{=================================================================
LOCAL SUBROUTINES
==================================================================}
{=================================================================
Final routines
==================================================================}
//––––––––––––-
:STEALTH_PASSED
0394: play_music 1
$TUT_MISSIONS_PASSED += 1
Player.Money($PLAYER_CHAR) += 10000
01E3: show_text_1number_styled GXT 'M_PASS' number 10000 time 5000 style 1 // MISSION PASSED!~n~~w~$~1~
return

//––––––––––––-
:STEALTH_FAIL
00BA: show_text_styled GXT 'M_FAIL' time 5000 style 1 // ~r~MISSION FAILED!
//0555: remove_weapon 31 from_actor $PLAYER_ACTOR
return

//––––––––––––-
:STEALTH_CLEANUP
$ONMISSION = 0
{CONDICIONES GENERALES}
010D: set_player $PLAYER_CHAR wanted_level_to 0
01F0: set_max_wanted_level_to 6 
{ACTORES}
{OBJECTS}
{MARKERS / CHECKPOINTS}
//0A41: destroy_entrance_marker BuildingDoor
08E7: disable_entrance_markers 0
//0164: disable_marker 304@
{SPHERES}
//03BD: destroy_sphere 300@ 
{DECISIONS}
065C: release_decision_maker 400@  // Comportamiento limpio para actores especiales
{VARIABLES GLOBALES}
$MOBJ1 = 0
$MOBJ2 = 0
$MOBJ3 = 0
$MOBJ4 = 0
$MOBJ5 = 0
$OBJ1_STATUS=0
$OBJ2_STATUS=0
$OBJ3_STATUS=0
$OBJ4_STATUS=0
$OBJ5_STATUS=0
$OTHER_TASKS_STATUS=0
$ALLOBJECTIVES=0
mission_cleanup
return







//––––-External script 0–––––
// put your external scripts here