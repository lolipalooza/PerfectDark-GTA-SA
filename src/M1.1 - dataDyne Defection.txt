DEFINE MISSIONS 2
DEFINE MISSION 0 AT @INITIAL
DEFINE MISSION 1 AT @DEFECTION


DEFINE EXTERNAL_SCRIPTS -1 // Use -1 in order not to compile AAA script
//DEFINE SCRIPT {NAME} AT {LABEL} @

DEFINE UNKNOWN_EMPTY_SEGMENT 0

DEFINE UNKNOWN_THREADS_MEMORY 0

{$VERSION 3.0.0000}
var
$PLAYER_CHAR: Player
end // var
03A4: name_thread 'MAIN'
01F0: set_max_wanted_level_to 6
0111: toggle_wasted_busted_check 0
00C0: set_current_time_hours_to 8 minutes_to 0
04E4: unknown_refresh_game_renderer_at 1049.9067 1031.3267
03CB: set_rendering_origin_at 1049.9067 1031.3267 10.685
0053: $PLAYER_CHAR = create_player #NULL at 1049.9067 1031.3267 10.685 //1453.0439 -1841.0349 13.5469
01F5: $PLAYER_ACTOR = create_player_actor $PLAYER_CHAR
07AF: $PLAYER_GROUP = player $PLAYER_CHAR group
0373: set_camera_directly_behind_player
01B6: set_weather 0
0001: wait 0 ms
087B: set_player $PLAYER_CHAR clothes_texture "PLAYER_FACE" model "HEAD" body_part 1
087B: set_player $PLAYER_CHAR clothes_texture "JEANSDENIM" model "JEANS" body_part 2
087B: set_player $PLAYER_CHAR clothes_texture "SNEAKERBINCBLK" model "SNEAKER" body_part 3
087B: set_player $PLAYER_CHAR clothes_texture "VEST" model "VEST" body_part 0
070D: rebuild_player $PLAYER_CHAR
01B4: toggle_player $PLAYER_CHAR can_move 0
016A: fade 0 time 0
04BB: select_interior 0
0629: change_integer_stat 181 to 4
016C: restart_if_wasted_at 2027.77 -1420.52 15.99 angle 137.0 town_number 0
016D: restart_if_busted_at 1550.68 -1675.49 14.51 angle 90.0 town_number 0
0180: set_on_mission_flag_to $ONMISSION // Note: your missions have to use the variable defined here
0004: $DEFAULT_WAIT_TIME = 250
03E6: remove_text_box

023C: load_special_actor 'reimu' as 1
038B: load_requested_models
repeat
wait 0 ms
until 023D:   special_actor 1 loaded
09C7: change_player $PLAYER_CHAR model_to #SPECIAL01
0296: unload_special_actor 1

wait 1000 ms
016A: fade 1 time 2000
01B4: toggle_player $PLAYER_CHAR can_move 1
0417: start_mission 0 // Initial
wait 0
// put your create_thread commands here
00D7: create_thread @CI_MISSIONS_SNIFFER

:MAIN_LOOP
0001: wait $DEFAULT_WAIT_TIME ms
0002: jump @MAIN_LOOP




{==============================================================
GLOBAL SUBROUTINES
===============================================================}
:SUB_FADE_500MS
0169: set_fade_color_RGB 0 0 0 
016A: fade 0 time 500 

:LITCAS_282
00D6: if 
016B:   fading 
004D: jump_if_false @LITCAS_306 
0001: wait 0 ms 
0002: jump @LITCAS_282 

:LITCAS_306
0051: return 
//------------------------
:TELEPORT
// Seleccionando Interior y colocándo al jugador en él
0050: gosub @SUB_FADE_500MS 
01B4: toggle_player $PLAYER_CHAR can_move 0
04BB: select_interior $ACTIVE_INTERIOR 
0860: link_actor $PLAYER_ACTOR to_interior $ACTIVE_INTERIOR 
04E4: unknown_refresh_game_renderer_at $PLAYER_X $PLAYER_Y 
03CB: set_rendering_origin_at $PLAYER_X $PLAYER_Y $PLAYER_Z 
//04F9: set_interior 1 color_to 0 
00A1: put_actor $PLAYER_ACTOR at $PLAYER_X $PLAYER_Y $PLAYER_Z 
0173: set_actor $PLAYER_ACTOR Z_angle_to $PLAYER_ANGLE 
0373: set_camera_directly_behind_player 
//02EB: restore_camera_with_jumpcut
0051: return
//------------------------
:FADEOUT
0001: wait 1000 ms 
016A: fade 1 time 1000 
01B4: set_player $PLAYER_CHAR can_move 1
0051: return
//------------------------
:JOANNA_RESPONSE
0209: $RAND = random_int_in_ranges 0 3
00D6: if
0039:   $RAND == 0
004D: jump_if_false @J01 
00BC: show_text_highpriority GXT 'MJO_1' time 4000 flag 1  // (Joanna) Received.
:J01
00D6: if
0039:   $RAND == 1
004D: jump_if_false @J02 
00BC: show_text_highpriority GXT 'MJO_2' time 4000 flag 1  // (Joanna) Understood.
:J02
00D6: if
0039:   $RAND == 2
004D: jump_if_false @J03 
00BC: show_text_highpriority GXT 'MJO_3' time 4000 flag 1  // (Joanna) Confirmed.
:J03
00D6: if
0039:   $RAND == 3
004D: jump_if_false @J04 
00BC: show_text_highpriority GXT 'MJO_4' time 4000 flag 1  // (Joanna) Affirmative.
:J04
0051: return  
//------------------------
:SELECT_DIFFICULTY
01B4: set_player $PLAYER_CHAR can_move 0
0512:  show_permanent_text_box 'MHLP01'
08D4: $SEL_DIF_MENU = create_panel_with_title 'MTBL01' position 29.0 145.0 width {220.0} 300.0 columns 1 interactive 1 background 1 alignment 1
08DB:  set_panel $SEL_DIF_MENU column 0 header s$MISSION data 'MDIF1' 'MDIF2' 'MDIF3' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY'// 'M3_2' 'M4_1' 'M4_2' 'M4_3' 'M5_1' 'M5_2'
repeat
wait 0 ms
until 00E1: player 0 pressed_key 16 
08D7: $choice = panel $SEL_DIF_MENU active_row
08DA: remove_panel $SEL_DIF_MENU
03E6: remove text box
if $choice == 0
then
    $MISSION_DIFFICULTY = 1     // Agent
    $SubMachineAmmo     = 35
    $MissionAccuracy    = 45
    $MissionHealth      = 45
end
if $choice == 1
then
    $MISSION_DIFFICULTY = 2     // Special Agent
    $SubMachineAmmo     = 25
    $MissionAccuracy    = 60
    $MissionHealth      = 70
end
if $choice == 2
then
    $MISSION_DIFFICULTY = 3     // Perfect Agent
    $SubMachineAmmo     = 18
    $MissionAccuracy    = 75
    $MissionHealth      = 100
end
0051: return  
//------------------------
:MISSION_BAR
if 00E1:   player 0 pressed_key 11    //~k~~CONVERSATION_YES~
then
    015D: set_gamespeed  0.001 // 0.3 o 1.0 slow motion
    01B4: set_player $PLAYER_CHAR can_move 0 

    038E: draw_box_position 400.0 270.0 size 400.0 220.0 RGBA 123 34 111 75
    038E: draw_box_position 400.0 185.0 size 340.0 1.2 RGBA 255 255 255 60

    033F: set_text_draw_letter_size 0.5 1.2
    0343: set_text_draw_linewidth 600.0 
    033E: set_draw_text_position 225.0 170.0 GXT s$MISSION    // Mission Name

    if 85AD:   not s$OBJ1 == 'DUMMY'
    then
        // Objective 1
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        033E: set_draw_text_position 225.0 200.0 GXT 'OBJ1'
        
        // Objective 1 Entry
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        033E: set_draw_text_position 315.0 200.0 GXT s$OBJ1
        
        // Objective 1 Status (Complete / Incomplete)
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        03E4: enable_text_draw_align_right 1
        if 0038: $MOBJ1 == 0
        then
        0340: set_text_draw_RGBA 250 164 96 255 
        033E: set_draw_text_position 540.0 210.0 GXT 'OBJINC'
        else
        0340: set_text_draw_RGBA 135 206 235 255 
        033E: set_draw_text_position 540.0 210.0 GXT 'OBJCMP' 
        end
    end 

    if 85AD:   not s$OBJ2 == 'DUMMY'
    then
        // Objective 2
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        033E: set_draw_text_position 225.0 225.0 GXT 'OBJ2'
        
        // Objective 2 Entry
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        033E: set_draw_text_position 315.0 225.0 GXT s$OBJ2
        
        // Objective 2 Status (Complete / Incomplete)
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        03E4: enable_text_draw_align_right 1
        if 0038: $MOBJ2 == 0
        then
        0340: set_text_draw_RGBA 250 164 96 255 
        033E: set_draw_text_position 540.0 235.0 GXT 'OBJINC'
        else
        0340: set_text_draw_RGBA 135 206 235 255 
        033E: set_draw_text_position 540.0 235.0 GXT 'OBJCMP' 
        end
    end 

    if 85AD:   not s$OBJ3 == 'DUMMY'
    then
        // Objective 3
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        033E: set_draw_text_position 225.0 250.0 GXT 'OBJ3'
        
        // Objective 3 Entry
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        033E: set_draw_text_position 315.0 250.0 GXT s$OBJ3
        
        // Objective 3 Status (Complete / Incomplete)
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        03E4: enable_text_draw_align_right 1
        if 0038: $MOBJ3 == 0
        then
        0340: set_text_draw_RGBA 250 164 96 255 
        033E: set_draw_text_position 540.0 260.0 GXT 'OBJINC'
        else
        0340: set_text_draw_RGBA 135 206 235 255 
        033E: set_draw_text_position 540.0 260.0 GXT 'OBJCMP' 
        end
    end 

    if 85AD:   not s$OBJ4 == 'DUMMY'
    then
        // Objective 4
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        033E: set_draw_text_position 225.0 275.0 GXT 'OBJ4'
        
        // Objective 4 Entry
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        033E: set_draw_text_position 315.0 275.0 GXT s$OBJ4
        
        // Objective 4 Status (Complete / Incomplete)
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        03E4: enable_text_draw_align_right 1
        if 0038: $MOBJ4 == 0
        then
        0340: set_text_draw_RGBA 250 164 96 255 
        033E: set_draw_text_position 540.0 285.0 GXT 'OBJINC'
        else
        0340: set_text_draw_RGBA 135 206 235 255 
        033E: set_draw_text_position 540.0 285.0 GXT 'OBJCMP' 
        end
    end 

    if 85AD:   not s$OBJ5 == 'DUMMY'
    then
        // Objective 5
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        033E: set_draw_text_position 225.0 300.0 GXT 'OBJ5'
        
        // Objective 5 Entry
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        033E: set_draw_text_position 315.0 300.0 GXT s$OBJ5
        
        // Objective 5 Status (Complete / Incomplete)
        033F: set_text_draw_letter_size 0.4 1.0
        0343: set_text_draw_linewidth 600.0 
        03E4: enable_text_draw_align_right 1
        if 0038: $MOBJ5 == 0
        then
        0340: set_text_draw_RGBA 250 164 96 255 
        033E: set_draw_text_position 540.0 310.0 GXT 'OBJINC'
        else
        0340: set_text_draw_RGBA 135 206 235 255 
        033E: set_draw_text_position 540.0 310.0 GXT 'OBJCMP' 
        end
    end 

    038E: draw_box_position 400.0 337.0 size 340.0 1.2 RGBA 255 255 255 60

    033F: set_text_draw_letter_size 0.4 1.0
    0343: set_text_draw_linewidth 600.0 
    03E4: enable_text_draw_align_right 1
    033E: set_draw_text_position 540.0 340.0 GXT 'OBTB01'     // Abort Mission

    repeat
    wait 0 ms
    until 80E1: not player 0 pressed_key 11
        
    repeat
    wait 0 ms
    until 00E1:   player 0 pressed_key 11

    015D: set_gamespeed  1.0 // 0.3 o 1.0 slow motion
    01B4: set_player $PLAYER_CHAR can_move 1
    03F0: enable_text_draw 0 

    repeat
    wait 0 ms
    until 80E1: not player 0 pressed_key 11
end
0051: return  







// put your mods (threads) here
:CI_MISSIONS_SNIFFER
thread "CI_SNIFF"
$MARKER_CARRINGTON_INSTITUTE = Marker.CreateIconAndSphere(42, $X_CARRINGTON_INSTITUTE, $Y_CARRINGTON_INSTITUTE, $Z_CARRINGTON_INSTITUTE)
repeat
wait $DEFAULT_WAIT_TIME
if and
    player.Defined($PLAYER_CHAR)
    $ONMISSION == 0
then
    if and
        00FF: actor $PLAYER_ACTOR sphere 0 in_sphere $X_CARRINGTON_INSTITUTE $Y_CARRINGTON_INSTITUTE $Z_CARRINGTON_INSTITUTE radius 1.0 1.0 2.0 on_foot
        Player.Controllable($PLAYER_CHAR)
        $TUT_MISSIONS_PASSED == 0
    then
        054C: use_GXT_table 'PD'
        $ONMISSION = 1
        s$MISSION = 'M1_1' // dataDyne Central: Defection
        0050: gosub @SELECT_DIFFICULTY
        00BA: show_text_styled GXT 'M1_1' time 1000 style 2 // dataDyne Central: Defection
        01B4: toggle_player $PLAYER_CHAR can_move 0
        0050: gosub @SUB_FADE_500MS 
        start_mission 1
    end
end
until $TUT_MISSIONS_PASSED == 1
marker.Disable($MARKER_CARRINGTON_INSTITUTE)
end_thread

//––––-Mission 0–––––
:INITIAL
$TUT_MISSIONS_PASSED = 0
$ONMISSION = 0
     
$X_CARRINGTON_INSTITUTE = 1046.3639
$Y_CARRINGTON_INSTITUTE = 1015.7791
$Z_CARRINGTON_INSTITUTE = 11.0
//0629: change_integer_stat 225 to 999 // capacidad pulmonar al máximo jolines tío no la empalmeis hostia!

model.Load(#FCR900)
038B: load_requested_models

:INITIAL_LOAD
wait 0 ms
if
model.Available(#FCR900)
004D: jump_if_false @INITIAL_LOAD

0674: set_car_model #FCR900 numberplate "DARK_"
$darkbike = car.Create(#FCR900,1452.9561, -1837.4128, 13.5469)
end_thread






//––––-Mission 1–––––
// Mission wrapper
:DEFECTION
thread 'M1_1'
gosub @DEFECTION_MAIN
if wasted_or_busted
then
gosub @DEFECTION_FAIL
end
gosub @DEFECTION_CLEANUP
end_thread

//––––––––––––-
:DEFECTION_MAIN
const         // Nomenclatura propuesta:    0-99: actors,vehicles, 100-199: flags, 200-299: objects, 300-399: markers, 400-499: decision makers
    Helicopter = 0@
    Carrington = 1@
    // 2@: guardia en la azotea del edificio
    // 3@: marker del helicóptero
    // 5@: racing checkpoint (aro aéreo)
    Cassandra = 20@
    Secretary = 21@
    dDWorker = 22@
    EntranceMarkerCreated = 100@
    Floor0EnemysCreated = 101@
    BasementEnemysCreated = 110@
    // 102@: Enemies Created 1
    BuildingDoor = 4@
    Elevator = 6@
    // 7@: actriz doble de johanna descendiendo sobre cuerda del helicóptero
    // 8@: checkpoint sobre azotea del edificio
    // 9@:checkpoint 2 sobre edificio
    // 10@-15@: Floor 0 Enemies (dD Defection X)
    // 16@-19@: guardaespaldas de Carrington
    // 23@ - 35@: Enemies
    // 36@ - xx@: Basement Enemies
    // 200@-209@: objects for the level
    // 210@: shield pickup
    // 211@: weapon pickup
end

// Initialize Mission Time
00C0: set_current_time_hours_to 0 minutes_to 0

// Initialize global variables for Mission Status Bar
if 0038:   $MISSION_DIFFICULTY == 1  // Agent
then s$OBJ1='M11OB5'
s$OBJ2='DUMMY'
s$OBJ3='DUMMY'
s$OBJ4='DUMMY'
s$OBJ5='DUMMY'
end
if 0038:   $MISSION_DIFFICULTY == 2  // Special Agent
then s$OBJ1='M11OB1'
s$OBJ2='M11OB2'
s$OBJ3='M11OB4'
s$OBJ4='M11OB5'
s$OBJ5='DUMMY'
end
if 0038:   $MISSION_DIFFICULTY == 3  // Perfect Agent
then s$OBJ1='M11OB1'
s$OBJ2='M11OB2'
s$OBJ3='M11OB3'
s$OBJ4='M11OB4'
s$OBJ5='M11OB5'
end

// Spawn actor
04E4: unknown_refresh_game_renderer_at -2053.0667 -183.5957 
03CB: set_rendering_origin_at -2053.0667 -183.5957 35.3274 
Actor.PutAt($PLAYER_ACTOR, -2053.0667, -183.5957, 35.3274)      //00A1: put_actor $PLAYER_ACTOR at -2053.0667 -183.5957 35.3274 
Actor.Angle($PLAYER_ACTOR) = 182.3029                           //0173: set_actor $PLAYER_ACTOR Z_angle_to 182.3029 
Camera.SetBehindPlayer()                                        //0373: set_camera_directly_behind_player 
Camera.Restore_WithJumpCut()                                    //02EB: restore_camera_with_jumpcut 

// Spawn Objects
0107: 200@ = create_object #CJ_SLOT_PROPg at 349.1 189.345 1018.98
0453: set_object 200@ XY_rotation 0.0 0.0 angle 90.0
0107: 201@ = create_object #CJ_SLOT_PROPg at 377.054 183.644 1007.33
0453: set_object 201@ XY_rotation 0.0 0.0 angle -90.0
if
0039: $MISSION_DIFFICULTY == 3  // Perfect Agent
then
0107: 202@ = create_object #Gen_doorINT04 at 370.789 179.088 1013.17    // #Gen_doorEXT07 la puerta azul
0453: set_object 202@ XY_rotation 0.0 0.0 angle 0.0
0107: 203@ = create_object #Gen_doorINT04 at 332.711 183.492 1013.17
0453: set_object 203@ XY_rotation 0.0 0.0 angle 90.0

0905: set_door 202@ locked 1
0905: set_door 203@ locked 1
end
029B: 204@ = init_object #freight_SFW_door at 358.785 217.56 1008.53 
0453: set_object 204@ XY_rotation 0.0 0.0 angle 270.0 
029B: 205@ = init_object #freight_SFW_door at 353.811 175.78 1025.93
0453: set_object 205@ XY_rotation 0.0 0.0 angle 90.0

// Spawn Weapons/Pickups
if 8038: not $MISSION_DIFFICULTY == 3   // Agent or Special Agent
then
0213: 210@ = create_pickup #BODYARMOUR type 3 at 349.0002 164.1536 1014.1875
end
032B: 211@ = create_weapon_pickup #MP5LNG  group 15 ammo 30 at 345.0554 164.1536 1014.1875

//Load models
Model.Load(#silenced)
Model.Load(#micro_uzi)
Model.Load(#lvemt1)
Model.Load(#heck1)
Model.Load(#maverick)
Model.Load(#TRIADA)
Model.Load(#TRIADB)
Model.Load(#mp5lng)
038B: load_requested_models

:DEFECTION_1_LOAD
00D6: if or
8248:   not model #silenced available 
8248:   not model #micro_uzi available 
8248:   not model #lvemt1 available 
8248:   not model #heck1 available 
8248:   not model #maverick available 
8248:   not model #triada available 
8248:   not model #triadb available  
8248:   not model #mp5lng available  
004D: jump_if_false @DEFECTION_MAIN_2 
0001: wait 0 ms 
0002: jump @DEFECTION_1_LOAD 

:DEFECTION_MAIN_2
060A: create_decision_maker_type 0 store_to 400@    // clean behavior for special actors
060A: create_decision_maker_type 0 store_to 401@    // comportamiento para la secretaria (si te le acercas se agacha del susto)
0709: set_decision_maker 401@ on_event 36 taskID 415 respect 0.0 hate 100.0 like 0.0 dislike 100.0 in_car 1 on_foot 1
01B2: give_actor $PLAYER_ACTOR weapon 23 ammo 35
01B9: set_actor $PLAYER_ACTOR armed_weapon_to 23
Helicopter = Car.Create(#maverick, -2053.4119, -196.507, 35.3274)//0674: set_car_model #TAMPA numberplate "PATRICK_"
Car.Angle(Helicopter) = 90.0
0229: set_car Helicopter primary_color_to 0 secondary_color_to 54 
Carrington = Actor.Create(CIVMALE, #heck1, -2057.8132, -192.0627, 35.3203)
2@ = Actor.Create(GANG1, #lvemt1, 1548.8909, -1367.2094, 326.2109)
02E2: set_actor 2@ weapon_accuracy_to $MissionAccuracy
0223: set_actor 2@ health_to $MissionHealth
01B2: give_actor 2@ weapon 28 ammo $SubMachineAmmo
01B9: set_actor 2@ armed_weapon_to 28
077A: set_char_relationship 2@ relationship 4 to_ped_type 0
Actor.Angle(Carrington) = 270.0
Actor.Angle(2@) = 0.0
3@ = Marker.CreateAboveCar(Helicopter)
08E7: disable_entrance_markers 1 // inhabilitar
0A40: BuildingDoor = create_entrance_marker_at 1548.5665 -1363.7417 327.0 color 14
16@ = Actor.Create(CIVMALE,#TRIADA,-2058.8789,-190.7403,35.3203)
17@ = Actor.Create(CIVMALE,#TRIADB,-2058.8789,-193.2907,35.3203)
18@ = Actor.Create(CIVMALE,#TRIADA,-2050.4958,-193.2907,35.3274)
19@ = Actor.Create(CIVMALE,#TRIADB,-2050.4958,-190.7403,35.3203)
01B2: give_actor 16@ weapon 29 ammo 10
01B9: set_actor 16@ armed_weapon_to 29
01B2: give_actor 17@ weapon 29 ammo 10
01B9: set_actor 17@ armed_weapon_to 29
01B2: give_actor 18@ weapon 29 ammo 10
01B9: set_actor 18@ armed_weapon_to 29
01B2: give_actor 19@ weapon 29 ammo 10
01B9: set_actor 19@ armed_weapon_to 29
060B: set_actor Carrington decision_maker_to 400@
060B: set_actor 16@ decision_maker_to 400@
060B: set_actor 17@ decision_maker_to 400@
060B: set_actor 18@ decision_maker_to 400@
060B: set_actor 19@ decision_maker_to 400@
Actor.Angle(16@) = 270.0
Actor.Angle(17@) = 270.0
Actor.Angle(18@) = 90.0
Actor.Angle(19@) = 90.0
0001: wait 1000 ms 
016A: fade 1 time 500
Player.CanMove($PLAYER_CHAR) = true
0249: release_model #silenced
0249: release_model #micro_uzi
0249: release_model #lvemt1
0249: release_model #heck1
0249: release_model #maverick
0249: release_model #triada
0249: release_model #triadb

//00BC: show_text_highpriority GXT 'M11_001' time 3000 flag 1  // (Joanna) Why the big hurry?

// Initializing
0006: Floor0EnemysCreated = 0
0006: BasementEnemysCreated = 0
$MOBJ1=0
$MOBJ2=0
$MOBJ3=0
$MOBJ4=0
$MOBJ5=0
$OBJ1_STATUS=0
$OBJ2_STATUS=0
$OBJ3_STATUS=0
$OBJ4_STATUS=0
$OBJ5_STATUS=0
$AREA_STATUS=0
$OTHER_TASKS_STATUS=0
$ALLOBJECTIVES=0
33@=60000

repeat
    wait 0 ms
    if
    Actor.Dead(Carrington)
    then
    jump @DEFECTION_FAIL
    end

    //-----------SKIP TRIP---------------
    if
    00E1:   player 0 pressed_key 11  // Tecla Y
    then
        $ACTIVE_INTERIOR = 0
        $PLAYER_X = 1544.3235
        $PLAYER_Y = -1356.3276
        $PLAYER_Z = 329.4691
        $PLAYER_ANGLE = 180.0
        0050: gosub @TELEPORT
        00A6: destroy_car Helicopter
        0247: load_model #MAVERICK
        038B: load_requested_models
        repeat
        wait 0 ms
        until 0248:   model #MAVERICK available 
        00A5: Helicopter = create_car #MAVERICK at 1543.0636 -1348.7539 329.4691
        0249: release_model #MAVERICK
        0175: set_car Helicopter Z_angle_to 270.0
        0229: set_car Helicopter primary_color_to 0 secondary_color_to 54
        0164: disable_marker 3@
        0050: gosub @FADEOUT
        03E6: remove_text_box 
        jump @DEFECTION_010
    else
        if and
        0029:   33@ >= 60000
        88FE: not text_box_displayed
        // 0039: $M1_1_ATTEMPTS == 1
        then
        03E5: show_text_box 'MSKIP' // You can ~b~skip~w~ the Intro by pressing ~k~~CONVERSATION_YES~.
        33@=0
        end
    end
until 00DB: actor $PLAYER_ACTOR in_car 0@            
                                                     
0164: disable_marker 3@
041E: set_radio_station 11     // turn off radio
018A: 8@ = create_checkpoint_at 1543.0636 -1348.7539 349.3356
06D5: 5@ = create_racing_checkpoint_at 1543.0636 -1348.7539 349.3356 point_to 1522.0209 -1348.2252 349.7328 type 3 radius 3.0 
08FB: set_checkpoint 8@ type_to 2   // 2: red
      
repeat                                                                         
wait 0 ms
    if 0039: $OTHER_TASKS_STATUS == 0
    then 00BC: show_text_highpriority GXT 'M11_001' time 3000 flag 1  // (Joanna) Why the big hurry?
    $OTHER_TASKS_STATUS = 1
    32@=0
    end
    
    if and
    0039: $OTHER_TASKS_STATUS == 1
    0029: 32@ >= 3000
    then 00BC: show_text_highpriority GXT 'M11_002' time 5000 flag 1  // (Carrington) If Dr Caroll is not extracted tonight, dataDyne will put him through mind conditioning,
    $OTHER_TASKS_STATUS = 2
    32@=0
    end
    
    if and
    0039: $OTHER_TASKS_STATUS == 2
    0029: 32@ >= 5000
    then 00BC: show_text_highpriority GXT 'M11_003' time 5000 flag 1  // (Carrington) and we'll lose our best chance of finding out what dataDyne are up to.
    $OTHER_TASKS_STATUS = 3
    32@=0
    end
    
    if and
    0039: $OTHER_TASKS_STATUS == 3
    0029: 32@ >= 5000
    then 00BC: show_text_highpriority GXT 'M11_004' time 4000 flag 1  // (Joanna) Are they all expendable?
    $OTHER_TASKS_STATUS = 4
    32@=0
    end
    
    if and
    0039: $OTHER_TASKS_STATUS == 4
    0029: 32@ >= 4000
    then 00BC: show_text_highpriority GXT 'M11_005' time 6000 flag 1  // (Carrington) Don't joke! You have to be careful, Joanna. Code keys will only operate while the owner is alive…
    $OTHER_TASKS_STATUS = 5
    32@=0
    end
    
    if and
    0039: $OTHER_TASKS_STATUS == 5
    0029: 32@ >= 6000
    then 00BC: show_text_highpriority GXT 'M11_006' time 6000 flag 1  // (Carrington) if you kill them, the key is useless. Armed guards are a different matter of course.
    $OTHER_TASKS_STATUS = 6
    32@=0
    end
    
    if and
    0039: $OTHER_TASKS_STATUS == 6
    0029: 32@ >= 6000
    then 00BC: show_text_highpriority GXT 'M11_007' time 4000 flag 1  // (Joanna) What's the target location?
    $OTHER_TASKS_STATUS = 7
    32@=0
    end
    
    if and
    0039: $OTHER_TASKS_STATUS == 7
    0029: 32@ >= 4000
    then 00BC: show_text_highpriority GXT 'M11_008' time 4000 flag 1  // (Carrington) Work your way down the building to the ground level.
    $OTHER_TASKS_STATUS = 8
    32@=0
    end
    
    if and
    0039: $OTHER_TASKS_STATUS == 8
    0029: 32@ >= 4000
    then 00BC: show_text_highpriority GXT 'M11_009' time 5000 flag 1  // (Carrington) Dr Caroll will be in a research lab somewhere in the underground facility.
    $OTHER_TASKS_STATUS = 9
    32@=0
    end
    
    if and
    0039: $OTHER_TASKS_STATUS == 9
    0029: 32@ >= 5000
    then 00BC: show_text_highpriority GXT 'M11_010' time 4000 flag 1  // (Joanna) How will I recognize him?
    $OTHER_TASKS_STATUS = 10
    32@=0
    end
    
    if and
    0039: $OTHER_TASKS_STATUS == 10
    0029: 32@ >= 4000
    then 00BC: show_text_highpriority GXT 'M11_011' time 5000 flag 1  // (Carrington) We don't have an image record, and we can't find any official files. All we have is the name.
    $OTHER_TASKS_STATUS = 11
    32@=0
    end
    
    if and
    0039: $OTHER_TASKS_STATUS == 11
    0029: 32@ >= 5000
    then 00BC: show_text_highpriority GXT 'M11_012' time 4000 flag 1  // (Carrington) Good luck, Perfect Dark.
    $OTHER_TASKS_STATUS = 12
    32@=0
    end
    
    //-----------SKIP TRIP---------------
    if 00E1:   player 0 pressed_key 11  // Tecla Y
    then
        $ACTIVE_INTERIOR = 0
        $PLAYER_X = 1544.3235
        $PLAYER_Y = -1356.3276
        $PLAYER_Z = 329.4691
        $PLAYER_ANGLE = 180.0
        0050: gosub @SUB_FADE_500MS
        0362: remove_actor $PLAYER_ACTOR from_car_and_place_at 1544.3235 -1356.3276 329.4691
        00A6: destroy_car Helicopter
        0247: load_model #MAVERICK
        038B: load_requested_models
        repeat
        wait 0 ms
        until 0248:   model #MAVERICK available 
        00A5: Helicopter = create_car #MAVERICK at 1543.0636 -1348.7539 329.4691
        0249: release_model #MAVERICK
        0175: set_car Helicopter Z_angle_to 270.0
        0229: set_car Helicopter primary_color_to 0 secondary_color_to 54
        0050: gosub @TELEPORT
        06D6: disable_racing_checkpoint 5@
        0164: disable_marker 8@
        0050: gosub @FADEOUT
        00BE: text_clear_all 
        jump @DEFECTION_010
    else
        if and
        0029: 33@ >= 60000
        88FE: not text_box_displayed
        //0039: $M1_1_ATTEMPTS == 1
        then
        03E5: show_text_box 'MSKIP' // You can ~b~skip~w~ the Intro by pressing ~k~~CONVERSATION_YES~.
        33@=0
        end
    end
until 00FE: actor $PLAYER_ACTOR 0 near_point_3d 1543.0636 -1348.7539 349.3356 radius 3.0 3.0 3.0    

01B4: toggle_player $PLAYER_CHAR can_move 0
00A1: put_actor $PLAYER_ACTOR at 1551.4124 -1246.4515 277.8824
0164: disable_marker 8@
06D6: disable_racing_checkpoint 5@
Car.PutAt(Helicopter, 1543.0636, -1348.7539, 352.0)
//0129: 99@ = create_actor_pedtype 4 model #NURGRL3 in_car Helicopter driverseat  
0503: create_actor_on_rope_with_pedtype 4 model #NURGRL3 at 1543.0636 -1348.7539 349.3356 handle_as 7@
015F: set_camera_position 1539.851 -1349.6829 335.3961 rotation 0.0 0.0 0.0
0160: set_camera_point_at 1543.0636 -1348.7539 349.3356 mode 2
wait 2000 ms

Car.PutAt(Helicopter, 1543.0636, -1348.7539, 352.0)
//0460: set_camera_transverse_delay 0.0 time 2000
015F: set_camera_position 1537.7487 -1341.4399 340.0 rotation 0.0 0.0 0.0
0160: set_camera_point_at 1544.0067 -1351.9817 329.4764 mode 2
wait 2000 ms

Car.PutAt(Helicopter, 1543.0636, -1348.7539, 352.0)
04ED: load_animation "swat"
repeat
wait 0
until 04EE:   animation "swat" loaded 

0605: actor 7@ perform_animation_sequence "swt_breach_01" IFP_file "swat" 10.0 loop 0 0 0 0 time -1

wait 2000 ms
04EF: release_animation "swat"  

Car.PutAt(Helicopter, 1543.0636, -1348.7539, 352.0)
01C2: remove_references_to_actor 7@ // Like turning an actor into a random pedestrian
009B: destroy_actor 7@

04ED: load_animation "heist9"
repeat
wait 0
until 04EE:   animation "heist9" loaded 

0362: remove_actor $PLAYER_ACTOR from_car_and_place_at 1544.2651 -1353.4175 329.4741 
0173: set_actor $PLAYER_ACTOR Z_angle_to 182.6516 
0605: actor $PLAYER_ACTOR perform_animation_sequence "swt_wllshoot_out_R" IFP_file "swat" 10.0 loop 0 0 0 0 time -1
0373: set_camera_directly_behind_player
02EB: restore_camera_with_jumpcut 
01B4: set_player $PLAYER_CHAR can_move 1 
04EF: release_animation "swat"  

:DEFECTION_010
01C2: remove_references_to_actor Carrington
01C2: remove_references_to_actor 16@
01C2: remove_references_to_actor 17@
01C2: remove_references_to_actor 18@
01C2: remove_references_to_actor 19@
$OTHER_TASKS_STATUS = 0

// Wanted level of the mission
010D: set_player $PLAYER_CHAR wanted_level_to 0 
01F0: set_max_wanted_level_to 0 


018A: 9@ = create_checkpoint_at 1548.5665 -1363.7417 327.0
08FB: set_checkpoint 9@ type_to 2   // 2: red

repeat                                                                         
wait 0 ms
    0050: gosub @MISSION_BAR
until 00FE: actor $PLAYER_ACTOR 0 near_point_3d 1548.5665 -1363.7417 326.2183 radius 1.0 1.0 1.0


0164: disable_marker 9@
EntranceMarkerCreated = 1
0050: gosub @DEFECTION_CHECK_DOORS

{==============================================================
EL LAZO WHILE PRINCIPAL
===============================================================}
while   0039: $ALLOBJECTIVES == 0
    wait 0 ms
    {CONDICION PARA CHEQUEAR SI EL JUGADOR ESTÁ EN LOS PUNTOS DE TELETRANSPORTACIÓN}
    0050: gosub @DEFECTION_CHECK_DOORS

    {OBJETIVOS DE SPECIAL AGENT Y PERFECT AGENT}
    if
    8039: not $MISSION_DIFFICULTY == 1 // Agent    
    then
        if 0039:   $MOBJ1 == 0
        then 0050: gosub @M1_1_OBJECTIVE1  // Disable internal security hub
        end
        
        if 0039:   $MOBJ2 == 0
        then 0050: gosub @M1_1_OBJECTIVE2  // Obtain keycode necklace
        end
        
        if and
        0039: $MISSION_DIFFICULTY == 2 // Special Agent
        0039: $MOBJ3 == 0 
        then 0050: gosub @M1_1_OBJECTIVE4  // Disable external comms hub            
        end
        
        if and
        0039: $MISSION_DIFFICULTY == 3 // Perfect Agent
        0039: $MOBJ3 == 0
        then 0050: gosub @M1_1_OBJECTIVE3  // Download project files
        end
        
        if and
        0039: not $MISSION_DIFFICULTY == 3 // Perfect Agent
        0039: not $MOBJ4 == 0 
        then 0050: gosub @M1_1_OBJECTIVE4  // Disable external comms hub
        end            
    end

    {ATENDER OTRAS TAREAS INDEPENDIENTES DE LOS OBJETIVOS}
    0050: gosub @M1_1_OTHER_TASKS

    {DETECTANDO AL JUGADOR EN EL ASCENSOR INFERIOR (PASAR LA MISION)}
    if
    00FE:   actor $PLAYER_ACTOR 0 near_point_3d 357.8952 217.2352 1008.3828 radius 1.0 1.0 1.0
    then
        if
        0039: $MISSION_DIFFICULTY == 1  // Agent
        then
        $ALLOBJECTIVES = 1
        end

        if and
        0039: $MISSION_DIFFICULTY == 2  // Special Agent
        0039: $MOBJ1 == 1
        0039: $MOBJ2 == 1
        0039: $MOBJ3 == 1
        then
        $ALLOBJECTIVES = 1
        end
        
        if and
        0039: $MISSION_DIFFICULTY == 3  // Perfect Agent
        0039: $MOBJ1 == 1
        0039: $MOBJ2 == 1
        0039: $MOBJ3 == 1
        0039: $MOBJ4 == 1
        then
        $ALLOBJECTIVES = 1
        end

        if
        0039: $ALLOBJECTIVES == 0
        then
        00BC: show_text_highpriority GXT 'MDRLCK' time 1000 flag 1  // The door is locked.
        end

    end

    {CONDICIÓN DE MISIÓN FALLIDA}    
    00D6: if 
    8039: not $OTHER_TASKS_STATUS == 99
    004D: jump_if_false @DEFECTION_FAIL
    
    {DESPLEGAR BARRA DE STATUS DE MISIÓN TRAS PULSACIÓN DE TECLA ESPECIAL -OBJETIVOS COMPLETADOS Y OBJETIVOS FALTANTES-}
    0050: gosub @MISSION_BAR
    
end
0002: jump @DEFECTION_PASSED 

{==============================================================
Maquina de estado de los teletransportadores
===============================================================}
:DEFECTION_CHECK_DOORS   
if
0039: $AREA_STATUS == 0    // Jugador afuera del edificio
then
    if
    80FE: not actor $PLAYER_ACTOR 0 near_point_3d 1548.5665 -1363.7417 327.0 radius 1.0 1.0 1.0
    then
        if
        0039: EntranceMarkerCreated == 0
        then
            0A40: BuildingDoor = create_entrance_marker_at 1548.5665 -1363.7417 327.0 color 14
            0006: EntranceMarkerCreated = 1     // entrance marker created
            0050: gosub @FADEOUT
        end
    else
        if
        0039: EntranceMarkerCreated == 1
        then
            $AREA_STATUS = 1
            $ACTIVE_INTERIOR = 3
            $PLAYER_X = 354.6288
            $PLAYER_Y = 173.218
            $PLAYER_Z = 1025.7964
            $PLAYER_ANGLE = 180.0
            0050: gosub @TELEPORT
            0A41: destroy_entrance_marker BuildingDoor                               
            0A40: Elevator = create_entrance_marker_at 357.8952 217.2352 1010.0 color 14
            EntranceMarkerCreated = 0
        end
    end
end

if
0039: $AREA_STATUS == 1   // Jugador adentro del edificio
then
    if
    80FE: not actor $PLAYER_ACTOR 0 near_point_3d 354.6515 175.3574 1025.7964 radius 1.0 1.0 1.0
    then
        if
        0039: EntranceMarkerCreated == 0
        then
            0A40: BuildingDoor = create_entrance_marker_at 354.679 175.3582 1027.0 color 14
            EntranceMarkerCreated = 1
            0050: gosub @FADEOUT
        end
    else
        if
        0039: EntranceMarkerCreated == 1
        then
            $AREA_STATUS = 0
            $ACTIVE_INTERIOR = 0
            $PLAYER_X = 1548.1379
            $PLAYER_Y = -1366.4137
            $PLAYER_Z = 326.2109
            $PLAYER_ANGLE = 90.0
            0050: gosub @TELEPORT
            0A41: destroy_entrance_marker BuildingDoor
            0A41: destroy_entrance_marker Elevator                               
            EntranceMarkerCreated = 0
        end
    end
end
0051: return

{=================================================================
OTHER TASKS
==================================================================}
:M1_1_OTHER_TASKS
if
8039: not $MISSION_DIFFICULTY == 1  // Special or Perfect Agent
then
    if
    0118:   actor Secretary dead
    then
        00BC: show_text_highpriority GXT 'M11_050' time 4000 flag 1  // Critical mission personnel killed.
        $OTHER_TASKS_STATUS = 99         // jump to fail mission routine
    end
end

if and
0039: $OTHER_TASKS_STATUS == 1
0029:   32@ >= 4000
then
    0687: clear_actor Secretary task
    074D: AS_actor Secretary turns_to_and_look_at_actor $PLAYER_ACTOR timelimit -1
    05C4: AS_actor Secretary hands_up 4000 ms
    if
    82D8: not actor $PLAYER_ACTOR current_weapon == 0    // Unarmed
    then        
        00BC: show_text_highpriority GXT 'M11_016' time 4000 flag 1  // (Secretary) Look out, she's got a gun!
    end
    32@ = 0
    $OTHER_TASKS_STATUS = 2
end

if and
0039: $OTHER_TASKS_STATUS == 2
0029:   32@ >= 4000
then
    060B: set_actor Secretary decision_maker_to 401@
    077A: set_actor Secretary acquaintance 4 to_actors_pedtype 0
    05D3: AS_actor Secretary goto_point 380.3754 195.5264 1014.1875 mode 6 -1 ms
    $OTHER_TASKS_STATUS = 3
end

if
8039: not $MISSION_DIFFICULTY == 1  // Special or Perfect Agent
then
    if and
    0039: $OTHER_TASKS_STATUS == 3
    0104:   actor $PLAYER_ACTOR near_actor Secretary radius 2.0 2.0 2.0 sphere 0
    then
        00BC: show_text_highpriority GXT 'M11_024' time 4000 flag 1  // (Secretary) Please don't kill me.
        $OTHER_TASKS_STATUS = 0
    end
end

if and
0039: $OTHER_TASKS_STATUS == 4
0029:   32@ >= 6000 
then
    00BC: show_text_highpriority GXT 'M11_039' time 4000 flag 1  // (Carrington) The basement elevator must be around there somewhere.
    018A: 303@ = create_checkpoint_at 358.1466 217.2133 1008.3828
    Marker.SetColor(303@,0) // red
    $OTHER_TASKS_STATUS=5
    32@=0
end

if and
0039: $OTHER_TASKS_STATUS == 5
0029:   32@ >= 4000 
then
    0050: gosub @JOANNA_RESPONSE  // (Joanna) Affirmative.
    $OTHER_TASKS_STATUS=0
end

{SPAWNING ENEMIES}
if and
00FE: actor $PLAYER_ACTOR sphere 0 in_sphere 354.6288 173.218 1025.7964 radius 1.0 1.0 1.0 
0039: 102@ == 0  // not Enemies Created 1
then
0050: gosub @DEFECTION_ENEMIES1
102@=1
end

if and
0039: Floor0EnemysCreated == 0
00FE: actor $PLAYER_ACTOR 0 near_point_3d 372.3281 153.5288 1010.1875 radius 2.0 2.0 2.0 
then
0050: gosub @DEFECTION_FLOOR0_ENEMIES
0006: Floor0EnemysCreated = 1
end

if
0039: BasementEnemysCreated == 0
then
    if or
    00FE: actor $PLAYER_ACTOR 0 near_point_3d 370.3462 181.3226 1008.3828 radius 2.0 2.0 2.0 
    00FE: actor $PLAYER_ACTOR 0 near_point_3d 365.0799 183.2257 1008.3828 radius 4.0 4.0 3.0 
    then
    0050: gosub @DEFECTION_BASEMENT_ENEMIES
    0006: BasementEnemysCreated = 1
    end
end
0051: return    

{=================================================================
OBJECTIVE 1 - Disable internal security hub
==================================================================}
:M1_1_OBJECTIVE1
if and
0039: $OBJ1_STATUS == 0
00FE: actor $PLAYER_ACTOR 0 near_point_3d 377.8236 163.261 1025.7891 radius 3.0 3.0 3.0
then
    00BC: show_text_highpriority GXT 'M11_013' time 4000 flag 1  // (Carrington) We're getting a positive reading - the internal comms hub is nearby.
    300@ = Marker.CreateAboveObject(200@)     // Primer internal hub
    Marker.SetColor(300@,0) // red
    $OBJ1_STATUS = 1
    32@=0
end
if and
0039: $OBJ1_STATUS == 1
0029:   32@ >= 4000 
then
    0050: gosub @JOANNA_RESPONSE    // (Joanna) Received.
    $OBJ1_STATUS = 2
end
if and
0039: $OBJ1_STATUS == 2
00FE: actor $PLAYER_ACTOR 0 near_point_3d 349.8629 189.2954 1019.9844 radius 1.0 1.0 1.0       
then
    if
    00E1:   player 0 pressed_key 15
    then
        03E6: remove_text_box
        0687: clear_actor $PLAYER_ACTOR task

        04ED: load_animation "bd_fire" 
        repeat
        wait 0 ms
        until 04EE: animation "bd_fire" loaded
        0605: actor $PLAYER_ACTOR perform_animation_sequence "wash_up" IFP_file "bd_fire" 10.0 loop 0 0 0 0 time 1500
        0173: set_actor $PLAYER_ACTOR Z_angle_to 90.0   

        wait 1000 ms
        04EF: release_animation "bd_fire"  
        00BC: show_text_highpriority GXT 'M11_040' time 4000 flag 1  // EMC Mine placed correctly.
        Marker.Disable(300@)    // Primer internal hub
        00BA: show_text_styled GXT 'MOBJ1' time 2000 style 2      //Objective 1: Completed
        $MOBJ1 = 1
        $OBJ1_STATUS = 3
    else
        if and
        0029:   33@ >= 10000
        88FE: not text_box_displayed
        then
        03E5: show_text_box 'M11_052' // Press ~s~ENTER~w~ to use ECM mines.
        33@=0
        end
    end
end
0051: return

{=================================================================
OBJECTIVE 2 - Obtain keycode necklace
==================================================================}
:M1_1_OBJECTIVE2
if
0039: $OBJ2_STATUS == 0
then
    :DEFECTION_SPAWNACTORS
    0247: load_model #wfystew
    0247: load_model #wfyri
    038B: load_requested_models
    :DEFECTION_SPAWNACTORS_LOAD
    00D6: if or
    8248:   not model #wfystew available 
    8248:   not model #wfyri available 
    004D: jump_if_false @DEFECTION_SPAWNACTORS_LOADED 
    0001: wait 0 ms 
    0002: jump @DEFECTION_SPAWNACTORS_LOAD 
    :DEFECTION_SPAWNACTORS_LOADED
    009A: Cassandra = create_actor_pedtype 5 model #wfystew at 376.5638 186.1377 1014.1875   // CIVFEMALE
    009A: Secretary = create_actor_pedtype 5 model #wfyri at 377.6356 185.3283 1014.1875     // CIVFEMALE
    0173: set_actor Cassandra Z_angle_to 139.6029 
    0173: set_actor Secretary Z_angle_to 129.2628
    060B: set_actor Cassandra decision_maker_to 400@
    060B: set_actor Secretary decision_maker_to 400@
    0249: release_model #wfystew
    0249: release_model #wfyri
    $OBJ2_STATUS = 1
end

if and       
00FE: actor $PLAYER_ACTOR 0 near_point_3d 367.9408 162.2078 1014.1884 radius 1.0 1.0 1.0
0039: $OBJ2_STATUS == 1
then
    00BC: show_text_highpriority GXT 'M11_014' time 4000 flag 1  // (Carrington) You're on the same floor as Cassandra's office.
    32@=0
    $OBJ2_STATUS = 2
end

if and
0039: $OBJ2_STATUS == 2
0029:   32@ >= 4000 
then
    if
    0039: $MISSION_DIFFICULTY == 2    // Special Agent
    then
        301@ = Marker.CreateAboveActor(Cassandra)
        0050: gosub @JOANNA_RESPONSE    // (Joanna) Understood.
        $OBJ2_STATUS = 4
    else 
        if
        0039: $MISSION_DIFFICULTY == 3    // Perfect Agent
        then
            018A: 301@ = create_checkpoint_at 320.1572 165.3247 1014.1797     // unlock computer
            03BC: 302@ = create_sphere_at 320.1572 165.3247 1014.1797 radius 1.0 
            08FB: set_checkpoint 301@ type_to 2   // 2: red
            0050: gosub @JOANNA_RESPONSE    // (Joanna) Understood.
            $OBJ2_STATUS = 3
        end 
    end
end
        
{HABILITANDO OBJ2 - ACCESANDO A LA COMPUTADORA (solo Perfect Agent)}    
if and
00FE: actor $PLAYER_ACTOR 0 near_point_3d 320.1572 165.3247 1014.1797 radius 1.0 1.0 1.0
0039: $OBJ2_STATUS == 3
then
    if
    00E1:   player 0 pressed_key 15
    then
        00BC: show_text_highpriority GXT 'M11_043' time 4000 flag 1  // Door unlocked.
        Marker.Disable(301@)
        03BD: destroy_sphere 302@ 
        301@ = Marker.CreateAboveActor(Cassandra)
        $OBJ2_STATUS = 4
        0108: destroy_object 202@
        0223: set_actor Cassandra health_to 10
    else
    00BC: show_text_highpriority GXT 'M11_051' time 1000 flag 1  // Press ~s~ENTER~w~ to access ~i~Security Locks.
    end          
end   

if and
0364:   actor Cassandra spotted_actor $PLAYER_ACTOR
0039:   $OBJ2_STATUS == 4
then
    074D: AS_actor Cassandra turns_to_and_look_at_actor $PLAYER_ACTOR timelimit -1
    074D: AS_actor Secretary turns_to_and_look_at_actor $PLAYER_ACTOR timelimit -1
    04ED: load_animation "lowrider" 
    04ED: load_animation "on_lookers" 
    repeat
    wait 0 ms
    until 04EE: animation "lowrider" loaded and 04EE: animation "on_lookers" loaded
    0605: actor Cassandra perform_animation_sequence "prtial_gngtlkD" IFP_file "lowrider" 10.0 loop 0 0 0 0 time -1
    0605: actor Secretary perform_animation_sequence "panic_in" IFP_file "on_lookers" 10.0 loop 0 0 0 0 time -1
    00BC: show_text_highpriority GXT 'M11_015' time 4000 flag 1  // (Cassandra) Who are you, and what are you doing here?
    05BA: AS_actor Cassandra move_mouth 4000 ms 
    $OTHER_TASKS_STATUS = 1                                       // (Secretary) Look out, she's got a gun!
    32@ = 0
    $OBJ2_STATUS = 5  
end    

if and
0039:   $OBJ2_STATUS == 5
0029:   32@ >= 4000
then
    0687: clear_actor Cassandra task
    074D: AS_actor Cassandra turns_to_and_look_at_actor $PLAYER_ACTOR timelimit -1
    //32@ = 0
    $OBJ2_STATUS = 6
end

if and
0039:   $OBJ2_STATUS == 6
0029:   32@ >= 4200
then
    05BA: AS_actor Cassandra move_mouth 4000 ms 
    074D: AS_actor Cassandra turns_to_and_look_at_actor $PLAYER_ACTOR timelimit -1
    0605: actor Cassandra perform_animation_sequence "prtial_gngtlkE" IFP_file "lowrider" 10.0 loop 1 0 0 0 time 3000
    00BC: show_text_highpriority GXT 'M11_017' time 3000 flag 1  // (Cassandra) You won't shoot me, foolish child!
    32@ = 0
    $OBJ2_STATUS = 7
end

if and
0039:   $OBJ2_STATUS == 7
0029:   32@ >= 3000
then
    05BA: AS_actor Cassandra move_mouth 4000 ms 
    074D: AS_actor Cassandra turns_to_and_look_at_actor $PLAYER_ACTOR timelimit -1
    0605: actor Cassandra perform_animation_sequence "prtial_gngtlkF" IFP_file "lowrider" 10.0 loop 1 0 0 0 time 3000
    00BC: show_text_highpriority GXT 'M11_018' time 3000 flag 1  // (Cassandra) Don't you know who I am?
    32@ = 0
    $OBJ2_STATUS = 8
end

if and
0039:   $OBJ2_STATUS == 8
0029:   32@ >= 5000
then
    05BA: AS_actor Cassandra move_mouth 4000 ms 
    05D3: AS_actor Cassandra goto_point 374.2972 195.3371 1014.1875 mode 4 -1 ms
    00BC: show_text_highpriority GXT 'M11_019' time 4000 flag 1  // (Cassandra) Let's see how you deal with security.
    $OBJ2_STATUS = 9
end

if and
0039:   $OBJ2_STATUS == 9
00FE:   actor Cassandra sphere 0 in_sphere 374.2972 195.3371 1014.1875 radius 1.0 1.0 1.0
then
    0729: AS_actor Cassandra hold_cellphone 1 
    32@ = 0
    $OBJ2_STATUS = 10
end
 
if and
0039:   $OBJ2_STATUS == 10
0029:   32@ >= 5000
then
    05BA: AS_actor Cassandra move_mouth 4000 ms 
    074D: AS_actor Cassandra turns_to_and_look_at_actor $PLAYER_ACTOR timelimit -1
    0729: AS_actor Cassandra hold_cellphone 0
    $OBJ2_STATUS = 11
    32@ = 0
    00BC: show_text_highpriority GXT 'M11_020' time 4000 flag 1  // (Cassandra) You will regret this intrusion, girl.
end

if and
0039:   $OBJ2_STATUS == 11
0029:   32@ >= 8000
0104:   actor $PLAYER_ACTOR near_actor Cassandra radius 5.0 5.0 5.0 sphere 0
then
    $OBJ2_STATUS = 12
    32@ = 0
    05BA: AS_actor Cassandra move_mouth 4000 ms 
    074D: AS_actor Cassandra turns_to_and_look_at_actor $PLAYER_ACTOR timelimit -1
    0605: actor Cassandra perform_animation_sequence "prtial_gngtlkE" IFP_file "lowrider" 10.0 loop 1 0 0 0 time 3000
    00BC: show_text_highpriority GXT 'M11_021' time 4000 flag 1  // (Cassandra) How dare you disturb me!
end

if and
0039:   $OBJ2_STATUS == 12
0029:   32@ >= 8000
0104:   actor $PLAYER_ACTOR near_actor Cassandra radius 5.0 5.0 5.0 sphere 0
then
    $OBJ2_STATUS = 13
    32@ = 0
    05BA: AS_actor Cassandra move_mouth 4000 ms 
    074D: AS_actor Cassandra turns_to_and_look_at_actor $PLAYER_ACTOR timelimit -1
    0605: actor Cassandra perform_animation_sequence "prtial_gngtlkE" IFP_file "lowrider" 10.0 loop 1 0 0 0 time 3000
    00BC: show_text_highpriority GXT 'M11_022' time 4000 flag 1  // (Cassandra) If I were you, I'd leave… NOW!
end

{OBJETIVO 2}    
if
0118:   actor Cassandra dead
then
    Marker.Disable(301@)
    if 
    02D8:   actor $PLAYER_ACTOR current_weapon == 0    // Unarmed
    then
        00BE: text_clear_all 
        00BC: show_text_highpriority GXT 'M11_044' time 4000 flag 1  // Picked up De Vries' necklace.
        00BA: show_text_styled GXT 'MOBJ2' time 2000 style 2      //Objective 2: Completed           
        $MOBJ2 = 1
        04EF: release_animation "lowrider"
        04EF: release_animation "on_lookers"
    else
        00BC: show_text_highpriority GXT 'M11_050' time 4000 flag 1  // Critical mission personnel killed.
        $OTHER_TASKS_STATUS = 99    // jump to fail mission routine
    end
end
0051: return

{=================================================================
OBJECTIVE 3 - Download project files
==================================================================}
:M1_1_OBJECTIVE3
if
0038:   $OBJ3_STATUS == 0    
then
    0247: load_model #vmaff3
    038B: load_requested_models         
    :LOAD    
    00D6: if 
    8248:   not model #vmaff3 available 
    004D: jump_if_false @LOADED 
    wait 0 ms
    jump @LOAD        
    :LOADED
    009A: dDWorker = create_actor_pedtype 4 model #vmaff3 at 329.2845 187.9877 1014.1875 
    0173: set_actor dDWorker Z_angle_to 0.0
    060B: set_actor dDWorker decision_maker_to 400@
    0249: release_model #vmaff3
    $OBJ3_STATUS = 1         
end
    
if and
0038:   $OBJ3_STATUS == 1    
00FE:   actor $PLAYER_ACTOR sphere 0 in_sphere 336.9054 181.9406 1014.1875 radius 3.0 3.0 3.0
then
    304@ = Marker.CreateAboveActor(dDWorker)
    07E0: set_marker 304@ type_to 1  //0: red / 1: blue
    0729: AS_actor dDWorker hold_cellphone 1  
    00BC: show_text_highpriority GXT 'M11_054' time 4000 flag 1  // ((Phone rings))
    wait 4000 ms
    00BC: show_text_highpriority GXT 'M11_025' time 4000 flag 1  // (dataDyne Worker) Yes, yes, I agree. Personality is expendable in this case.
    wait 4000 ms//32@ = 0
    00BC: show_text_highpriority GXT 'M11_026' time 4000 flag 1  // (dataDyne Worker) No, I can start the process from my office terminal and delete the necessary files at the same time.
    wait 4000 ms//32@ = 0
    0729: AS_actor dDWorker hold_cellphone 0  
    00BC: show_text_highpriority GXT 'M11_027' time 4000 flag 1  // (dataDyne Worker) I'll begin at once. Goodbye.
    wait 4000 ms//32@ = 0
    05D3: AS_actor dDWorker goto_point 335.1003 184.2019 1014.1875 mode 4 -1 ms  // caminata no inteligente
    0905: set_door 203@ locked 0 
    $OBJ3_STATUS = 4
end

if and
0038:   $OBJ3_STATUS == 4
00FE:   actor dDWorker sphere 0 in_sphere 335.1003 184.2019 1014.1875 radius 1.0 1.0 1.0
then
    05F5: AS_actor dDWorker goto_point 353.7029 165.6282 1019.9844 mode 4 -1 ms  // caminata inteligente
    $OBJ3_STATUS = 11
end

{if or
0038:   $OBJ3_STATUS == 1 
0038:   $OBJ3_STATUS == 2 
0038:   $OBJ3_STATUS == 3
0038:   $OBJ3_STATUS == 4
then
    if
    0364:   actor dDWorker spotted_actor $PLAYER_ACTOR
    then
    $OTHER_TASKS_STATUS = 99    // jump to fail mission routine - jump @DEFECTION_FAIL
    end       
end}

if
0038:   $OBJ3_STATUS == 11
then 
    if 09ED:   actor $PLAYER_ACTOR is_within_field_of_view_actor dDWorker
    then
        074D: AS_actor dDWorker turns_to_and_look_at_actor $PLAYER_ACTOR timelimit -1 
        05C3: AS_actor dDWorker hands_cower
        00BC: show_text_highpriority GXT 'M11_028' time 4000 flag 1  // (dataDyne Worker) Help - Intruder!
        wait 4000 ms
        00BC: show_text_highpriority GXT 'M11_029' time 4000 flag 1  // (dataDyne Worker) Security… help me!
        05F5: AS_actor dDWorker goto_point 347.3622 165.5928 1014.1875 mode 6 -1 ms
        wait 4000 ms
        $OBJ3_STATUS = 5     
    end
end
    
if or
0038:   $OBJ3_STATUS == 5
0038:   $OBJ3_STATUS == 6 
then
    if and
    0457:   player $PLAYER_CHAR aiming_at_actor dDWorker
    0104:   actor $PLAYER_ACTOR near_actor dDWorker radius 4.0 4.0 4.0 sphere 0
    then
        05C4: AS_actor dDWorker hands_up 8000 ms 
        if
        0038:   $OBJ3_STATUS == 5
        then
        00BC: show_text_highpriority GXT 'M11_031' time 4000 flag 1  // (Joanna) I've got a password problem, and you're the man to help me with it.
        else  //$OBJ3_STATUS == 6
        00BC: show_text_highpriority GXT 'M11_033' time 4000 flag 1  // (Joanna) Move it!
        end 
        wait 4000 ms
        00BC: show_text_highpriority GXT 'M11_032' time 4000 flag 1  // (dataDyne Worker) Don't shoot, don't shoot!
        wait 4000 ms
        05F5: AS_actor dDWorker goto_point 353.7029 165.6282 1019.9844 mode 4 -1 ms
        $OBJ3_STATUS = 7
    end
end
    
if and
0038:   $OBJ3_STATUS == 7
00FE:   actor dDWorker sphere 0 in_sphere 353.7029 165.6282 1019.9844 radius 1.0 1.0 1.0 
then 
    05D3: AS_actor dDWorker goto_point 351.3848 161.4195 1019.9912 mode 4 -1 ms
    $OBJ3_STATUS = 8
end
    
if and
0038:   $OBJ3_STATUS == 8
00FE:   actor dDWorker sphere 0 in_sphere 351.3848 161.4195 1019.9912 radius 1.0 1.0 1.0 
then
    05D3: AS_actor dDWorker goto_point 354.6878 161.0207 1019.9844 mode 4 -1 ms
    $OBJ3_STATUS = 9
end     
    
if and
0038:   $OBJ3_STATUS == 9
00FE:   actor dDWorker sphere 0 in_sphere 354.6878 161.0207 1019.9844 radius 1.0 1.0 1.0 
then
    05D3: AS_actor dDWorker goto_point 354.5591 162.6714 1019.9844 mode 4 -1 ms
    $OBJ3_STATUS = 10
end     
    
    
if or
0038:   $OBJ3_STATUS == 7
0038:   $OBJ3_STATUS == 8
0038:   $OBJ3_STATUS == 9
0038:   $OBJ3_STATUS == 10
then
    if
    00FE:   actor dDWorker sphere 0 in_sphere 354.5591 162.6714 1019.9844 radius 1.0 1.0 1.0 
    then
        0687: clear_actor dDWorker task
        04ED: load_animation "bd_fire" 
        repeat
        wait 0 ms
        until 04EE: animation "bd_fire" loaded
        0605: actor dDWorker perform_animation_sequence "wash_up" IFP_file "bd_fire" 10.0 loop 1 0 0 0 time -1
        04EF: release_animation "bd_fire"  
        00BC: show_text_highpriority GXT 'M11_035' time 4000 flag 1  // (dataDyne Worker) I… I'm logging on now.
        wait 4000 ms
        00BC: show_text_highpriority GXT 'M11_036' time 4000 flag 1  // (dataDyne Worker) Right, I'm in…
        0164: disable_marker 304@
        304@ = Marker.CreateAboveActor(dDWorker)
        07E0: set_marker 304@ type_to 0  //0: red / 1: blue
        32@ = 0
        $OBJ3_STATUS = 12
    else
        if
        8104:   not actor $PLAYER_ACTOR near_actor dDWorker radius 7.0 7.0 7.0 sphere 0
        then
            00BC: show_text_highpriority GXT 'M11_030' time 4000 flag 1  // (dataDyne Worker) HELP, HELP!
            05F5: AS_actor dDWorker goto_point 347.3622 165.5928 1014.1875 mode 6 -1 ms
            wait 4000 ms
            $OBJ3_STATUS = 6
        end
    end
end
                        
if
0038:   $OBJ3_STATUS == 12
then
    if and 
    0029: 32@ >= 4000
    8118:   not actor dDWorker dead 
    then
        00BC: show_text_highpriority GXT 'M11_037' time 4000 flag 1  // (dataDyne Worker) Goodbye, Dr Caroll.
        $OTHER_TASKS_STATUS = 99    // jump to fail mission routine
    end
        
    if
    0118:   actor dDWorker dead
    then
        Marker.Disable(304@)
        03BC: 304@ = create_sphere_at 354.6705 162.6868 1019.9844 radius 1.0
        018A: 305@ = create_checkpoint_at 354.6705 162.6868 1019.9844             
        Marker.SetColor(305@,0) // red
        $OBJ3_STATUS = 13
    end
end
    
if and
00FE: actor $PLAYER_ACTOR 0 near_point_3d 354.6705 162.6868 1019.9844 radius 1.0 1.0 1.0
0039: $OBJ3_STATUS == 13
then
    if
    00E1:   player 0 pressed_key 15
    then
        00BC: show_text_highpriority GXT 'M11_046' time 4000 flag 1  // File download initiated.
        03BD: destroy_sphere 304@ 
        0164: disable_marker 305@ 
        wait 6000 ms
        00BC: show_text_highpriority GXT 'M11_047' time 4000 flag 1  // File download completed.           
        00BA: show_text_styled GXT 'MOBJ3' time 2000 style 2      //Objective 3: Completed           
        $OBJ3_STATUS = 14
        $MOBJ3 = 1
    else
    00BC: show_text_highpriority GXT 'M11_053' time 1000 flag 1  // Press ~s~ENTER~w~ to download project files.
    end          
end
    
if and
8039: not $OBJ3_STATUS == 12
8039: not $OBJ3_STATUS == 13
8039: not $OBJ3_STATUS == 14
0118:   actor dDWorker dead
then
    00BC: show_text_highpriority GXT 'M11_050' time 4000 flag 1  // Critical mission personnel killed.
    $OTHER_TASKS_STATUS = 99    // jump to fail mission routine
end       
0051: return

{=================================================================
OBJECTIVE 4 - Disable external comms hub
==================================================================}
:M1_1_OBJECTIVE4
{HABILITANDO OBJ4 - SEGUNDO INTERNAL HUB}    
if and
0039: $MOBJ1 == 1
0039: $OBJ4_STATUS == 0
then
    if or
    00FE: actor $PLAYER_ACTOR 0 near_point_3d 364.8702 182.2754 1008.3828 radius 2.0 2.0 2.0
    00FE: actor $PLAYER_ACTOR 0 near_point_3d 370.881 180.8616 1008.3828 radius 2.0 2.0 2.0
    then
        00BC: show_text_highpriority GXT 'M11_038' time 4000 flag 1  // (Joanna) The other hub has got to be in the security room.
        303@ = Marker.CreateAboveObject(201@)
        Marker.SetColor(303@,0)   // red
        $OBJ4_STATUS = 1
    end
end            
if and
0039: $OBJ4_STATUS == 1
00FE: actor $PLAYER_ACTOR 0 near_point_3d 376.2914 183.6349 1008.3828 radius 1.0 1.0 1.0
then
    if
    00E1:   player 0 pressed_key 15
    then
        03E6: remove_text_box
        0687: clear_actor $PLAYER_ACTOR task
        
        04ED: load_animation "bd_fire" 
        repeat
        wait 0 ms
        until 04EE: animation "bd_fire" loaded
        0605: actor $PLAYER_ACTOR perform_animation_sequence "wash_up" IFP_file "bd_fire" 10.0 loop 0 0 0 0 time 1500
        0173: set_actor $PLAYER_ACTOR Z_angle_to 270.0

        wait 1000 ms
        04EF: release_animation "bd_fire"  
        00BC: show_text_highpriority GXT 'M11_040' time 4000 flag 1  // EMC Mine placed correctly.
        Marker.Disable(303@)
        if
        0039: $MISSION_DIFFICULTY == 2  // Special Agent
        then
        00BA: show_text_styled GXT 'MOBJ3' time 2000 style 2      //Objective 3: Completed
        $MOBJ3 = 1
        else                            // Perfect Agent
        00BA: show_text_styled GXT 'MOBJ4' time 2000 style 2      //Objective 4: Completed
        $MOBJ4 = 1
        end
        32@=0
        $OTHER_TASKS_STATUS=4
    else
        if and
        0029:   33@ >= 10000
        88FE: not text_box_displayed
        then
        03E5: show_text_box 'M11_052' // Press ~s~ENTER~w~ to use ECM mines.
        33@=0
        end
    end                        
end
0051: return

{=================================================================
Create Enemies
==================================================================}
:DEFECTION_ENEMIES1
0247: load_model #laemt1    // El negrito
0247: load_model #lvemt1    // El Naco
0247: load_model #sfemt1    // El blanco
0247: load_model #micro_uzi
038B: load_requested_models

repeat
wait 0 ms
until 0248: model #laemt1 available and 0248: model #lvemt1 available and 0248: model #sfemt1 available and 0248: model #micro_uzi available

009A: 23@ = create_actor_pedtype 4 model #laemt1 at 347.3132 162.0751 1025.7891
009A: 24@ = create_actor_pedtype 4 model #sfemt1 at 359.7106 154.128 1025.7964
009A: 25@ = create_actor_pedtype 4 model #sfemt1 at 372.9938 176.4716 1019.9844
009A: 26@ = create_actor_pedtype 4 model #lvemt1 at 350.1267 197.892 1019.9844
009A: 27@ = create_actor_pedtype 4 model #lvemt1 at 349.27 167.5185 1019.9844
009A: 28@ = create_actor_pedtype 4 model #laemt1 at 356.2734 162.5072 1019.9844
009A: 29@ = create_actor_pedtype 4 model #sfemt1 at 370.5733 175.1353 1014.1797
009A: 30@ = create_actor_pedtype 4 model #lvemt1 at 338.587 193.7971 1014.1797
009A: 31@ = create_actor_pedtype 4 model #lvemt1 at 320.0589 183.3739 1014.1875
009A: 32@ = create_actor_pedtype 4 model #sfemt1 at 319.8577 192.989 1014.1875
009A: 33@ = create_actor_pedtype 4 model #laemt1 at 328.7975 164.0612 1014.1797
009A: 34@ = create_actor_pedtype 4 model #laemt1 at 330.7495 149.8253 1014.1875
009A: 35@ = create_actor_pedtype 4 model #lvemt1 at 326.6021 156.345 1014.1797
0173: set_actor 23@ Z_angle_to 270.0 
0173: set_actor 24@ Z_angle_to 270.0 
0173: set_actor 25@ Z_angle_to 90.0 
0173: set_actor 26@ Z_angle_to 180.0 
0173: set_actor 27@ Z_angle_to 337.0 
0173: set_actor 28@ Z_angle_to 121.0 
0173: set_actor 29@ Z_angle_to 90.0 
0173: set_actor 30@ Z_angle_to 90.0 
0173: set_actor 31@ Z_angle_to 308.0 
0173: set_actor 32@ Z_angle_to 226.0 
0173: set_actor 33@ Z_angle_to 90.0 
0173: set_actor 34@ Z_angle_to 0.0 
0173: set_actor 35@ Z_angle_to 0.0 
01B2: give_actor 23@ weapon 28 ammo $SubMachineAmmo
01B2: give_actor 24@ weapon 28 ammo $SubMachineAmmo
01B2: give_actor 25@ weapon 28 ammo $SubMachineAmmo
01B2: give_actor 26@ weapon 28 ammo $SubMachineAmmo
01B2: give_actor 27@ weapon 28 ammo $SubMachineAmmo
01B2: give_actor 28@ weapon 28 ammo $SubMachineAmmo
01B2: give_actor 29@ weapon 28 ammo $SubMachineAmmo
01B2: give_actor 30@ weapon 28 ammo $SubMachineAmmo
01B2: give_actor 31@ weapon 28 ammo $SubMachineAmmo
01B2: give_actor 32@ weapon 28 ammo $SubMachineAmmo
01B2: give_actor 33@ weapon 28 ammo $SubMachineAmmo
01B2: give_actor 34@ weapon 28 ammo $SubMachineAmmo
01B2: give_actor 35@ weapon 28 ammo $SubMachineAmmo
01B9: set_actor 23@ armed_weapon_to 28
01B9: set_actor 24@ armed_weapon_to 28
01B9: set_actor 25@ armed_weapon_to 28
01B9: set_actor 26@ armed_weapon_to 28
01B9: set_actor 27@ armed_weapon_to 28
01B9: set_actor 28@ armed_weapon_to 28
01B9: set_actor 29@ armed_weapon_to 28
01B9: set_actor 30@ armed_weapon_to 28
01B9: set_actor 31@ armed_weapon_to 28
01B9: set_actor 32@ armed_weapon_to 28
01B9: set_actor 33@ armed_weapon_to 28
01B9: set_actor 34@ armed_weapon_to 28
01B9: set_actor 35@ armed_weapon_to 28
02E2: set_actor 23@ weapon_accuracy_to $MissionAccuracy
02E2: set_actor 24@ weapon_accuracy_to $MissionAccuracy
02E2: set_actor 25@ weapon_accuracy_to $MissionAccuracy
02E2: set_actor 26@ weapon_accuracy_to $MissionAccuracy
02E2: set_actor 27@ weapon_accuracy_to $MissionAccuracy
02E2: set_actor 28@ weapon_accuracy_to $MissionAccuracy
02E2: set_actor 29@ weapon_accuracy_to $MissionAccuracy
02E2: set_actor 30@ weapon_accuracy_to $MissionAccuracy
02E2: set_actor 31@ weapon_accuracy_to $MissionAccuracy
02E2: set_actor 32@ weapon_accuracy_to $MissionAccuracy
02E2: set_actor 33@ weapon_accuracy_to $MissionAccuracy
02E2: set_actor 34@ weapon_accuracy_to $MissionAccuracy
02E2: set_actor 35@ weapon_accuracy_to $MissionAccuracy
0223: set_actor 23@ health_to $MissionHealth
0223: set_actor 24@ health_to $MissionHealth
0223: set_actor 25@ health_to $MissionHealth
0223: set_actor 26@ health_to $MissionHealth
0223: set_actor 27@ health_to $MissionHealth
0223: set_actor 28@ health_to $MissionHealth
0223: set_actor 29@ health_to $MissionHealth
0223: set_actor 30@ health_to $MissionHealth
0223: set_actor 31@ health_to $MissionHealth
0223: set_actor 32@ health_to $MissionHealth
0223: set_actor 33@ health_to $MissionHealth
0223: set_actor 34@ health_to $MissionHealth
0223: set_actor 35@ health_to $MissionHealth
077A: set_char_relationship 23@ relationship 4 to_ped_type 0
077A: set_char_relationship 24@ relationship 4 to_ped_type 0
077A: set_char_relationship 25@ relationship 4 to_ped_type 0
077A: set_char_relationship 26@ relationship 4 to_ped_type 0
077A: set_char_relationship 27@ relationship 4 to_ped_type 0
077A: set_char_relationship 28@ relationship 4 to_ped_type 0
077A: set_char_relationship 29@ relationship 4 to_ped_type 0
077A: set_char_relationship 30@ relationship 4 to_ped_type 0
077A: set_char_relationship 31@ relationship 4 to_ped_type 0
077A: set_char_relationship 32@ relationship 4 to_ped_type 0
077A: set_char_relationship 33@ relationship 4 to_ped_type 0
077A: set_char_relationship 34@ relationship 4 to_ped_type 0
077A: set_char_relationship 35@ relationship 4 to_ped_type 0

0249: release_model #laemt1
0249: release_model #lvemt1
0249: release_model #sfemt1
0249: release_model #micro_uzi

// Rutas para actores
05D6: clear_scmpath 
05D7: add_point_to_scmpath 378.2578 162.3785 1025.7891
05D7: add_point_to_scmpath 347.3132 162.0751 1025.7891
05D8: AS_assign_scmpath to_actor 23@ flags 4 2

05D6: clear_scmpath 
05D7: add_point_to_scmpath 328.0312 176.2375 1019.9912
05D7: add_point_to_scmpath 327.949 171.1322 1019.9844
05D7: add_point_to_scmpath 366.5492 171.1225 1019.9844
05D7: add_point_to_scmpath 366.6825 176.4342 1019.9844
05D7: add_point_to_scmpath 372.9938 176.4716 1019.9844
05D8: AS_assign_scmpath to_actor 25@ flags 4 2

05D6: clear_scmpath 
05D7: add_point_to_scmpath 350.1241 189.4005 1019.9844
05D7: add_point_to_scmpath 362.969 188.9214 1019.9912
05D7: add_point_to_scmpath 362.607 197.4305 1019.9844
05D7: add_point_to_scmpath 350.1267 197.892 1019.9844
05D8: AS_assign_scmpath to_actor 26@ flags 4 2

05D6: clear_scmpath 
05D7: add_point_to_scmpath 336.1253 175.9453 1014.1875
05D7: add_point_to_scmpath 335.8254 155.9933 1014.1797
05D7: add_point_to_scmpath 348.7267 155.5716 1014.1875
05D7: add_point_to_scmpath 339.0721 155.2876 1014.1875
05D7: add_point_to_scmpath 338.9924 171.5749 1014.1875
05D7: add_point_to_scmpath 369.6134 171.7658 1014.1875
05D7: add_point_to_scmpath 370.5733 175.1353 1014.1797
05D8: AS_assign_scmpath to_actor 29@ flags 4 2

05D6: clear_scmpath 
05D7: add_point_to_scmpath 359.8943 193.8484 1014.1875
05D7: add_point_to_scmpath 338.587 193.7971 1014.1797
05D8: AS_assign_scmpath to_actor 30@ flags 4 2

05D6: clear_scmpath 
05D7: add_point_to_scmpath 321.942 164.2955 1014.1797
05D7: add_point_to_scmpath 328.7975 164.0612 1014.1797
05D8: AS_assign_scmpath to_actor 33@ flags 4 2

0051: return 

{=================================================================
Create Floor 0 Enemies
==================================================================}
:DEFECTION_FLOOR0_ENEMIES
0247: load_model #swat
0247: load_model #mp5lng
038B: load_requested_models
    
:DEFECTION_FLOOR0_ENEMIES_LOAD
00D6: if or
8248:   not model #swat available 
8248:   not model #mp5lng available 
004D: jump_if_false @DEFECTION_FLOOR0_ENEMIES_LOADED 
0001: wait 0 ms 
0002: jump @DEFECTION_FLOOR0_ENEMIES_LOAD 
    
:DEFECTION_FLOOR0_ENEMIES_LOADED
009A: 10@ = create_actor_pedtype 6 model #swat at 383.4605 176.6676 1008.3828
009A: 11@ = create_actor_pedtype 6 model #swat at 383.4929 170.9342 1008.3828
009A: 12@ = create_actor_pedtype 6 model #swat at 355.8498 170.9398 1008.3773
009A: 13@ = create_actor_pedtype 6 model #swat at 355.6469 176.8063 1008.3778
009A: 14@ = create_actor_pedtype 6 model #swat at 363.6854 161.2624 1008.3828
009A: 15@ = create_actor_pedtype 6 model #swat at 371.0025 187.0153 1008.3893
009A: 36@ = create_actor_pedtype 6 model #swat at 368.0143 192.6443 1008.3828
0173: set_actor 10@ Z_angle_to 90.0 
0173: set_actor 11@ Z_angle_to 90.0 
0173: set_actor 12@ Z_angle_to 270.0 
0173: set_actor 13@ Z_angle_to 270.0 
0173: set_actor 14@ Z_angle_to 0.0 
0173: set_actor 15@ Z_angle_to 0.0 
0173: set_actor 36@ Z_angle_to 90.0 
01B2: give_actor 10@ weapon 29 ammo $SubMachineAmmo
01B2: give_actor 11@ weapon 29 ammo $SubMachineAmmo
01B2: give_actor 12@ weapon 29 ammo $SubMachineAmmo
01B2: give_actor 13@ weapon 29 ammo $SubMachineAmmo
01B2: give_actor 14@ weapon 29 ammo $SubMachineAmmo
01B2: give_actor 15@ weapon 29 ammo $SubMachineAmmo
01B2: give_actor 36@ weapon 29 ammo $SubMachineAmmo
01B9: set_actor 10@ armed_weapon_to 29
01B9: set_actor 11@ armed_weapon_to 29
01B9: set_actor 12@ armed_weapon_to 29
01B9: set_actor 13@ armed_weapon_to 29
01B9: set_actor 14@ armed_weapon_to 29
01B9: set_actor 15@ armed_weapon_to 29
01B9: set_actor 36@ armed_weapon_to 29
02E2: set_actor 10@ weapon_accuracy_to $MissionAccuracy
02E2: set_actor 11@ weapon_accuracy_to $MissionAccuracy
02E2: set_actor 12@ weapon_accuracy_to $MissionAccuracy
02E2: set_actor 13@ weapon_accuracy_to $MissionAccuracy
02E2: set_actor 14@ weapon_accuracy_to $MissionAccuracy
02E2: set_actor 15@ weapon_accuracy_to $MissionAccuracy
02E2: set_actor 36@ weapon_accuracy_to $MissionAccuracy
0223: set_actor 10@ health_to $MissionHealth
0223: set_actor 11@ health_to $MissionHealth
0223: set_actor 12@ health_to $MissionHealth
0223: set_actor 13@ health_to $MissionHealth
0223: set_actor 14@ health_to $MissionHealth
0223: set_actor 15@ health_to $MissionHealth
0223: set_actor 36@ health_to $MissionHealth
077A: set_char_relationship 10@ relationship 4 to_ped_type 0
077A: set_char_relationship 11@ relationship 4 to_ped_type 0
077A: set_char_relationship 12@ relationship 4 to_ped_type 0
077A: set_char_relationship 13@ relationship 4 to_ped_type 0
077A: set_char_relationship 14@ relationship 4 to_ped_type 0
077A: set_char_relationship 15@ relationship 4 to_ped_type 0
077A: set_char_relationship 36@ relationship 4 to_ped_type 0
0249: release_model #swat
0249: release_model #mp5lng
0051: return

{=================================================================
Create Basement Enemies
==================================================================}
:DEFECTION_BASEMENT_ENEMIES
0247: load_model #laemt1    // El negrito
0247: load_model #lvemt1    // El Naco
0247: load_model #sfemt1    // El blanco
0247: load_model #micro_uzi
0247: load_model #swat
0247: load_model #mp5lng
038B: load_requested_models
    
:DEFECTION_BASEMENT_ENEMIES_LOAD
00D6: if or
8248:   not model #laemt1 available 
8248:   not model #lvemt1 available 
8248:   not model #sfemt1 available 
8248:   not model #micro_uzi available 
8248:   not model #swat available 
8248:   not model #mp5lng available 
004D: jump_if_false @DEFECTION_BASEMENT_ENEMIES_LOADED 
0001: wait 0 ms 
0002: jump @DEFECTION_BASEMENT_ENEMIES_LOAD 
    
:DEFECTION_BASEMENT_ENEMIES_LOADED
009A: 36@ = create_actor_pedtype 6 model #laemt1 at 366.5355 215.915 1008.3828
009A: 37@ = create_actor_pedtype 6 model #lvemt1 at 358.1165 209.1479 1008.3828
009A: 38@ = create_actor_pedtype 6 model #sfemt1 at 358.0892 201.175 1008.3828
009A: 39@ = create_actor_pedtype 6 model #swat at 354.9982 206.7606 1008.3828
009A: 40@ = create_actor_pedtype 6 model #swat at 362.6396 208.0461 1008.3828
0173: set_actor 36@ Z_angle_to 180.0 
0173: set_actor 37@ Z_angle_to 180.0 
0173: set_actor 38@ Z_angle_to 315.0 
0173: set_actor 39@ Z_angle_to 270.0 
0173: set_actor 40@ Z_angle_to 0.0 
01B2: give_actor 36@ weapon 28 ammo $SubMachineAmmo
01B2: give_actor 37@ weapon 28 ammo $SubMachineAmmo
01B2: give_actor 38@ weapon 28 ammo $SubMachineAmmo
01B2: give_actor 39@ weapon 29 ammo $SubMachineAmmo
01B2: give_actor 40@ weapon 29 ammo $SubMachineAmmo
01B9: set_actor 36@ armed_weapon_to 28
01B9: set_actor 37@ armed_weapon_to 28
01B9: set_actor 38@ armed_weapon_to 28
01B9: set_actor 39@ armed_weapon_to 29
01B9: set_actor 40@ armed_weapon_to 29
02E2: set_actor 36@ weapon_accuracy_to $MissionAccuracy
02E2: set_actor 37@ weapon_accuracy_to $MissionAccuracy
02E2: set_actor 38@ weapon_accuracy_to $MissionAccuracy
02E2: set_actor 39@ weapon_accuracy_to $MissionAccuracy
02E2: set_actor 40@ weapon_accuracy_to $MissionAccuracy
0223: set_actor 36@ health_to $MissionHealth
0223: set_actor 37@ health_to $MissionHealth
0223: set_actor 38@ health_to $MissionHealth
0223: set_actor 39@ health_to $MissionHealth
0223: set_actor 40@ health_to $MissionHealth
077A: set_char_relationship 36@ relationship 4 to_ped_type 0
077A: set_char_relationship 37@ relationship 4 to_ped_type 0
077A: set_char_relationship 38@ relationship 4 to_ped_type 0
077A: set_char_relationship 39@ relationship 4 to_ped_type 0
077A: set_char_relationship 40@ relationship 4 to_ped_type 0

0249: release_model #laemt1
0249: release_model #lvemt1
0249: release_model #sfemt1
0249: release_model #micro_uzi
0249: release_model #swat
0249: release_model #mp5lng
0051: return 

{=================================================================
Final routines
==================================================================}
//––––––––––––-
:DEFECTION_PASSED
0394: play_music 1
$TUT_MISSIONS_PASSED += 1
Player.Money($PLAYER_CHAR) += 10000
01E3: show_text_1number_styled GXT 'M_PASS' number 10000 time 5000 style 1 // MISSION PASSED!~n~~w~$~1~
return

//––––––––––––-
:DEFECTION_FAIL
00BA: show_text_styled GXT 'M_FAIL' time 5000 style 1 // ~r~MISSION FAILED!
//0555: remove_weapon 31 from_actor $PLAYER_ACTOR
return

//––––––––––––-
:DEFECTION_CLEANUP
$ONMISSION = 0
// Actors
01C2: remove_references_to_actor Carrington // Like turning an actor into a random pedestrian
01C2: remove_references_to_actor 2@ // Like turning an actor into a random pedestrian
01C2: remove_references_to_actor Cassandra // Like turning an actor into a random pedestrian
01C2: remove_references_to_actor Secretary // Like turning an actor into a random pedestrian
01C2: remove_references_to_actor dDWorker // Like turning an actor into a random pedestrian
01C3: remove_references_to_car Helicopter  // Like turning a car into any random car
01C2: remove_references_to_actor 16@
01C2: remove_references_to_actor 17@
01C2: remove_references_to_actor 18@
01C2: remove_references_to_actor 19@
01C2: remove_references_to_actor 10@
01C2: remove_references_to_actor 11@
01C2: remove_references_to_actor 12@
01C2: remove_references_to_actor 13@
01C2: remove_references_to_actor 14@
01C2: remove_references_to_actor 15@
01C2: remove_references_to_actor 23@
01C2: remove_references_to_actor 24@
01C2: remove_references_to_actor 25@
01C2: remove_references_to_actor 26@
01C2: remove_references_to_actor 27@
01C2: remove_references_to_actor 28@
01C2: remove_references_to_actor 29@
01C2: remove_references_to_actor 30@
01C2: remove_references_to_actor 31@
01C2: remove_references_to_actor 32@
01C2: remove_references_to_actor 33@
01C2: remove_references_to_actor 34@
01C2: remove_references_to_actor 35@
01C2: remove_references_to_actor 36@
// Objects - Pick ups
01C4: remove_references_to_object 200@
01C4: remove_references_to_object 201@
01C4: remove_references_to_object 202@
01C4: remove_references_to_object 203@
01C4: remove_references_to_object 204@
01C4: remove_references_to_object 205@
0215: destroy_pickup 210@
0215: destroy_pickup 211@
// Markers - Spheres - Checkpoints
08E7: disable_entrance_markers 0
0A41: destroy_entrance_marker BuildingDoor
//0A41: destroy_entrance_marker BuildingDoor2
0A41: destroy_entrance_marker Elevator
Marker.Disable(3@)      // Helicopter
Marker.Disable(300@)    // Marker Primer internal hub
Marker.Disable(301@)    // Checkpoint para Computer Lock / Marker Cassandra
Marker.Disable(302@)    // Sphere para Computer Lock
Marker.Disable(303@)    // Marker Segundo internal hub - Elevador
Marker.Disable(304@)    // Marker dD Worker / Sphere download data
Marker.Disable(305@)    // Checkpoint download data
0164: disable_marker 8@ // Checkpoint en edificio
0164: disable_marker 9@ // Checkpoint 2 en edificio
06D6: disable_racing_checkpoint 5@   // anillo en edificio
// Other
010D: set_player $PLAYER_CHAR wanted_level_to 0
01F0: set_max_wanted_level_to 6 
065C: release_decision_maker 400@  // Comportamiento limpio para actores especiales
04EF: release_animation "lowrider"
04EF: release_animation "on_lookers"
04EF: release_animation "bd_fire"
$MOBJ1 = 0
$MOBJ2 = 0
$MOBJ3 = 0
$MOBJ4 = 0
$MOBJ5 = 0
$OBJ1_STATUS=0
$OBJ2_STATUS=0
$OBJ3_STATUS=0
$OBJ4_STATUS=0
$OBJ5_STATUS=0
$OTHER_TASKS_STATUS=0
$ALLOBJECTIVES=0
mission_cleanup
return







//––––-External script 0–––––
// put your external scripts here