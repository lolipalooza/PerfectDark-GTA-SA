{======================================================================
======================================================================}
{$CLEO .s}
thread 'Defection'

gosub @Defection_Start
if or
0117:   player $PLAYER_CHAR wasted
0741:   actor $PLAYER_ACTOR busted
then gosub @Defection_Fail
end
gosub @Defection_CleanUp
0A93: end_custom_thread

{======================================================================
======================================================================}
{$I common/common.txt}
{$I common/DialogueMachine.txt}

{======================================================================
======================================================================}
:Defection_GetObjectivesGxt // difficulty_id 0@
if 0038:   0@ == 1  // Agent
then
    10@s = 'M11OB5' // Gain entrance to laboratory
    12@s = 'DUMMY'
    14@s = 'DUMMY'
    16@s = 'DUMMY'
    18@s = 'DUMMY'
end
if 0038:   0@ == 2  // Special Agent
then
    10@s = 'M11OB1' // Disable internal security hub
    12@s = 'M11OB2' // Obtain keycode necklace
    14@s = 'M11OB4' // Disable external comms hub
    16@s = 'M11OB5' // Gain entrance to laboratory
    18@s = 'DUMMY'
end
if 0038:   0@ == 3  // Perfect Agent
then
    10@s = 'M11OB1' // Disable internal security hub
    12@s = 'M11OB2' // Obtain keycode necklace
    14@s = 'M11OB3' // Download project files
    16@s = 'M11OB4' // Disable external comms hub
    18@s = 'M11OB5' // Gain entrance to laboratory
end
0AB2: ret 10 objective1_gxt 10@ 11@ objective2_gxt 12@ 13@ objective3_gxt 14@ 15@ objective4_gxt 16@ 17@ objective5_gxt 18@ 19@

{======================================================================
======================================================================}
:Defection_Start
0@ += 1 // translating difficulty scale from 0,1,2 to 1,2,3

// Initialize Mission Time
00C0: set_current_time_hours_to 23 minutes_to 52

// Spawn actor
04E4: unknown_refresh_game_renderer_at -2053.0667 -183.5957 
03CB: set_rendering_origin_at -2053.0667 -183.5957 35.3274 
Actor.PutAt($PLAYER_ACTOR, -2053.0667, -183.5957, 35.3274)      //00A1: put_actor $PLAYER_ACTOR at -2053.0667 -183.5957 35.3274 
Actor.Angle($PLAYER_ACTOR) = 182.3029                           //0173: set_actor $PLAYER_ACTOR Z_angle_to 182.3029 
Camera.SetBehindPlayer()                                        //0373: set_camera_directly_behind_player 
Camera.Restore_WithJumpCut()                                    //02EB: restore_camera_with_jumpcut 

// Spawn Objects
0AB1: @CreateObject 7 label @Defection_Objects model #CJ_SLOT_PROPg position 349.1 189.345 1018.98 angle 90.0 offset 0
0AB1: @CreateObject 7 label @Defection_Objects model #CJ_SLOT_PROPg position 377.054 183.644 1007.33 angle -90.0 offset 4
if 0039: 0@ == 3  // Perfect Agent
then
    // #Gen_doorEXT07: blue door model
    0AB1: @CreateObject 7 label @Defection_Objects model #Gen_doorINT04 position 370.789 179.088 1013.17 angle 0.0 offset 8 ret 10@
    0905: set_door 10@ locked 1
    0AB1: @StoreData 3 label @Defection_Objects value 10@ offset 8
    
    0AB1: @CreateObject 7 label @Defection_Objects model #Gen_doorINT04 position 332.711 183.492 1013.17 angle 90.0 offset 12 ret 10@
    0905: set_door 10@ locked 1
    0AB1: @StoreData 3 label @Defection_Objects value 10@ offset 12
end
0AB1: @CreateObject 7 label @Defection_Objects model #freight_SFW_door position 358.785 217.56 1008.53 angle 270.0 offset 16
0AB1: @CreateObject 7 label @Defection_Objects model #freight_SFW_door position 353.811 175.78 1025.93 angle 90.0 offset 20

// Spawn Weapons/Pickups
if 8038: not 0@ == 3   // Agent or Special Agent
then 0AB1: @CreatePickup 7 label @Defection_Pickups model #BODYARMOUR position 349.0002 164.1536 1014.1875 type 3 offset 0 ret 10@ //0213: 210@ = create_pickup #BODYARMOUR type 3 at 349.0002 164.1536 1014.1875
end
0AB1: @CreateWeaponPickup 8 label @Defection_Pickups model #MP5LNG position 345.0554 164.1536 1014.1875 group 15 ammo 30 offset 4 ret 10@ //032B: 211@ = create_weapon_pickup #MP5LNG  group 15 ammo 30 at 345.0554 164.1536 1014.1875

//Load models
Model.Load(#silenced)
Model.Load(#micro_uzi)
Model.Load(#lvemt1)
Model.Load(#heck1)
Model.Load(#maverick)
Model.Load(#TRIADA)
Model.Load(#TRIADB)
Model.Load(#mp5lng)
038B: load_requested_models
repeat
wait 0
    if and
    0248:   not model #silenced available 
    0248:   not model #micro_uzi available 
    0248:   not model #lvemt1 available 
    0248:   not model #heck1 available 
    0248:   not model #maverick available 
    0248:   not model #triada available 
    0248:   not model #triadb available  
    0248:   not model #mp5lng available  
    then break
    end
until false

01B2: give_actor $PLAYER_ACTOR weapon 23 ammo 35
01B9: set_actor $PLAYER_ACTOR armed_weapon_to 23
0AB1: @CreateVehicle 9 label @Defection_Vehicles model #maverick position -2053.4119 -196.507 35.3274 angle 90.0 primary_color 0 secondary_color 54 offset 0 ret 2@
0AB1: @CreateMarker 4 label @Defection_Markers offset 0 type MARKER_ABOVE_CAR_TYPE target 2@ ret_marker 3@

// Carrington
0AB1: @CreateActor 8 label @Defection_ActorsAllies model #heck1 position -2057.8132 -192.0627 35.3203 angle 270.0 pedtype 24 offset 0 ret 11@

// CI guards
0AB1: @CreateActor 10 label @Defection_ActorsAllies model #TRIADA position -2058.8789 -190.7403 35.3203 angle 270.0 pedtype 24 offset 4 weapon 29 ammo 10 ret 12@
0AB1: @CreateActor 10 label @Defection_ActorsAllies model #TRIADB position -2058.8789 -193.2907 35.3203 angle 270.0 pedtype 24 offset 8 weapon 29 ammo 10 ret 13@
0AB1: @CreateActor 10 label @Defection_ActorsAllies model #TRIADA position -2050.4958 -193.2907 35.3274 angle 90.0 pedtype 24 offset 12 weapon 29 ammo 10 ret 14@
0AB1: @CreateActor 10 label @Defection_ActorsAllies model #TRIADB position -2050.4958 -190.7403 35.3203 angle 90.0 pedtype 24 offset 16 weapon 29 ammo 10 ret 15@

060A: create_decision_maker_type 0 store_to 20@
060B: set_actor 11@ decision_maker_to 20@
060B: set_actor 12@ decision_maker_to 20@
060B: set_actor 13@ decision_maker_to 20@
060B: set_actor 14@ decision_maker_to 20@
060B: set_actor 15@ decision_maker_to 20@
065C: release_decision_maker 20@

wait 1000 
fade 1 500
Player.CanMove($PLAYER_CHAR) = true
0249: release_model #silenced
0249: release_model #micro_uzi
0249: release_model #lvemt1
0249: release_model #heck1
0249: release_model #maverick
0249: release_model #triada
0249: release_model #triadb

10@ = 0
33@=60000

repeat
wait 0
    
    0AB1: @ForceRadioOff 0
    
    if or
    0117:   player $PLAYER_CHAR wasted
    0741:   actor $PLAYER_ACTOR busted
    then return
    end
    
    // Pause Menu
    0AB1: @Defection_GetObjectivesGxt 1 difficulty_id 0@ ret_objective1_gxt 12@ 13@ objective2_gxt 14@ 15@ objective3_gxt 16@ 17@ objective4_gxt 18@ 19@ objective5_gxt 20@ 21@    
    22@s = 'M1-1'
    0AB1: @GetObjectivesStatus 1 label @Defection_Objectives ret obj1_status 24@ obj2_status 25@ obj3_status 26@ obj4_status 27@ obj5_status 28@
    0AB1: @PauseMenu 17 mission_name_gxt 22@ 23@ objective1_gxt 12@ 13@ objective2_gxt 14@ 15@ objective3_gxt 16@ 17@ objective4_gxt 18@ 19@ objective5_gxt 20@ 21@ objective_flags 24@ 25@ 26@ 27@ 28@ ret 12@
    
    if 12@ == 1 // Aborted mission from pause menu
    then
        00BA: show_text_styled GXT 'M_ABR1' time 5000 style 1       // ~r~MISSION ABORTED
        00BB: show_text_lowpriority GXT 'M_ABR2' time 6000 flag 1   // ~r~You have aborted the mission.
        return
    end
    
    if 10@ == 0 // Take helicopter
    then
        if 00DB: actor $PLAYER_ACTOR in_car 2@
        then
            0164: disable_marker 3@
            018A: 3@ = create_checkpoint_at 1543.0636 -1348.7539 349.3356
            06D5: 4@ = create_racing_checkpoint_at 1543.0636 -1348.7539 349.3356 point_to 1522.0209 -1348.2252 349.7328 type 3 radius 3.0 
            08FB: set_checkpoint 3@ type_to 2   // 2: red
            0AB1: @DialogueMachine_Init 0
            10@ = 1
        end
    end
    
    if 10@ == 1 // Fly through helicopter to aerial checkpoint in top of building
    then
        0AB1: @DialogueMachine 2 num_dialogues 12 dialogues_array_label @Defection_IntroDialogue
        
        if 00FE: actor $PLAYER_ACTOR 0 near_point_3d 1543.0636 -1348.7539 349.3356 radius 3.0 3.0 3.0
        then
            01B4: toggle_player $PLAYER_CHAR can_move 0
            00A1: put_actor $PLAYER_ACTOR at 1551.4124 -1246.4515 277.8824
            0164: disable_marker 3@
            06D6: disable_racing_checkpoint 4@
            Car.PutAt(2@, 1543.0636, -1348.7539, 352.0)
            0247: load_model #NURGRL3
            repeat
            wait 0
            until 0248:   model #NURGRL3 available
            0503: create_actor_on_rope_with_pedtype 4 model #NURGRL3 at 1543.0636 -1348.7539 349.3356 handle_as 4@
            0249: release_model #NURGRL3
            015F: set_camera_position 1539.851 -1349.6829 335.3961 rotation 0.0 0.0 0.0
            0160: set_camera_point_at 1543.0636 -1348.7539 349.3356 mode 2
            wait 2000 ms
            
            Car.PutAt(2@, 1543.0636, -1348.7539, 352.0)
            //0460: set_camera_transverse_delay 0.0 time 2000
            015F: set_camera_position 1537.7487 -1341.4399 340.0 rotation 0.0 0.0 0.0
            0160: set_camera_point_at 1544.0067 -1351.9817 329.4764 mode 2
            wait 2000 ms
            
            Car.PutAt(2@, 1543.0636, -1348.7539, 352.0)
            04ED: load_animation "swat"
            repeat
            wait 0
            until 04EE:   animation "swat" loaded 
            
            0605: actor 4@ perform_animation_sequence "swt_breach_01" IFP_file "swat" 10.0 loop 0 0 0 0 time -1
            
            wait 2000 ms
            04EF: release_animation "swat"  
            
            Car.PutAt(2@, 1543.0636, -1348.7539, 352.0)
            009B: destroy_actor 4@
            
            04ED: load_animation "heist9"
            repeat
            wait 0
            until 04EE:   animation "heist9" loaded 
            
            0362: remove_actor $PLAYER_ACTOR from_car_and_place_at 1544.2651 -1353.4175 329.4741 
            0173: set_actor $PLAYER_ACTOR Z_angle_to 182.6516 
            0605: actor $PLAYER_ACTOR perform_animation_sequence "swt_wllshoot_out_R" IFP_file "swat" 10.0 loop 0 0 0 0 time -1
            0373: set_camera_directly_behind_player
            02EB: restore_camera_with_jumpcut 
            01B4: set_player $PLAYER_CHAR can_move 1 
            04EF: release_animation "swat"  
            
            // Wanted level of the mission
            010D: set_player $PLAYER_CHAR wanted_level_to 0 
            01F0: set_max_wanted_level_to 0 
            
            018A: 3@ = create_checkpoint_at 1548.5665 -1363.7417 327.0
            08FB: set_checkpoint 3@ type_to 2   // 2: red
            
            // The building ceiling enemy
            {0AB1: @CreateActor 8 label @Defection_ActorsEnemies model #lvemt1 position 1548.8909 -1367.2094 326.2109 angle 0.0 pedtype 24 offset 0 ret 12@
            0AB1: @SetActorAsEnemy 4 label @Defection_ActorsEnemies offset 0 weapon 28 difficulty_id 0@ ret 12@}
            // The entrance markers
            08E7: disable_entrance_markers 1
            0AB1: @CreateMarker 8 label @Defection_Markers offset 4 type ENTRANCE_MARKER_TYPE target 0 position 1548.5665 -1363.7417 327.0 color 14 ret 12@
            0AB1: @StoreInt 3 label @Defection_StateVariables value 1 offset 1 // Entrance Marker created flag
            
            10@ = 2
        end
    end
    
    //-----------SKIP TRIP---------------
    if or
    10@ == 0
    10@ == 1
    then
        if Actor.Dead(11@)
        then jump @Defection_Fail
        end
                
        if 00E1:   player 0 pressed_key 11  // Key Y
        then
            fade 0 500
            wait 500
            if 00DB:   actor $PLAYER_ACTOR in_car 2@
            then 0362: remove_actor $PLAYER_ACTOR from_car_and_place_at 1544.3235 -1356.3276 329.4691
            else 0AB1: @TeleportTo 5 pos 1544.3235 -1356.3276 329.4691 angle 180.0 interior 0
            end
            0164: disable_marker 3@
            00A6: destroy_car 2@
            0247: load_model #MAVERICK
            038B: load_requested_models
            repeat
            wait 0 ms
            until 0248:   model #MAVERICK available 
            0AB1: @CreateVehicle 9 label @Defection_Vehicles model #MAVERICK position 1543.0636 -1348.7539 329.4691 angle 270.0 primary_color 0 secondary_color 54 offset 0 ret 2@
            0249: release_model #MAVERICK
            if 10@ == 1
            then
                0AB1: @TeleportTo 5 pos 1544.3235 -1356.3276 329.4691 angle 180.0 interior 0
                06D6: disable_racing_checkpoint 4@
                0164: disable_marker 3@
                00BE: text_clear_all 
            end
            wait 450
            fade 1 1000
            02EB: restore_camera_with_jumpcut
            01B4: set_player $PLAYER_CHAR can_move 1
            03E6: remove_text_box 
            0AB1: @RemoveActors 3 label @Defection_ActorsAllies min_index 0 max_index 4
            
            // The building ceiling enemy
            {0AB1: @CreateActor 8 label @Defection_ActorsEnemies model #lvemt1 position 1548.8909 -1367.2094 326.2109 angle 0.0 pedtype 24 offset 0 ret 12@
            0AB1: @SetActorAsEnemy 4 label @Defection_ActorsEnemies offset 0 weapon 28 difficulty_id 0@ ret 12@}
            // The entrance markers
            08E7: disable_entrance_markers 1
            0AB1: @CreateMarker 8 label @Defection_Markers offset 4 type ENTRANCE_MARKER_TYPE target 0 position 1548.5665 -1363.7417 327.0 color 14 ret 12@
            0AB1: @StoreInt 3 label @Defection_StateVariables value 1 offset 1 // Entrance Marker created flag
            
            10@ = 2
        else
            if and
            0029:   33@ >= 60000
            88FE: not text_box_displayed
            // 0039: $M1_1_ATTEMPTS == 1
            then
                03E5: show_text_box 'MSKIP' // You can ~b~skip~w~ the Intro by pressing ~k~~CONVERSATION_YES~.
                33@ = 0
            end
        end
    end
    
    if 10@ == 2 // Go to enter building point
    then
        if 00FE: actor $PLAYER_ACTOR 0 near_point_3d 1548.5665 -1363.7417 326.2183 radius 1.0 1.0 1.0
        then
            0164: disable_marker 3@
            10@ = 3
        end
    end
    
    if 10@ == 3 // Mission main loop
    then
        if gosub @Defection_MainLoop
        then jump @Defection_Passed
        end
        
        // Failure mission condition
        0AB1: @GetInt 2 label @Defection_StateVariables offset 2 ret_stat 15@ // General stat
        if 15@ == 99
        then jump @Defection_Fail
        end
        
        if 0ADC:   test_cheat "OBJECT"
        then
            0AB1: @GetData 2 label @Defection_Objects offset 0 ret_object 12@ // Internal hub
            0108: destroy_object 12@
            wait 500
            0AB1: @CreateObject 7 label @Defection_Objects model #CJ_SLOT_PROPg position 349.1 189.345 1018.98 angle 90.0 offset 0 ret_object 12@
        end
    end
until false
return

{======================================================================
======================================================================}
:Defection_MainLoop
// Return false by default
059A:  return_false

// Enter/exit areas
0AB1: @Defection_CheckDoors 0
        
// Objectives
0AB1: @GetObjectivesStatus 1 label @Defection_Objectives ret obj1_status 11@ obj2_status 12@ obj3_status 13@ obj4_status 14@ obj5_status 15@
if not 0@ == 1 // not 'Agent' difficulty    
then
    if 11@ == 0 // Objective 1 incomplete
    then 0AB1: @Defection_Objective1 0 // Disable internal security hub
    end
    
    if 12@ == 0 // Objective 2 incomplete
    then 0AB1: @Defection_Objective2 0 // Obtain keycode necklace
    end
            
    if and
    0@ == 2  // Special Agent
    13@ == 0 // Objective 3 incomplete
    then 0AB1: @Defection_Objective4 0 // Disable external comms hub            
    end
            
    if and
    0@ == 3  // Perfect Agent
    13@ == 0 // Objective 3 incomplete
    then 0AB1: @Defection_Objective3 0 // Download project files
    end
            
    if and
    not 0@ == 3 // Perfect Agent
    not 14@ == 0 // Objective 4 incomplete
    then 0AB1: @Defection_Objective4 0 // Disable external comms hub
    end            
end
        
// General Machine
0AB1: @Defection_General 0

// Passed mission condition
if 00FE:   actor $PLAYER_ACTOR 0 near_point_3d 357.8952 217.2352 1008.3828 radius 1.0 1.0 1.0
then
    if 0@ == 1  // Agent
    then 15@ = 1
    end
    
    if and
    0@ == 2  // Special Agent
    11@ == 1
    12@ == 1
    13@ == 1
    then 15@ = 1
    end
            
    if and
    0@ == 3  // Perfect Agent
    11@ == 1
    12@ == 1
    13@ == 1
    14@ == 1
    then 15@ = 1
    end
    
    if 15@ == 0
    then 00BC: show_text_highpriority GXT 'MDRLCK' time 1000 flag 1  // The door is locked.
    end
    
    if 15@ == 1
    then 0485:  return_true
    end
end
return

{======================================================================
======================================================================}
:Defection_General
0AB1: @GetInt 2 label @Defection_StateVariables offset 2 ret_stat 10@ // General stat

0AB1: @StoreInt 3 label @Defection_StateVariables value 10@ offset 2 // General stat
0AB2: ret 0

{======================================================================
OBJECTIVE 1 - Disable internal security hub
======================================================================}
:Defection_Objective1
0AB1: @GetInt 2 label @Defection_StateVariables offset 3 ret_stat 10@ // Objective 1 stat (offset 3)

if 10@ == 0
then
    if 00FE: actor $PLAYER_ACTOR 0 near_point_3d 377.8236 163.261 1025.7891 radius 3.0 3.0 3.0
    then
        00BC: show_text_highpriority GXT 'M11_013' time 4000 flag 1  // (Carrington) We're getting a positive reading - the internal comms hub is nearby.
        0AB1: @GetData 2 label @Defection_Objects offset 0 ret_object 11@ // Internal hub
        0AB1: @CreateMarker 4 label @Defection_Markers offset 0 type MARKER_ABOVE_OBJECT_TYPE target 11@ ret_marker 3@
        Marker.SetColor(3@,0) // red
        10@ = 1
        32@=0
    end
end
if 10@ == 1
then
    if 0029:   32@ >= 4000 
    then
        0AB1: @JoannaResponse 0 // (Joanna) Received.
        10@ = 2
    end
end
if 10@ == 2
then
    if 00FE: actor $PLAYER_ACTOR 0 near_point_3d 349.8629 189.2954 1019.9844 radius 1.0 1.0 1.0       
    then
        if 00E1:   player 0 pressed_key 15 // F/Enter
        then
            03E6: remove_text_box
            0687: clear_actor $PLAYER_ACTOR task
    
            04ED: load_animation "bd_fire" 
            repeat
            wait 0 ms
            until 04EE: animation "bd_fire" loaded
            0605: actor $PLAYER_ACTOR perform_animation_sequence "wash_up" IFP_file "bd_fire" 10.0 loop 0 0 0 0 time 1500
            0173: set_actor $PLAYER_ACTOR Z_angle_to 90.0   
    
            wait 1000 ms
            04EF: release_animation "bd_fire"  
            00BC: show_text_highpriority GXT 'M11_040' time 4000 flag 1  // EMC Mine placed correctly.
            
            0AB1: @GetData 2 label @Defection_Markers offset 0 ret_marker 3@
            Marker.Disable(3@) // Internal hub
            
            00BA: show_text_styled GXT 'MOBJ1' time 2000 style 2      //Objective 1: Completed
            0AB1: @StoreInt 3 label @Defection_Objectives value 1 offset 0 // Objective 1 flag checked
            10@ = 3
        else
            if and
            0029:   33@ >= 10000
            88FE: not text_box_displayed
            then
                03E5: show_text_box 'M11_052' // Press ~s~ENTER~w~ to use ECM mines.
                33@=0
            end
        end
    end
end

0AB1: @StoreInt 3 label @Defection_StateVariables value 10@ offset 3 // Objective 1 stat (offset 3)
0AB2: ret 0

{======================================================================
OBJECTIVE 2 - Obtain keycode necklace
======================================================================}
:Defection_Objective2
0AB1: @GetInt 2 label @Defection_StateVariables offset 4 ret_stat 10@ // Objective 2 stat (offset 4)

0AB1: @StoreInt 3 label @Defection_StateVariables value 10@ offset 4 // Objective 2 stat (offset 4)
0AB2: ret 0

{======================================================================
OBJECTIVE 3 - Download project files
======================================================================}
:Defection_Objective3
0AB1: @GetInt 2 label @Defection_StateVariables offset 5 ret_stat 10@ // Objective 3 stat (offset 5)

0AB1: @StoreInt 3 label @Defection_StateVariables value 10@ offset 5 // Objective 3 stat (offset 5)
0AB2: ret 0

{======================================================================
OBJECTIVE 4 - Disable external comms hub
======================================================================}
:Defection_Objective4
0AB1: @GetInt 2 label @Defection_StateVariables offset 6 ret_stat 10@ // Objective 4 stat (offset 6)

0AB1: @StoreInt 3 label @Defection_StateVariables value 10@ offset 6 // Objective 4 stat (offset 6)
0AB2: ret 0

{======================================================================
======================================================================}
:Defection_Objects
hex
00000000 00000000 00000000 00000000 00000000 00000000
end

{======================================================================
======================================================================}
:Defection_Pickups
hex
00000000 00000000
end

{======================================================================
======================================================================}
:Defection_Vehicles
hex
00000000
end

{======================================================================
======================================================================}
:Defection_ActorsAllies
hex
00000000 00000000 00000000 00000000 00000000
end

{======================================================================
======================================================================}
:Defection_ActorsEnemies
hex
00000000 00000000 00000000 00000000 00000000
end

{======================================================================
======================================================================}
:Defection_Markers
hex
00000000 00000000 00000000
end

{======================================================================
======================================================================}
:Defection_Objectives
hex
00 00 00 00 00
end

{======================================================================
======================================================================}
:Defection_StateVariables
hex
00 // Check doors (Area status)
00 // Entrance marker created flag
00 // General stat
00 // Objective 1 stat (offset 3)
00 // Objective 2 stat (offset 4)
00 // Objective 3 stat (offset 5)
00 // Objective 4 stat (offset 6)
end

{======================================================================
======================================================================}
:Defection_IntroDialogue
hex
    "M11_001" 0 "3000" // (Joanna) Why the big hurry?
    "M11_002" 0 "5000" // (Carrington) If Dr Caroll is not extracted tonight, dataDyne will put him through mind conditioning,
    "M11_003" 0 "5000" // (Carrington) and we'll lose our best chance of finding out what dataDyne are up to.
    "M11_004" 0 "4000" // (Joanna) Are they all expendable?
    "M11_005" 0 "6000" // (Carrington) Don't joke! You have to be careful, Joanna. Code keys will only operate while the owner is alive…
    "M11_006" 0 "6000" // (Carrington) if you kill them, the key is useless. Armed guards are a different matter of course.
    "M11_007" 0 "4000" // (Joanna) What's the target location?
    "M11_008" 0 "4000" // (Carrington) Work your way down the building to the ground level.
    "M11_009" 0 "5000" // (Carrington) Dr Caroll will be in a research lab somewhere in the underground facility.
    "M11_010" 0 "4000" // (Joanna) How will I recognize him?
    "M11_011" 0 "5000" // (Carrington) We don't have an image record, and we can't find any official files. All we have is the name.
    "M11_012" 0 "4000" // (Carrington) Good luck, Perfect Dark.
end

{==============================================================
===============================================================}
:Defection_CheckDoors // entries: none
0AB1: @GetInt 2 label @Defection_StateVariables offset 0 ret_stat 10@ // Area status
0AB1: @GetInt 2 label @Defection_StateVariables offset 1 ret_stat 11@ // Entrance Marker created flag

if 10@ == 0 // Outside building (rooftop)
then
    if 00FE:   actor $PLAYER_ACTOR 0 near_point_3d 1548.5665 -1363.7417 327.0 radius 1.0 1.0 1.0
    then
        if 11@ == 1 // Entrance Marker created flag
        then
            10@ = 1
            fade 0 750
            wait 750 ms
            0AB1: @TeleportTo 5 pos 354.6288 173.218 1025.7964 angle 180.0 interior 3
            0AB1: @DestroyMarkers 3 label @Defection_Markers min_index 1 max_index 1
            // Elevator
            0AB1: @CreateMarker 8 label @Defection_Markers offset 8 type ENTRANCE_MARKER_TYPE target 0 position 357.8952 217.2352 1010.0 color 14 ret 12@
            11@ = 0
        end
    else
        if 11@ == 0 // Entrance Marker created flag
        then
            // Building door entrance
            0AB1: @CreateMarker 8 label @Defection_Markers offset 4 type ENTRANCE_MARKER_TYPE target 0 position 1548.5665 -1363.7417 327.0 color 14 ret 12@
            11@ = 1
            fade 1 1000
        end
    end
end

if 10@ == 1 // Inside building
then
    if 00FE:   actor $PLAYER_ACTOR 0 near_point_3d 354.6515 175.3574 1025.7964 radius 1.0 1.0 1.0
    then
        if 11@ == 1 // Entrance Marker created flag
        then
            10@ = 0
            fade 0 750
            wait 750 ms
            0AB1: @TeleportTo 5 pos 1548.1379 -1366.4137 326.2109 angle 90.0 interior 0
            0AB1: @DestroyMarkers 3 label @Defection_Markers min_index 1 max_index 2
            11@ = 0
        end
    else
        if 11@ == 0 // Entrance Marker created flag
        then
            // Building door entrance
            0AB1: @CreateMarker 8 label @Defection_Markers offset 4 type ENTRANCE_MARKER_TYPE target 0 position 354.679 175.3582 1027.0 color 14 ret 12@
            //0A40: BuildingDoor = create_entrance_marker_at 354.679 175.3582 1027.0 color 14
            11@ = 1
            fade 1 1000
        end
    end
end

0AB1: @StoreInt 3 label @Defection_StateVariables value 10@ offset 0 // Area status
0AB1: @StoreInt 3 label @Defection_StateVariables value 11@ offset 1 // Entrance Marker created flag
0AB2: ret 0

{======================================================================
======================================================================}
:Defection_Passed
0394: play_music 1
Player.Money($PLAYER_CHAR) += 10000
01E3: show_text_1number_styled GXT 'M_PASS' number 10000 time 5000 style 1 // MISSION PASSED!~n~~w~$~1~
return

:Defection_Fail
00BA: show_text_styled GXT 'M_FAIL' time 5000 style 1 // ~r~MISSION FAILED!
return

:Defection_CleanUp
$ONMISSION = 0
08E7: disable_entrance_markers 0
0164: disable_marker 3@
01C3: remove_references_to_car 2@  // Like turning a car into any random car
0AB1: @DestroyMarkers 3 label @Defection_Markers min_index 0 max_index 2
0AB1: @RemoveActors 3 label @Defection_ActorsEnemies min_index 0 max_index 4
0AB1: @RemoveActors 3 label @Defection_ActorsAllies min_index 0 max_index 4
0AB1: @RemoveObjects 3 label @Defection_Objects min_index 0 max_index 5
0AB1: @DestroyPickups 3 label @Defection_Pickups min_index 0 max_index 1
0AB1: @DestroyPickups 3 label @Defection_Pickups min_index 0 max_index 1
return
