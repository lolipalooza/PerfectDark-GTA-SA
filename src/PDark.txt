{======================================================================
======================================================================}
{$CLEO .cs}
thread 'MainTriggerer'

11@ = 1046.3639
12@ = 1015.7791
13@ = 11.0
10@ = Marker.CreateIconAndSphere(42, 11@, 12@, 13@)

repeat
wait 0
    if $ONMISSION == 0
    then
        gosub @CIMissionsSniffer
        
        if 0ADC:   test_cheat "PD"
        then
            03CB: set_rendering_origin_at 1050.1146 1021.1041 11.0
            04E4: refresh_game_renderer_at 1050.1146 1021.1041
            00A1: put_actor $PLAYER_ACTOR at 1050.1146 1021.1041 10.0
            0173: set_actor $PLAYER_ACTOR Z_angle_to 145.0
            0373: set_camera_directly_behind_player
        end
        
        if 0ADC:   test_cheat "TEST"
        then 0A92: create_custom_thread "test.s"
        end
    end
until false

marker.Disable(10@)
0A93: end_custom_thread


{======================================================================
======================================================================}
{$I common/common.txt}

{==============================================================
MISSION SNIFFER
===============================================================}
:CIMissionsSniffer
if 20@ == 0
then
    if and
    0102:   actor $PLAYER_ACTOR in_sphere 11@ 12@ 13@ radius 1.0 1.0 2.0 sphere 0 stopped_on_foot
    03EE:   player $PLAYER_CHAR controllable
    then
        0164: disable_marker 10@
        01B4: set_player $PLAYER_CHAR can_move 0
        0512:  show_permanent_text_box 'MHLP01' // ~b~~k~~GO_FORWARD~ / ~k~~GO_BACK~~w~: Change option ~b~~k~~PED_SPRINT~~w~: Accept
        08D4: 21@ = create_panel_with_title 'PD' position 29.0 145.0 width 300.0 columns 1 interactive 1 background 1 alignment 1 // Perfect Dark
        08DB:  set_panel 21@ column 0 header 'MTBL02' data 'CI' 'MTBL_1' 'MTBL_2' 'MTBL_3' 'MTBL_4' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' // Perfect Menu
        20@ = 1
    end
end
if 20@ == 1
then
    if 00E1:    player 0 pressed_key 16 // Sprint (SPACE)
    then 20@ = 2
    end
    if 00E1: player 0 pressed_key 15 // F/Enter
    then
        08DA: remove_panel 21@
        03E6: remove text box
        20@ = 10
        01B4: set_player $PLAYER_CHAR can_move 1
    end
end
if 20@ == 2
then
    if 80E1:    not player 0 pressed_key 16 // Sprint (SPACE)
    then
        08D7: 22@ = panel 21@ active_row
        08DA: remove_panel 21@
        03E6: remove text box
        
        if not 22@ == 1 // Any other option than Solo Missions
        then
            00BC: show_text_highpriority GXT 'M_ERR2' time 2000 flag 1  // This option is not ~b~suported~w~ yet...
            wait 2000 ms
            20@ = 0
        else 20@ = 3 // Solo Missions
        end
    end
end
if 20@ == 3
then
    0512:  show_permanent_text_box 'MHLP01' // ~b~~k~~GO_FORWARD~ / ~k~~GO_BACK~~w~: Change option ~b~~k~~PED_SPRINT~~w~: Accept
    08D4: 21@ = create_panel_with_title 'PD' position 29.0 145.0 width 300.0 columns 1 interactive 1 background 1 alignment 1 // Perfect Dark
    08DB:  set_panel 21@ column 0 header 'MTBL03' data 'M1-1' 'M1-2' 'M1-3' 'M2-1' 'M3-1' 'M3-2' 'M4-1' 'M4-2' 'M4-3' 'M5-1' 'M5-2' 'M5-3' 'M6-1' 'M6-2' 'M7-1' 'M8-1' 'M9-1'// Mission Select
    20@ = 4
end
if 20@ == 4
then
    if 00E1:    player 0 pressed_key 16 // Sprint (SPACE)
    then 20@ = 5
    end
    if 00E1: player 0 pressed_key 15 // F/Enter
    then
        repeat
        wait 0 ms
        until 80E1: not player 0 pressed_key 15
        08DA: remove_panel 21@
        03E6: remove text box
        20@ = 0
    end
end
if 20@ == 5
then
    if 80E1:    not player 0 pressed_key 16 // Sprint (SPACE)
    then
        08D7: 22@ = panel 21@ active_row
        08DA: remove_panel 21@
        03E6: remove text box
        
        0AB1: @GetMissionGxt 1 mission_id 22@ ret gxt 24@ 25@
        0AD3: 26@v = format "cleo/%s.s" 24@s
        if 0AAB:   file_exists 26@v
        then 20@ = 6 // Select Difficulty
        else
            00BC: show_text_highpriority GXT 'M_ERR1' time 2000 flag 1  // The Mission you ~y~selected~w~ is not ready yet...
            //0AD0: show_formatted_text_lowpriority "Mission selected: %s, file: %s" time 4000 24@s 26@v
            wait 2000 ms
            20@ = 3 // Mission Select
        end
    end
end
if 20@ == 6
then
    0512:  show_permanent_text_box 'MHLP01' // ~b~~k~~GO_FORWARD~ / ~k~~GO_BACK~~w~: Change option ~b~~k~~PED_SPRINT~~w~: Accept
    08D4: 21@ = create_panel_with_title 'MTBL01' position 29.0 145.0 width 300.0 columns 1 interactive 1 background 1 alignment 1 // Enter Mission
    08DB:  set_panel 21@ column 0 header 24@s data 'MDIF1' 'MDIF2' 'MDIF3' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY'
    20@ = 7
end
if 20@ == 7
then
    if 00E1:    player 0 pressed_key 16 // Sprint (SPACE)
    then 20@ = 8
    end
    if 00E1: player 0 pressed_key 15
    then
        repeat
        wait 0 ms
        until 80E1: not player 0 pressed_key 15
            
        08DA: remove_panel 21@
        03E6: remove text box
        20@ = 3 // Mission Select
    end
end
if 20@ == 8
then
    if 80E1:    not player 0 pressed_key 16 // Sprint (SPACE)
    then
        08D7: 22@ = panel 21@ active_row
        08DA: remove_panel 21@
        03E6: remove text box
        20@ = 9
    end
end
if 20@ == 9
then
    $ONMISSION = 1
    00BA: show_text_styled GXT 24@s time 1000 style 2
    fade 0 500
    wait 500
    0AD3: 26@v = format "%s.s" 24@s
    0A92: create_custom_thread 26@v difficulty 22@
    20@ = 10
end
if 20@ == 10
then
    if 80FF: not actor $PLAYER_ACTOR sphere 0 in_sphere 11@ 12@ 13@ radius 1.5 1.5 2.0 on_foot
    then
        20@ = 0
        10@ = Marker.CreateIconAndSphere(42, 11@, 12@, 13@)
    end
end
return

{======================================================================
======================================================================}
:MissionsGxt
hex
"M1-1" "M1-2" "M1-3" "M2-1" "M3-1" "M3-2"
"M4-1" "M4-2" "M4-3" "M5-1" "M5-2" "M5-3"
"M6-1" "M6-2" "M7-1" "M8-1" "M9-1"
end

:DifficultyData
hex
end

{======================================================================
======================================================================}
:GetMissionGxt // mission_id 0@
0012: 0@ *= 4
0AB1: @GetData 2 label @MissionsGxt offset 0@ ret 30@
0AB2: ret 2 30@ 31@
